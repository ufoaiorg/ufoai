// ==================
// TRANSFER MENU
// ==================
component cmp_t_storage extends panel {
	{
		size			"260 20"
		bgcolor			"0.56 0.81 0.76 0.1"
		onClick {
			cmd "ui_trans_add <node:this.id@string> 1;"
		}
		onRClick {
			cmd "ui_trans_add <node:this.id@string> -1;"
		}
	}

	data id {
	}

	string name {
		pos				"0 0"
		size			"285 20"
		color			"0.56 0.81 0.76 1"
		ghost			true
		contentalign	ALIGN_CL
	}
	string src_store {
		pos				"250 0"
		size			"100 20"
		color			"0.56 0.81 0.76 1"
		contentalign	ALIGN_CR
		ghost			true
	}
	spinner_green spinner {
		tooltip				"_Add/Remove item to transfer cargo, hold SHIFT for speed increase."
		pos					"352 0"
		size			"   20 20"
		shiftincreasefactor	100.0
		onChange {
			cmd "ui_trans_add <node:parent.id@string> <lastdiff>;"
		}
	}
	string dst_store {
		pos				"375 0"
		size			"100 20"
		color			"0.56 0.81 0.76 1"
		contentalign	ALIGN_CR
		ghost			true
	}
}

component cmp_t_cargo extends panel {
	{
		size		"368 20"
		onClick {
			cmd "ui_trans_add <node:this.id@string> -1;"
		}
	}

	data id {
	}
	string name {
		pos				"0 0"
		size			"270 20"
		color			"0.56 0.81 0.76 1"
		ghost			true
		contentalign	ALIGN_CL
	}
	string amount {
		pos				"248 0"
		size			"100 20"
		color			"0.56 0.81 0.76 1"
		contentalign	ALIGN_CR
		ghost			true
	}
}

component cmp_t_capacity extends panel {
	{
		size		"285 20"
	}
	string name {
		pos			"0 0"
		size		"150 20"
		font			"f_verysmall"
		contentalign	ALIGN_CL
	}
	string free {
		pos			"150 0"
		size		"60 20"
		font			"f_verysmall"
		contentalign	ALIGN_CR
	}
	string separator {
		string		"/"
		pos			"210 0"
		size		"20 20"
		font			"f_verysmall"
		contentalign	ALIGN_CC
	}
	string max {
		pos			"230 0"
		size		"60 20"
		font			"f_verysmall"
		contentalign	ALIGN_CR
	}
}

window transfer
{
	/** @todo Remove me */
	{ } // empty properties to fix parsing problem with "image" token (behaviour or property?)


	// Base name and container panel
	BaseContainerPanel base_container
	{
		// Transfer panel
		panel mainBody
		{
			{ size "715 534" }
			// Title bar
			panel header
			{
				{}
				string title
				{
					string			"_Transfer"
				}
			}
			// ==================
			// tab
			// ==================

			panel tab_background {
				pos		"10 30"
				size	"700 50"
			}

			tab trans_navigation {
				{
					pos		"1 30"
					size	"714 39"
					cvar	*cvar:mn_itemtype
					onChange	{
						cmd "ui_trans_fill <cvar:mn_itemtype>;"
						if ( *cvar:mn_itemtype eq "item" ) {
							*cvar:mn_itemtype_name = "_Equipment"
						} elif ( *cvar:mn_itemtype eq "employee" ) {
							*cvar:mn_itemtype_name = "_Employees"
						} elif ( *cvar:mn_itemtype eq "alien" ) {
							*cvar:mn_itemtype_name = "_Aliens"
						} elif ( *cvar:mn_itemtype eq "aircraft" ) {
							*cvar:mn_itemtype_name = "_Aircraft"
						}
					}
				}
				option item {
					label	"_Equipment"
					value	"item"
					icon	"icons/tab_equipment"
				}
				option employee {
					label	"_Employees"
					value	"employee"
					icon	"icons/tab_employees"
				}
				option alien {
					label	"_Aliens"
					value	"alien"
					icon	"icons/tab_aliens"
				}
				option aircraft {
					label	"_Aircraft"
					value	"aircraft"
					icon	"icons/tab_aircraft"
				}
			}
			// ==================
			// lists
			// ==================

			panel itemlist {
				{
					pos			"12 80"
					size		"552 450"
				}

				rows lines {
					pos			"16 16"
					size		"520 40"
					lineheight	20
				}

				string title_name {
					string		*cvar:mn_itemtype_name
					pos			"25 0"
					size		"250 20"
					contentalign	ALIGN_CL
					font		"f_small_bold"
				}

				string title_src_store {
					string		"_From"
					pos			"275 0"
					size		"100 20"
					contentalign	ALIGN_CR
					font		"f_small_bold"
				}
				string dst_store {
					string		"_To"
					pos			"400 0"
					size		"100 20"
					contentalign	ALIGN_CR
					font		"f_small_bold"
				}

				panel translist {
					pos				"25 30"
					size			"475 416"
					layout			LAYOUT_TOP_DOWN_FLOW
					layoutMargin	2
					wheelscrollable	true

					onViewChange {
						*node:parent.translist_scroll@fullsize = <fullsize>
						*node:parent.translist_scroll@current = <viewpos>
						*node:parent.translist_scroll@viewsize = <viewsize>
					}
				}
				vscrollbar translist_scroll {
					image		"ui/scrollbar_v"
					pos			"510 30"
					height		"416"
					hidewhenunused	true
					onChange	{
						*node:parent.translist@viewpos = <current>
					}
				}

				confunc ui_translist_clear {
					call *node:parent.translist@removeAllchild
				}

				confunc ui_translist_add {
					call *node:parent.translist@createChild("trans_<1>", "cmp_t_storage")

					*node:parent.translist.trans_<1>.id@string = <1>
					*node:parent.translist.trans_<1>.name@string = <2>
					*node:parent.translist.trans_<1>.src_store@string = <3>
					if ( *node:parent.translist.trans_<1>.src_store@string eq "-1" ) {
						*node:parent.translist.trans_<1>.src_store@invis = true
					} else {
						*node:parent.translist.trans_<1>.src_store@invis = false
					}
					*node:parent.translist.trans_<1>.dst_store@string = <4>
					if ( *node:parent.translist.trans_<1>.dst_store@string eq "-1" ) {
						*node:parent.translist.trans_<1>.dst_store@invis = true
					} else {
						*node:parent.translist.trans_<1>.dst_store@invis = false
					}
					*node:parent.translist.trans_<1>.spinner@min = <5>
					*node:parent.translist.trans_<1>.spinner@current = <6>
					*node:parent.translist.trans_<1>.spinner@max = <7>
					if ( *node:parent.translist.trans_<1>.spinner@max == -1 ) {
						*node:parent.translist.trans_<1>.spinner@invis = true
					} else {
						*node:parent.translist.trans_<1>.spinner@invis = false
					}
				}
			}
		}
	}



	/**
	 * @brief Cargolist
	 */
	panel transfer_cargolist {
		{
			pos		"20 570"
			size	"420 190"
		}

/*		rows lines_transfer_cargolist {
			pos			"16 16"
			size		"388 145"
			lineheight	20
		}
*/
		string title_name {
			string			"_Cargo"
			pos				"26 16"
			size			"268 20"
			contentalign	ALIGN_CL
			font			"f_small_bold"
			color			"0.7 0.7 0.7 1"
		}

		string title_amount {
			string			"_Amount"
			pos				"275 16"
			size			"100 20"
			contentalign	ALIGN_CR
			font			"f_small_bold"
			color			"0.7 0.7 0.7 1"
		}

		panel cargolist {
			pos				"26 36"
			size			"358 135"
			layout			LAYOUT_TOP_DOWN_FLOW
			layoutMargin	2
			wheelscrollable	true

			onViewChange {
				*node:parent.cargolist_scroll@fullsize = <fullsize>
				*node:parent.cargolist_scroll@current = <viewpos>
				*node:parent.cargolist_scroll@viewsize = <viewsize>
			}
		}
		vscrollbar cargolist_scroll {
			image			"ui/scrollbar_v"
			pos				"384 36"
			height			"135"
			hidewhenunused	true
			onChange	{
				*node:parent.cargolist@viewpos = <current>
			}
		}

		confunc ui_cargolist_clear {
			call *node:parent.cargolist@removeAllchild
		}

		confunc ui_cargolist_add {
			call *node:parent.cargolist@createChild("cargo_<1>", "cmp_t_cargo")

			*node:parent.cargolist.cargo_<1>.id@string = <1>
			*node:parent.cargolist.cargo_<1>.name@string = <2>
			*node:parent.cargolist.cargo_<1>.amount@string = <3>
		}
	}

	// ==================
	// change target base
	// ==================
	panel infopanel {
		{
			pos			"726 160"
			size		"295 260"
		}

		string s_current_base {
			string			"_Destination base"
			pos				"0 22"
			size			"280 20"
			contentalign	ALIGN_UC
			color			"0.7 0.7 0.7 1"
			font			"f_small_bold"
		}

		selectbox destination_base 	{
			cvar		*cvar:mn_trans_base_id
			pos			"5 45"
			size		"288 20"
			dataid		OPTION_BASELIST
			onChange {
				cmd "trans_selectbase <cvar:mn_trans_base_id>;"
				cmd "ui_trans_fill <cvar:mn_itemtype>;"
			}
		}

		string s_cap_building {
			string			"_Building"
			pos				"5 90"
			size			"150 20"
			contentalign	ALIGN_UL
			color			"0.7 0.7 0.7 1"
			font			"f_verysmall"
		}
		string s_cap_freespace {
			string			"_free"
			pos				"145 90"
			size			"70 20"
			contentalign	ALIGN_UR
			color			"0.7 0.7 0.7 1"
			font			"f_verysmall"
		}
		string s_cap_per {
			string			"/"
			pos				"215 90"
			size			"20 20"
			contentalign	ALIGN_UC
			color			"0.7 0.7 0.7 1"
			font			"f_verysmall"
		}
		string s_cap_allspace {
			string			"_all space"
			pos				"225 90"
			size			"70 20"
			contentalign	ALIGN_UR
			color			"0.7 0.7 0.7 1"
			font			"f_verysmall"
		}
		panel capacities {
			pos				"5 115"
			size			"290 125"
			color			"0.56 0.81 0.76 1"
			layout			LAYOUT_TOP_DOWN_FLOW
		}

		confunc ui_t_capacities_clear {
			call *node:parent.capacities@removeAllchild
			cmd "trans_disable enable;"
		}
		confunc ui_t_capacities_add {
			call *node:parent.capacities@createChild("cap_<1>", "cmp_t_capacity")

			*node:parent.capacities.cap_<1>.name@string = <2>
			*node:parent.capacities.cap_<1>.free@string = ( <4> - <3> )
			*node:parent.capacities.cap_<1>.max@string = <4>

			if ( <3> > <4>) {
				*node:parent.capacities.cap_<1>.name@color = "1 0 0 1"
				*node:parent.capacities.cap_<1>.free@color = "1 0 0 1"
				*node:parent.capacities.cap_<1>.max@color = "1 0 0 1"
				cmd "trans_disable disable \"_Capacity overload\";"
			} else {
				*node:parent.capacities.cap_<1>.name@color = "0.56 0.81 0.76 1"
				*node:parent.capacities.cap_<1>.free@color = "0.56 0.81 0.76 1"
				*node:parent.capacities.cap_<1>.max@color = "0.56 0.81 0.76 1"
			}
		}

	}

	data transfer_denied {
		integer 		0
	}

	confunc trans_disable {
		if ( <1> eq "enable" ) {
			*node:root.transfer_denied@integer = 0
			*node:root.bt_starttrans@color = "0 0 0 0.9"
			*node:root.bt_starttrans@selectcolor = "0 0 0 1"
			*node:root.bt_starttrans@tooltip = "_Start the transfer"
		} elif ( <1> eq "disable" ) {
			*node:root.transfer_denied@integer = 1
			*node:root.bt_starttrans@color = "1 0 0 0.6"
			*node:root.bt_starttrans@selectcolor = "1 0 0 0.6"
			*node:root.bt_starttrans@tooltip = "<2>"
		}
	}

	// ==================
	// buttons
	// ==================
	MainMenuBtn bt_translist
	{
		string		"_List"
		tooltip		"_List active transfers"
		pos			"760 610"
		size		"198 30"
		font		"f_small_bold"
		color		"0 0 0 0.9"
		selectcolor	"0 0 0 1"
		onClick		{ cmd "ui_push popup_transferlist;" }
	}

	MainMenuBtn bt_starttrans
	{
		string		"_Transfer"
		tooltip		"_Start the transfer"
		pos			"760 660"
		size		"198 30"
		font		"f_small_bold"
		color		"0 0 0 0.9"
		selectcolor	"0 0 0 1"
		onClick	{
			if ( *node:root.transfer_denied@integer == 0 ) {
				cmd "trans_start;"
			}
		}
	}

	MainMenuBtn bt_emptycraftstorage
	{
		string		"_Clear"
		tooltip		"_Clear cargo"
		pos			"760 710"
		size		"198 30"
		font		"f_small_bold"
		color		"0 0 0 0.9"
		selectcolor	"0 0 0 1"
		onClick		{ cmd "trans_emptyairstorage;" }
	}

	// ==================
	// init function
	// ==================

	func onWindowOpened
	{
		cmd "trans_init;"
		if ( *cvar:mn_itemtype_name eq "" ) { *cvar:mn_itemtype_name = "_Equipment" }
	}

	func onWindowClosed
	{
		cmd "trans_close;"

		// Call base_init so storage data can be updated
		cmd "base_init;"
	}

	// Back to geoscape
	Geoscape geoscape_button
	{
		onClick			{ cmd "ui_pop; ui_pop;" }
	}

	// Fuzzy screen effect (see assets.ufo)
	fuzzyScreen overlay { }

}

// ===============================
// POPUP TRANSFER BASE SELECT LIST
// ===============================
window popup_transferbaselist extends popupUI2
{
	{
		pos			"256 256"
		size		"512 256"
	}

	string title
	{
		width		"256"
		string		"_Select base to transfer to"
	}

	string info
	{
		string		"_Choose a base to host alien bodies:"
		pos			"26 58"
		size		"450 30"
	}

	optionlist trans_baselist
	{
		cvar		*cvar:tr_base
		dataid		OPTION_BASELIST
		pos			"26 101"
		size		"450 112"
		onChange	{ cmd "trans_baselist_click <cvar:tr_base>;" }
		onRClick	{ cmd "ui_pop; aircraft_return;" }
	}

	MainMenuCancelBtn bt_cancel
	{
		size		"180 30"
		pos			"332 226"
		onClick		{ cmd "ui_pop; aircraft_return;" }
	}

	fuzzyScreen overlay { }

	func onWindowOpened {
		*cvar:tr_base = -1
	}

}

// ===================
// POPUP TRANSFER LIST
// ===================

component trListEntry extends panel
{
	{
		size		"472 150"
		ghost		true
		border		1
		bordercolor	"0.527 0.6 0.21 1"
		ghost		true
	}

	string tr_from {
		string		"_From:"
		pos			"0 0"
		size		"75 20"
		font		"f_small_bold"
		ghost		true
	}
	string tr_srcbase {
		pos			"80 0"
		size		"150 25"
		font		"f_small"
		ghost		true
	}

	string tr_to {
		string		"_To:"
		pos			"235 0"
		size		"75 20"
		font		"f_small_bold"
		ghost		true
	}
	string tr_destbase {
		pos			"315 0"
		size		"150 25"
		font		"f_small"
		ghost		true
	}

	string tr_eta {
		string		"_ETA:"
		pos			"235 25"
		size		"75 20"
		font		"f_small_bold"
		ghost		true
	}
	string tr_arrive {
		pos			"315 25"
		size		"150 25"
		font		"f_small"
		ghost		true
	}

	optiontree tr_cargo {
		{
			cvar		*cvar:none
			pos			"0 50"
			size		"472 100"
			color		"0.56 0.81 0.76 1"
			selectcolor	"0.56 0.81 0.76 1"
			lineHeight	18

			// event mimic missing dynamic nodesize feature
			onViewchange	{
				*node:this@height = ((<fullsize> * <lineHeight>) + (<padding> * 2))
				*node:parent@height = (((<fullsize> * <lineHeight>) + (<padding> * 2)) + <top>)
			}

			// events mimic "ghost-wheel" functionality
			onWheelUp {
				*cvar:wheelpos = (*node:root.tr_list@viewpos - 1)
				if (*cvar:wheelpos < 0) {
					*cvar:wheelpos = 0
				}

				*node:root.tr_list@viewpos = <cvar:wheelpos>
				*node:root.tr_list_scroll@current = <cvar:wheelpos>
				delete *cvar:wheelpos
			}
			onWheelDown {
				*cvar:wheelpos = (*node:root.tr_list@viewpos + 1)
				if (*cvar:wheelpos > (*node:root.tr_list@fullsize - *node:root.tr_list@viewsize)) {
					*cvar:wheelpos = (*node:root.tr_list@fullsize - *node:root.tr_list@viewsize)
				}

				*node:root.tr_list@viewpos = <cvar:wheelpos>
				*node:root.tr_list_scroll@current = <cvar:wheelpos>
				delete *cvar:wheelpos
			}
		}
	}
}

window popup_transferlist extends popupUI2
{
	{
		pos			"256 96"
		size		"512 512"
		color		"0.56 0.81 0.76 1"
		modal		true
	}

	string title
	{
		width		"512"
		string		"_Actual transfers"
	}

	panel tr_list {
		pos				"13 58"
		size			"478 414"
		color			"0.56 0.81 0.76 1"
		layout			LAYOUT_TOP_DOWN_FLOW
		wheelscrollable	true

		onViewChange	{
			*node:parent.tr_list_scroll@fullsize = <fullsize>
			*node:parent.tr_list_scroll@current = <viewpos>
			*node:parent.tr_list_scroll@viewsize = <viewsize>
		}
	}
	vscrollbar tr_list_scroll {
		image		"ui/scrollbar_v"
		pos			"494 58"
		height		"414"
		hidewhenunused	true
		onChange	{
			*node:parent.tr_list@viewpos = <current>
		}
	}

	MainMenuBtn tr_close
	{
		string		"_Close"
		pos			"332 482"
		size		"180 30"
		onClick		{ cmd "ui_pop;" }
	}

	confunc tr_listclear {
		call *node:root.tr_list@removeAllChild
	}

	confunc tr_listaddtransfer {
		call *node:root.tr_list@createChild("trans<1>", "trListEntry")
		*node:root.tr_list.trans<1>.tr_srcbase@string = <2>
		*node:root.tr_list.trans<1>.tr_destbase@string = <3>
		*node:root.tr_list.trans<1>.tr_arrive@string = <4>
	}

	confunc tr_listaddcargo {
		call *node:root.tr_list.trans<1>.<2>@createChild("<3>", "option")
		*node:root.tr_list.trans<1>.<2>.<3>@label = "<4>"
		*node:root.tr_list.trans<1>.<2>.<3>@collapsed = true
	}

	func onWindowOpened
	{
		cmd "trans_list;"
	}

	func onWindowClosed {
		call *node:root.tr_listclear
	}

	// Fuzzy screen effect (see assets.ufo)
	fuzzyScreen overlay { }

}
