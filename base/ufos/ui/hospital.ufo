// ==================
// HOSPITAL MENU
// ==================

component cmpHospitalImplant extends panel {
	{
		size	"210 35"
		bgcolor "0 0 0 0.7"
		border	1
		bordercolor		"1 1 1 0.2"
	}

	string name {
		string	""
		size	"170 17"
		pos		"35 5"
		ghost	true
		font	"f_verysmall"
	}

	image img {
		src		""
		pos		"5 5"
		size	"25 25"
		ghost	true
	}

	bar timeremaining {
		pos		"35 22"
		size	"170 8"
		current	0
		max		0
		disabled	true
		border	1
		bordercolor	"1 1 1 0.2"
		ghost	true
	}
}

window hospital
{
	{
		fill		true
	}

	// Base name and container panel
	BaseContainerPanel base_container {
		// Hospital panel
		panel mainBody {
			// Title bar
			panel header {
				{}
				string title
				{
					string			"_Hospital"
				}
			}

			// ==================
			// employee list
			// ==================

			/**
			 * @param <1> ucn
			 * @param <2> light, medium, serious, healty
			 * @param <3> HP
			 * @param <4> MaxHP
			 * @param <5> Name
			 * @param <6> Rank
			 */
			confunc hospitaladd	{
				call *node:parent.patients@createChild("hos<1>", "HospitalItem")
				if ( <2> eq "light" ) {
					*node:parent.patients.hos<1>.icon@src		= "icons/health_normal"
					*node:parent.patients.hos<1>.icon@tooltip	= "_Slightly wounded"
				} elif ( <2> eq "medium" ) {
					*node:parent.patients.hos<1>.icon@src		= "icons/health_medium"
					*node:parent.patients.hos<1>.icon@tooltip	= "_Medium wounds"
				} elif ( <2> eq "serious" ) {
					*node:parent.patients.hos<1>.icon@src		= "icons/health_critical"
					*node:parent.patients.hos<1>.icon@tooltip	= "_Heavily wounded"
				} else {
					*node:parent.patients.hos<1>.icon@src		= "icons/health_unused"
					*node:parent.patients.hos<1>.icon@tooltip	= "_Healthy"
				}
				*node:parent.patients.hos<1>.health@current = <3>
				*node:parent.patients.hos<1>.health@max = <4>
				*node:parent.patients.hos<1>.name@string = "<5>"
				*node:parent.patients.hos<1>.type@string = "<6>"
				*node:parent.patients.hos<1>.id@integer = <1>
			}

			/**
			 * @param <1> Entry index
			 * @param <2> Body part type (id)
			 * @param <3> Severity of wounds
			 * @param <4> Tooltip
			 */
			confunc hospital_wounds {
				call *node:root.panel.hos<1>.wounds@createChild("wound_<2>", "cmpH<2>")
				if (<3> >= 0.3) {
					*node:root.panel.hos<1>.wounds.wound_<2>@icon = "icons/<2>_wound"
				} else {
					*node:root.panel.hos<1>.wounds.wound_<2>@icon = "icons/<2>_wound_treated"
				}
				*node:root.panel.hos<1>.wounds.wound_<2>@tooltip = "<4>"
			}

			panel patients {
				pos			"0 40"
				size		"690 492"
				layout		LAYOUT_TOP_DOWN_FLOW
				layoutMargin	5
				padding			20
				wheelscrollable	true

				onViewChange	{
					*node:parent.hos_scroll@fullsize = <fullsize>
					*node:parent.hos_scroll@current = <viewpos>
					*node:parent.hos_scroll@viewsize = <viewsize>
				}
			}
			vscrollbar hos_scroll {
				image		"ui/scrollbar_v"
				pos			"680 50"
				height		"480"
				current		0
				viewsize	21
				fullsize	21
				hidewhenunused true
				onChange {
					*node:parent.patients@viewpos = <current>
				}
			}
		}
	}
	// Back to geoscape
	Geoscape geoscape_button
	{
		onClick			{ cmd "ui_pop; ui_pop;" }
	}
	// ==================
	// init function
	// ==================

	func onWindowOpened
	{
		call *node:root.base_container.mainBody.patients@removeAllchild
		cmd "hosp_init;"
	}
}

// ======================
// HOSPITAL EMPLOYEE MENU
// ======================
window hospital_employee
{
	{
		fill		true
	}

	// Base name and container panel
	BaseContainerPanel base_container {
		// Hospital panel
		panel mainBody {
			// Title bar
			panel header {
				{}
				string title {
					string			"_Hospital"
				}
			}
		}
	}

	data ucn {
		integer	-1
	}

	actor_info local {
		pos		"20 100"
	}

	panel stats {
		{
			pos		"750 493"
			size	"250 270"
			border	2
			bordercolor	"0.56 0.81 0.76 1"
			layout	LAYOUT_TOP_DOWN_FLOW
			layoutMargin 0
		}
		abilities abilities {}
		skillbars skills {}
		healthbar health {}
	}

	panel implants {
		{
			pos		"750 5"
			size	"250 471"
			border	2
			bordercolor	"0.56 0.81 0.76 1"
		}
		baseinventory equip {
			pos						"20 20"
			size					"220 200"
			displayimplant			true
			displayunavailableitem	true
			columns					1
			filter					"FILTER_S_IMPLANT"
			onViewChange {
				*node:root.implants.equip_scroll@current = <viewpos>
				*node:root.implants.equip_scroll@viewsize = <viewsize>
				*node:root.implants.equip_scroll@fullsize = <fullsize>
			}
			onSelect { cmd "hosp_implant_details <lastselectedid>;" }
			onChange { cmd "hosp_implant_change <node:root.ucn@integer> <lastselectedid>;" }
		}

		vscrollbar equip_scroll {
			image	"ui/scrollbar_v"
			pos 	"242 20"
			height	"200"
			hidewhenunused true
			onChange {
				*node:root.implants.equip@viewpos = <current>
			}
		}

		container implant {
			pos "20 240"
		}

		panel list {
			pos "20 280"
			size "236 190"
			layout			LAYOUT_TOP_DOWN_FLOW
			layoutMargin	4
			ghost			true
		}
	}
	// Back to geoscape
	Geoscape geoscape_button
	{
		onClick			{ cmd "ui_pop; ui_pop; ui_pop;" }
	}
	/**
	 * @param <1> UCN
	 * @param <2> Name
	 * @param <3> Body
	 * @param <4> BodySkin
	 * @param <5> Head
	 * @param <6> HeadSkin
	 * @param <7> HP
	 * @param <8> MaxHP
	 * @param <9> Rank
	 * @param <10> Rank image
	 * @param <11> 1 if implants are possible, 0 if not
	 */
	confunc hospital_employee_show {
		*node:root.ucn@integer = <1>
		*node:root.name@string = "<2>"
		*node:root.local.body@src = "<3>"
		*node:root.local.body@skin = <4>
		*node:root.local.body.head@src = "<5>"
		*node:root.local.body.head@skin = <6>
		*node:root.stats.health.hp.valuenum@string = "<7>"
		*node:root.stats.health.hp.valuebar@current = <7>
		*node:root.stats.health.hp.valuebar@max = <8>
		*node:root.local.rank_lbl@string = <9>
		*node:root.local.rank_img@src = <10>
		if ( <11> == 1 ) {
			*node:root.implants@invis = false
		} else {
			*node:root.implants@invis = true
		}
	}

	/**
	 * @param <1> index
	 * @param <2> skill
	 * @param <3> skill description (Poor, Mediocre, Average, ...)
	 */
	confunc hospital_employee_set_values {
		*node:root.bar<1>.bar@current = "<2>"
		*node:root.bar<1>.val@string = "<3> (<2>)"
	}

	/**
	 * @param <1> implant id
	 * @param <2> implant name: translated name of the implant
	 * @param <3> implant image
	 * @param <4> installation time: The complete time (days) it needs to be fully installed
	 * @param <5> installed time: The time (days) that is still needs to be fully installed
	 * @param <6> remove time: The complete time (days) it needs to be fully removed
	 * @param <7> removed time: The time (days) that is still needs to be fully removed
	 */
	confunc hospital_employee_add_implant {
		call *node:root.implants.list@createChild("implant<1>", "cmpHospitalImplant")
		*node:root.implants.list.implant<1>.name@string = <2>
		*node:root.implants.list.implant<1>.img@src = <3>
		if ( <5> > 0 ) {
			*node:root.implants.list.implant<1>.timeremaining@max = <4>
			*node:root.implants.list.implant<1>.timeremaining@current = <5>
			*node:root.implants.list.implant<1>.timeremaining@invis = false
		} elif ( <7> > 0 ) {
			*node:root.implants.list.implant<1>.timeremaining@max = <6>
			*node:root.implants.list.implant<1>.timeremaining@current = <7>
			*node:root.implants.list.implant<1>.timeremaining@invis = false
		} else {
			*node:root.implants.list.implant<1>.timeremaining@invis = true
		}
	}

	confunc hospital_employee_clear_implants {
		call *node:root.implants.list@removeAllchild
	}

	func onWindowOpened {
		cmd "hosp_empl_init <1>;"
	}
}
