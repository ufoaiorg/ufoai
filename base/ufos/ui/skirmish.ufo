// ==============
// SKIRMISH MENU
// ==============

component radiobutton cmpMapPreviewButton
{
	{
		icon	"icons/mapselection"
		size	"128 128"
		cvar	"*cvar:mn_svmapid"
		stringValue	" "
		onClick {
			cmd "mn_selectmap \"<node:this.id@string>\";"
		}
	}

	image preview {
		pos	"14 15"
		size	"100 64"
		src	"maps/shots/default"
		ghost	true
	}

	string name {
		string	"The map name"
		pos	"14 96"
		size	"100 32"
		ghost	true
	}

	string id {
		invis	true
		string	"The map name"
		pos	"14 96"
		size	"100 32"
	}

	// @param <1> name of the map
	// @param <2> image name of the map
	// @todo we should not need more than a map id
	func init {
		*node:parent.name@string = "_<1>"
		*node:parent.id@string = "<1>"
		*node:parent@stringValue = "<1>"
		*node:parent.preview@src = "maps/shots/<2>"
	}
}

window skirmish
{
	/** @todo Remove me */
	{ }
	// empty properties to fix parsing problem with "image" token (behaviour or property?)
	// ===========
	// BACKGROUND
	// ===========
	image background
	{
		src		"background/skirmish2"
	}

	panel maplist {
		pos	"61 50"
		size	"282 588"
		layout		LAYOUT_COLUMN
		layoutColumns	2
		layoutMargin	5
		padding		5

		onViewChange {
			*node:root.maplist_scroll@fullsize = <fullsize>
			*node:root.maplist_scroll@current = <viewpos>
			*node:root.maplist_scroll@viewsize = <viewsize>
		}

		// subnode buttons added at runtime
	}


	/**
	 * Callback the receive maplist from client
	 * @param <1> "begin", "add" or "end"
	 * @param <2> If <1> == "add", map id, else nothing
	 * @param <3> If <1> == "add", map picture name, else nothing
	 */
	confunc	sk_maplist_listener {
		if (param1 eq "begin") {
			*cvar:count = 0
			*cvar:mn_svmapid = ""
			call *node:root.maplist@removeAllChild
		} elif (param1 eq "add") {
//			cmd "echo <1> <2> <3>"
			call *node:root.maplist@createchild("map<cvar:count>", "cmpMapPreviewButton")
			call *node:root.maplist.map<cvar:count>.init(param2, param3)
			*cvar:count = (*cvar:count + 1)
			if (*cvar:mn_svmapid eq "") {
				*cvar:mn_svmapid = param2
			}
		} elif (param1 eq "end") {
			delete *cvar:count
			cmd "mn_selectmap \"<cvar:mn_svmapid>\""
		} else {
			cmd "echo sk_maplist_listener: command \"<1>\" unknown."
		}
	}


	vscrollbar maplist_scroll {
		image		"ui/scrollbar_v_green"
		pos		"345 50"
		height		"588"
		onChange	{ *node:root.maplist@viewpos = <current> }
	}

	// ============
	// BACK BUTTON
	// ============
	cmpMenuButton button_back
	{
		string		"_Back"
		pos		"724 514"
		width		"220"
		onClick
		{
			cmd "ui_pop;"
		}
	}
	// ==================
	// START GAME BUTTON
	// ==================
	cmpMenuButton button_start
	{
		string		"_Start Game"
		pos		"491 514"
		width		"220"
		onClick
		{
			cmd "sk_start;"
		}
	}
	// ==============================
	// SKIRMISH GAME SETTINGS STRING
	// ==============================
	string settings
	{
		string		"_Skirmish Game Settings"
		pos			"500 66"
		size		"450 30"
		font		"f_big"
	}
	// ============================
	// MAP/OPTIONS NAVIGATION TABS
	// ============================
	tab create_navigation
	{
		{
			pos	"462 106"
			size	"500 40"
			cvar	*cvar:mn_skirmish_config_tab
			onChange
			{
				call *node:root.sk_update_config_navigation
			}
		}
		option map
		{
			label	"_Map"
			value	"1"
		}
		option options
		{
			label	"_Options"
			value	"2"
		}
	}
	// ========
	// MAP TAB
	// ========
	cmpMapPanel panel_map
	{
		{
			pos	"495 150"
			size	"475 330"
		}
		// ====================
		// UFO(S) SELECTBOX
		// ====================
		string ufos_str
		{
			string		"_UFO:"
			pos			"10 245"
			size		"200 20"
			tooltip		"_Select the UFO that should be used (only possible if available for that map)."
		}
		selectbox ufos_box
		{
			pos		"240 245"
			size	"200 20"
			color	"0.6 0.6 0.6 0.3"
			cvar	*cvar:rm_ufo
			dataid	OPTION_UFOS
		}
		// ======================
		// DROPSHIP(S) SELECTBOX
		// ======================
		string dropships_str
		{
			string		"_Dropship:"
			pos			"10 295"
			size		"200 20"
			tooltip		"_Select the dropship that should be used (only possible if available for that map)."
		}
		selectbox dropships_box
		{
			pos		"240 295"
			size	"200 20"
			color	"0.6 0.6 0.6 0.3"
			cvar	*cvar:rm_drop
			dataid	OPTION_DROPSHIPS
		}
	}
	// ============
	// OPTIONS TAB
	// ============
	panel panel_options
	{
		{
			pos	"495 150"
			size	"475 330"
		}
		// ==========================================
		// EQUIPMENT AI STRINGS, TEXTENTRY & BUTTONS
		// ==========================================
		string equipment_alien
		{
			string		"_Equipment AI Team."
			pos			"10 0"
			size		"450 20"
		}
		textentry slot_alien_equipment
		{
			string		*cvar:ai_equipment
			image		"ui/button_green_verysmall"
			color		"0 .78 0 1"
			selectcolor	"1 1 1 1"
			textalign	ALIGN_CL
			padding		8
			font		"f_verysmall"
			pos			"35 30"
			size		"256 34"
			disabled	true
			onWheelUp	{ cmd "sk_prevequip ai_equipment;" }
			onWheelDown	{ cmd "sk_nextequip ai_equipment;" }
		}
		custombutton prev_alien_equipment
		{
			pos			"0 30"
			size		"35 35"
			icon		"icons/prev"
			onWheelUp	{ cmd "sk_prevequip ai_equipment;" }
			onWheelDown	{ cmd "sk_nextequip ai_equipment;" }
			onClick		{ cmd "sk_prevequip ai_equipment;" }
		}
		custombutton next_alien_equipment
		{
			pos			"285 30"
			size		"35 35"
			icon		"icons/next"
			onWheelUp	{ cmd "sk_prevequip ai_equipment;" }
			onWheelDown	{ cmd "sk_nextequip ai_equipment;" }
			onClick		{ cmd "sk_nextequip ai_equipment;" }
		}
		// ==============================================
		// EQUIPMENT PLAYER STRINGS, TEXTENTRY & BUTTONS
		// ==============================================
		string equipment
		{
			string		"_Equipment of your team."
			pos			"10 75"
			size		"450 20"
		}
		textentry slot_equipment
		{
			string		*cvar:cl_equip
			image		"ui/button_green_verysmall"
			color		"0 .78 0 1"
			selectcolor	"1 1 1 1"
			textalign	ALIGN_CL
			padding		8
			font		"f_verysmall"
			pos			"35 105"
			size		"256 34"
			disabled	true
			onWheelUp	{ cmd "sk_prevequip cl_equip;" }
			onWheelDown	{ cmd "sk_nextequip cl_equip;" }
		}
		custombutton prev_equipment
		{
			pos			"0 105"
			size		"35 35"
			icon		"icons/prev"
			onWheelUp	{ cmd "sk_prevequip cl_equip;" }
			onWheelDown	{ cmd "sk_nextequip cl_equip;" }
			onClick		{ cmd "sk_prevequip cl_equip;" }
		}
		custombutton next_equipment
		{
			pos			"285 105"
			size		"35 35"
			icon		"icons/next"
			onWheelUp	{ cmd "sk_prevequip cl_equip;" }
			onWheelDown	{ cmd "sk_nextequip cl_equip;" }
			onClick		{ cmd "sk_nextequip cl_equip;" }
		}
		// ======================================
		// NUMBER OF AI ACTORS STRINGS & SPINNER
		// ======================================
		string ai_numactors_str
		{
			string		"_Max. AI actors:"
			pos			"10 150"
			size		"200 20"
			tooltip		"_The maximum of AI actors for this game."
		}
		string ai_numactors_value
		{
			string		*cvar:ai_numaliens
			pos			"310 150"
			size		"50 20"
			textalign	ALIGN_CC
		}
		spinner ai_numactors_change
		{
			current		*cvar:ai_numaliens
			image		"ui/spinner_blue"
			pos			"368 149"
			min			1
			max			30
			delta		1
		}
		// ===========================================
		// MAXSOLDIER(S) PER PLAYER STRINGS & SPINNER
		// ===========================================
		string maxsoldierperplayer_str
		{
			string		"_Max. Soldiers:"
			tooltip		"_Sets the maximum amount of soldiers."
			pos			"10 175"
			size		"300 20"
		}
		string maxsoldierperplayer_value
		{
			string		*cvar:sv_maxsoldiersperplayer
			pos			"310 175"
			size		"50 20"
			textalign	ALIGN_UC
		}
		spinner maxsoldierperplayer_change
		{
			current		*cvar:sv_maxsoldiersperplayer
			image		"ui/spinner_blue"
			pos			"368 174"
			min			1
			max			8
			delta		1
		}
		// =======================
		// UGVS STRINGS & SPINNER
		// =======================
		string numugvs_str
		{
			string		"_UGVs:"
			pos			"10 200"
			size		"200 20"
			tooltip		"_UGVs for this game - only valid if enough free soldier slots are available."
		}
		string numugvs_value
		{
			string		*cvar:cl_ugvs
			pos			"310 200"
			size		"50 20"
			textalign	ALIGN_CC
		}
		spinner numugvs_change
		{
			current		*cvar:cl_ugvs
			image		"ui/spinner_blue"
			pos			"368 199"
			min			0
			max			2
			delta		1
		}
		// ========================
		// TEAM STRING & SELECTBOX
		// ========================
		string team_str
		{
			string		"_Team:"
			pos			"10 225"
			size		"200 20"
			tooltip		"_Select which side you want to play with."
		}
		selectbox team_box
		{
			pos		"230 225"
			size	"200 20"
			color	"0.6 0.6 0.6 0.3"
			cvar	*cvar:cl_teamdef
			dataid	OPTION_TEAMDEFS
		}
		cmpMenuButton button_team
		{
			string		"_Team"
			pos			"0 260"
			onClick		{ cmd "ui_push game_team;" }
		}
	}
	// ============
	// INTERACTION
	// ============
	/**
	 * @param<1> boolean to hide or show
	 */
	confunc skirmish_hide_dropships
	{
		*node:root.panel_map.dropships_str@invis = <1>
		*node:root.panel_map.dropships_box@invis = <1>
	}
	/**
	 * @param<1> boolean to hide or show
	 */
	confunc skirmish_hide_ufos
	{
		*node:root.panel_map.ufos_str@invis = <1>
		*node:root.panel_map.ufos_box@invis = <1>
	}
	func sk_update_config_navigation
	{
		*node:root.panel_map@invis = true
		*node:root.panel_options@invis = true
		if ( *cvar:mn_skirmish_config_tab == 1 )
		{
			*node:root.panel_map@invis = false
		}
		elif ( *cvar:mn_skirmish_config_tab == 2 )
		{
			*node:root.panel_options@invis = false
		}
	}
	// =====
	// INIT
	// =====
	func onWindowOpened
	{
		// TODO: Allow changing the difficulty via menu
		cmd	"game_setmode;set ai_numaliens 8;set ai_equipment multiplayer_alien;set difficulty 0;"
		cmd "set mn_serverday 1;mn_getmaps;define cl_ugvs 0;define cl_teamdef phalanx;"
		*cvar:mn_skirmish_config_tab = 1
		*cvar:mn_svmapid = "none"
		cmd "mn_requestmaplist sk_maplist_listener"
		call *node:root.sk_update_config_navigation
	}
}
