// ==================
// WON MENU
// ==================

window won
{
	/** @todo Remove me */
	{ } // empty properties to fix parsing problem with "image" token (behaviour or property?)

	// ==================
	// background
	// ==================

	image background
	{
		src	"background/won_bg"
	}

	// ==================
	// text
	// ==================

	text results
	{
		pos			"500 150"
		size		"480 460"
		lineheight	20
		tabwidth	350
		rows		"23"
		dataid		TEXT_STANDARD
	}

	// ==================
	// buttons
	// ==================

	button bt_continue
	{
		string		"_Continue"
		tooltip		"_Continue"
		image		"ui/button_blue"
		pos			"865 700"
		size		"128 53"
		color		"1 1 1 0.8"
		selectcolor "1 1 1 1"
		font		"f_big"
		onClick		{ cmd "set cp_mission_tryagain 0;mn_pop;uforecover_check;" }
	}

	confunc uforecover_check
	{
		if ( *cvar:mission_uforecovered == 1 ) {
			cmd "mn_push uforecovery;"
		}
	}

	func onInit
	{
		cmd "snd_music ufo2;"
	}

	func onClose
	{
		cmd "cp_results 1;"
	}
}

// ==================
// LOST MENU
// ==================

window lost
{
	/** @todo Remove me */
	{ } // empty properties to fix parsing problem with "image" token (behaviour or property?)

	// ==================
	// background
	// ==================

	image background
	{
		src	"background/lost_bg"
	}

	// ==================
	// text
	// ==================

	text results
	{
		pos			"500 150"
		size		"480 460"
		lineheight	20
		tabwidth	350
		rows		"23"
		dataid		TEXT_STANDARD
	}

	// ==================
	// buttons
	// ==================

	button bt_tryagain
	{
		string		"_Try again"
		tooltip		"_Retry this mission"
		image		"ui/button_blue"
		pos			"480 700"
		size		"128 53"
		color		"1 1 1 0.8"
		selectcolor "1 1 1 1"
		font		"f_big"
		onClick		{ cmd "set cp_mission_tryagain 1;mn_pop;" }
	}

	button bt_continue
	{
		string		"_Continue"
		tooltip		"_Continue"
		image		"ui/button_blue"
		pos			"865 700"
		size		"128 53"
		color		"1 1 1 0.8"
		selectcolor "1 1 1 1"
		font		"f_big"
		onClick		{ cmd "set cp_mission_tryagain 0;mn_pop;" }
	}

	func onInit
	{
		cmd "snd_music ufo1;"
	}

	func onClose
	{
		cmd "cp_results 0;"
	}
}

// ==================================
// POPUP FOR UFO RECOVERY (Base/sell)
// ==================================
window uforecovery
{
	{
		pos			"100 150"
		size		"700 435"
		dragbutton	true
		modal		true
		preventtypingescape true		// we can't use ESC
		image		"ui/popup_alpha_tile"
		string		"_UFO recovery"
	}

	tab uforecovery_navigation {
		{
			pos		"17 125"
			size	"667 38"
			cvar	*cvar:mn_uforecovery_tab
			onChange {
				cmd "uforecovery_tabselect <cvar:mn_uforecovery_tab> ;"
			}
		}
		option store {
			label	"_Store UFO"
			value	"store"
		}
		option sell {
			label	"_Sell UFO"
			value	"sell"
		}
	}

	string actualufo
	{
		string		*cvar:mn_uforecovery_actualufo
		pos			"30 70"
		size		"610 40"
		font		"f_small"
		textalign	ALIGN_UL
	}

	//
	// store panel
	//
	panel store_panel {
		{
			pos		"20 165"
			size	"660 255"
		}
		rows lines {
			pos			"25 50"
			size		"590 128"
			color1		"0 0.08 0 1"
			color2		"0 0.16 0 1"
			lineheight	16
		}

		string ur_yards_title
		{
			string		"_UFO Yard"
			pos			"25 30"
			size		"300 20"
			font		"f_small_bold"
		}
		string ur_capacity_title
		{
			string		"_Capacity (free/all)"
			pos			"325 30"
			size		"290 20"
			font		"f_small_bold"
			textalign	ALIGN_UR
		}

		textlist ur_ufoyards
		{
			dataid		TEXT_UFORECOVERY_UFOYARDS
			pos			"30 50"
			size		"395 128"
			color		"0.60 0.60 0.60 1"
			selectcolor	"1 1 1 1"
			lineheight	16
			tabwidth	100
			rows		"8"
			onClick		{
				*node:root.store_panel.ur_capacities@lineselected = <lineselected>
			}
			onViewChange	{
				*node:root.store_panel.ur_ufoyards_scroll@fullsize = <fullsize>
				*node:root.store_panel.ur_ufoyards_scroll@current = <viewpos>
				*node:root.store_panel.ur_ufoyards_scroll@viewsize = <viewsize>
				*node:root.store_panel.ur_capacities@viewpos = <viewpos>
			}
		}
		textlist ur_capacities
		{
			dataid		TEXT_UFORECOVERY_CAPACITIES
			pos			"425 50"
			size		"185 128"
			color		"0.60 0.60 0.60 1"
			selectcolor	"1 1 1 1"
			lineheight	16
			rows		"8"
			textalign	ALIGN_UR
			onClick		{
				*node:root.store_panel.ur_ufoyards@lineselected = <lineselected>
			}
			onViewChange	{
				*node:root.store_panel.ur_ufoyards_scroll@fullsize = <fullsize>
				*node:root.store_panel.ur_ufoyards_scroll@current = <viewpos>
				*node:root.store_panel.ur_ufoyards_scroll@viewsize = <viewsize>
				*node:root.store_panel.ur_ufoyards@viewpos = <viewpos>
			}
		}
		vscrollbar ur_ufoyards_scroll {
			image		"ui/scrollbar_v_green"
			pos			"620 50"
			height		"128"
			current		0
			viewsize	8
			fullsize	8
			hidewhenunused true
			onChange	{
				*node:root.store_panel.ur_ufoyards@viewpos = <current>
				*node:root.store_panel.ur_capacities@viewpos = <current>
			}
		}

		cmpPopupButton bt_basesel
		{
			string		"_Recover"
			tooltip		"_Begin UFO recovery process"
			pos			"242 210"
			onClick		{ cmd "cp_uforecovery_store_start <node:root.store_panel.ur_ufoyards@lineselected>; mn_pop;" }
		}
	}

	//
	// sell panel
	//
	panel sell_panel
	{
		{
			pos		"20 165"
			size	"660 255"
		}

		button name_label
		{
			string		"_Nation:"
			tooltip		"_Sort nations by name"
			pos			"25 30"
			size		"250 20"
			font		"f_small_bold"
			color		"0 0.5 0 1"
			onClick		{ cmd "cp_uforecovery_sort nation;" }
		}

		button bt_price
		{
			string		"_Price:"
			tooltip		"_Sort nations by price"
			pos			"275 30"
			size		"125 20"
			font		"f_small_bold"
			color		"0 0.5 0 1"
			onClick		{ cmd "cp_uforecovery_sort price;" }
		}

		button bt_relation
		{
			string		"_Relations:"
			tooltip		"_Sort nations by relation"
			pos			"450 30"
			size		"175 20"
			font		"f_small_bold"
			color		"0 0.5 0 1"
			onClick		{ cmd "cp_uforecovery_sort happiness;" }
		}

		rows lines {
			pos			"25 50"
			size		"590 128"
			color1		"0 0.08 0 1"
			color2		"0 0.16 0 1"
			lineheight	16
		}

		/* tabbed textlist */
		text cp_uforecovery_nationlist
		{
			dataid		TEXT_UFORECOVERY_NATIONS
			pos			"25 50"
			size		"590 128"
			lineheight	16
			tabwidth	100
			color		"0.60 0.60 0.60 1"
			selectcolor	"1 1 1 1"
			rows		"8"
			mousefx		true
			longlines	LONGLINES_PRETTYCHOP
			onClick		{ cmd "cp_uforecovery_nationlist_click <lineselected>;" }
			onViewChange	{
				*node:root.sell_panel.cp_ur_nationlist_scroll@fullsize = <fullsize>
				*node:root.sell_panel.cp_ur_nationlist_scroll@current = <viewpos>
				*node:root.sell_panel.cp_ur_nationlist_scroll@viewsize = <viewsize>
			}
		}
		vscrollbar cp_ur_nationlist_scroll {
			image		"ui/scrollbar_v_green"
			pos			"620 50"
			height		"128"
			current		0
			viewsize	8
			fullsize	8
			hidewhenunused true
			onChange	{ *node:root.sell_panel.cp_uforecovery_nationlist@viewpos = <current> }
		}

		string selected_nation_str
		{
			string		"_Selected nation:"
			pos			"30 186"
			size		"210 20"
		}

		string selected_nation_val
		{
			string		*cvar:mission_recoverynation
			pos			"250 186"
			size		"360 20"
		}

		cmpPopupButton bt_natsel
		{
			string		"_Sell"
			tooltip		"_Begin UFO sell process"
			pos			"242 210"
			onClick		{ cmd "cp_uforecovery_sell_start; mn_pop;" }
		}
	}

	/*
	 * function to manage tab
	 */
	confunc uforecovery_tabselect {
		*cvar:mn_uforecovery_tab = <1>
		if ( *cvar:mn_uforecovery_tab eq "store" ) {
			*node:root.store_panel@invis = false
			*node:root.sell_panel@invis = true
		}
		if ( *cvar:mn_uforecovery_tab eq "sell" ) {
			*node:root.sell_panel@invis = false
			*node:root.store_panel@invis = true
		}
	}

	confunc btnatsel {
		*cvar:tmp = "<1>"
		if ( *cvar:tmp eq "enable" ) {
			*node:root.sell_panel.bt_natsel@disabled = false
		} else {
			*node:root.sell_panel.bt_natsel@disabled = true
		}
		delete *cvar:tmp
	}

	confunc cp_nationsel_select {
		*node:root.sell_panel.cp_uforecovery_nationlist@lineselected = <1>
	}

	confunc btbasesel {
		*cvar:tmp = "<1>"
		if ( *cvar:tmp eq "enable" ) {
			*node:root.store_panel.bt_basesel@disabled = false
		} else {
			*node:root.store_panel.bt_basesel@disabled = true
		}
		delete *cvar:tmp
	}

	confunc cp_basesel_select {
		*node:root.store_panel.ur_ufoyards@lineselected = <1>
		*node:root.store_panel.ur_capacities@lineselected = <1>
	}

	func onInit {
		cmd "uforecovery_tabselect store"
		*node:root.store_panel.ur_ufoyards@lineselected = -1
		*node:root.store_panel.ur_capacities@lineselected = -1
		*node:root.sell_panel.cp_uforecovery_nationlist@lineselected = -1
		*cvar:mission_recoverynation = ""

		if ( *cvar:mission_uforecovered == 0 ) {
			cmd "mn_pop;"
		} else {
			cmd "cp_uforecovery_sell_init; cp_uforecovery_store_init;"
		}
	}

	func onClose {
		delete *cvar:mn_uforecovery_tab
		delete *cvar:mn_uforecovery_actualufo
	}
}

