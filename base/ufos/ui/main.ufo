// ==================
// MAIN MENU
// ==================

window main
{
	/** @todo Remove me */
	{ } // empty properties to fix parsing problem with "image" token (behaviour or property?)

	// ==================
	// background
	// ==================

	image background
	{
		src	"background/main_bg"
	}

	string version
	{
		string	*cvar:version
		pos		"10 744"
	}

	// ==================
	// buttons
	// ==================

	panel buttons {
		{
			pos			"640 170"
			size		"256 344"
			layout		LAYOUT_TOP_DOWN_FLOW
			layoutMargin	6
		}

		cmpMenuButton button_singleplayer
		{
			onClick	{ cmd "mn_push singleplayer;" }
			string	"_Single-player"
		}

		cmpMenuButton button_multiplayer
		{
			onClick	{ cmd "mn_push_multiplayer;" }
			string	"_Multiplayer"
		}

		cmpMenuButton button_options
		{
			onClick	{ cmd "mn_push options;" }
			string	"_Options"
		}

		cmpMenuButton button_tut
		{
			onClick	{ cmd "mn_push tutorials;" }
			string	"_Tutorials"
		}

		cmpMenuButton button_credits
		{
			onClick	{ cmd "seq_start credits;" }
			string	"_Credits"
		}
	}

	cmpMenuButton button_exit
	{
		pos	"640 520"
		onClick	{ cmd "quit;" }
		string	"_Exit"
	}

	func onInit
	{
		cmd "music_change main;"
		cmd "mn_push_child popup_tipoftheday main;check_cvars;"
		if ( *cvar:cl_introshown != 1 ) {
			cmd "mn_push intro;"
			*cvar:cl_introshown = 1
		}
	}
}

// ==================
// CVAR CHECK MENU
// ==================

window checkcvars
{
	{
		pos			"124 192"
		size		"777 402"
		dragbutton	true
		modal		true
		image		"ui/popup_alpha_tile"
		string		"_Initial settings"
	}

	string name_label
	{
		string		"_User Information"
		pos			"26 48"
		size		"300 30"
		font		"f_normal"
	}

	// ===================
	// NAME
	// ===================
	string current_name
	{
		string		"_Name:"
		pos			"41 88"
		size		"300 20"
		color		"1 1 1 1"
	}
	textentry slot_name
	{
		string		*cvar:cl_name
		image		"ui/button_green_verysmall"
		color		"0 .78 0 1"
		selectcolor	"1 1 1 1"
		textalign	ALIGN_CL
		padding		8
		font		"f_verysmall"
		pos			"170 82"
		size		"185 34"
	}

	// ===================
	// BARS
	// ===================
	string volume_str
	{
		string		"_Volume Control"
		pos			"26 168"
		size		"300 30"
		font		"f_normal"
	}
	string effects_str
	{
		string		"_Effects:"
		pos			"41 198"
		size		"150 20"
	}
	image effects_rahmen
	{
		src		"ui/bar_rahmen"
		pos			"161 203"
	}
	bar effects_bar
	{
		current		*cvar:snd_volume
		pos			"170 208"
		size		"238 6"
		color		"0 .8 0 1"
		max			1.0
	}
	string music_str
	{
		string		"_Music:"
		pos			"41 223"
		size		"150 20"
	}
	image music_rahmen
	{
		src		"ui/bar_rahmen"
		pos			"161 228"
	}
	bar music_bar
	{
		current		*cvar:snd_music_volume
		pos			"170 233"
		size		"238 6"
		color		"0 .8 0 1"
		max			128
	}

	// ===================
	// LANGUAGE
	// ===================
	string language_str
	{
		string		"_Language"
		pos			"410 48"
		size		"300 30"
		font		"f_normal"
	}
	string language_str2
	{
		string		"_Language:"
		pos			"425 82"
		size		"108 20"
	}
	optionlist select_language
	{
		pos			"531 78"
		cvar		*cvar:s_language
		image		"ui/panel_green_slim"
		size		"155 226"
		color		"0 .78 0 1"
		selectcolor	"1 1 1 1"
		dataid		OPTION_LANGUAGES
		onViewChange {
			*node:root.select_language_scroll@current = <viewpos>
			*node:root.select_language_scroll@fullsize = <fullsize>
			*node:root.select_language_scroll@viewsize = <viewsize>
		}
	}

	vscrollbar select_language_scroll {
		image			"ui/scrollbar_v_green"
		pos				"692 78"
		height			"226"
		current			0
		viewsize		8
		fullsize		21
		onChange {
			*node:root.select_language@viewpos = <current>
		}
	}

	// ===================
	// VALIDATION
	// ===================

	string message
	{
		string		"_You have to set the playername and hit the 'OK' button:"
		pos			"41 338"
		size		"500 40"
		color		"1 1 1 0.5"
	}

	cmpPopupButton bt_ok
	{
		string		"_OK"
		tooltip		"_Save settings"
		pos			"566 338"
		onClick		{ cmd "mn_pop; saveconfig config.cfg;" }
	}

	// check again - maybe the user has forgotton something
	func onClose
	{
		cmd "check_cvars;"
	}
}

// ==================
// TIP OF THE DAY
// ==================
window popup_tipoftheday
{
	{
		pos			"38 456"
		size		"496 247"
		dragbutton	true
		image		"ui/popup_alpha_tile"
		string		"_Tip of the day"
	}

	checkbox activate
	{
		current		*cvar:cl_showTipOfTheDay
		pos			"36 194"
		image		"ui/checkbox_blue"
		size		"20 18"
		onChange		{ cmd "check_nexttip;" }
	}
	string activate_string
	{
		string		"_Show tip of the day"
		pos			"58 194"
		size		"200 25"
		font		"f_small"
		onClick		{
			call *node:root.activate@toggle
		}
	}

	text popup_tipoftheday_text
	{
		pos			"26 58"
		size		"444 120"
		lineheight	20
		tabwidth	150
		dataid		TEXT_TIPOFTHEDAY
		rows		6
	}

	button bt_next
	{
		string		"_Next"
		tooltip		"_Next tip"
		image		"ui/button_green_small"
		width		"176"
		pos			"266 188"
		font		"f_menu"
		color		"0 0.5 0 1"
		onClick		{ 
			if ( *cvar:cl_showTipOfTheDay == 1 ) {
				cmd "tipoftheday;"
			} else {
				cmd "mn_close popup_tipoftheday;"
			}
		}
	}

	confunc check_nexttip
	{
		if ( *cvar:cl_showTipOfTheDay == 1 ) {
			*node:root.bt_next@string = "_Next"
			*node:root.bt_next@tooltip = "_Next tip"
		} else {
			*node:root.bt_next@string = "_Close"
			*node:root.bt_next@tooltip = "_Close window"
		}
	}

	func onInit
	{
		if ( *cvar:cl_showTipOfTheDay == 1 ) {
			cmd "tipoftheday random; check_nexttip;"
		} else {
			cmd "mn_close popup_tipoftheday;"
		}
	}
}
