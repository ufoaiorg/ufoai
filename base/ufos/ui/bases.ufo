// ===============
// BASEMANAGEMENT
// ===============

component button cmpBaseButton
{
	image		"ui/button_green_small"
	size		"230 35"
	color		"0 0.8 0 1"
	selectcolor "1 1 1 1"
	font		"f_menu"
}

component panel cmpBuildingSpace
{
	{
		size	"230 25"
	}
	string label
	{
		pos				"0 0"
		size			"140 15"
		font			"f_verysmall"
		color			"0 0.8 0 1"
		contentalign	ALIGN_CL
	}
	string data
	{
		pos				"140 0"
		size			"90 15"
		font			"f_verysmall"
		color			"0 0.8 0 1"
		contentalign	ALIGN_CR
	}
	bar data_bar
	{
		pos		"4 15"
		size	"224 5"
		color	"0 0.8 0 1"
		bordercolor	"0 0.8 0 1"
		border	1
		current	0
		max		100
	}
}

window bases
{
	/** @todo Remove me */
	{ } // empty properties to fix parsing problem with "image" token (behaviour or property?)

	// ==================
	// background
	// ==================

	image back_ul
	{
		src		"ui/base_ul"
		pos			"0 0"
	}

	image back_ur
	{
		src		"ui/base_ur"
		pos			"512 0"
	}

	image back_lr
	{
		src		"ui/base_lr"
		pos			"512 256"
	}

	cmpBaseHeader baseHeader {
	}

	// here we need the back-to-geoscape button
	custombutton bt_back_to_geoscape
	{
		tooltip		"_Back to geoscape"
		icon		"icons/geoscape"
		background	"buttons/big_blue"
		pos			"950 20"
		size		"59 55"
		onClick		{ *cvar:mn_lastsubmenu = "bases" cmd "ui_pop;" }
	}

	basemap thebasemap
	{
		pos			"0 96"
		size		"721 672"
	}

	// ===============
	// buttons
	// ===============
	panel base_buttons
	{
		{
			pos "758 118"
			size "230 640"
			layout		LAYOUT_TOP_DOWN_FLOW
			layoutMargin	6
		}

		cmpBaseButton buildings
		{
			string		"_Buildings"
			tooltip		"_Build base facilities"
			onClick		{ cmd "ui_push buildings;" }
		}

		cmpBaseButton aircraft
		{
			string		"_Aircraft"
			tooltip		"_Aircraft management and crew equipment"
			onClick		{ cmd "ui_push aircraft;" }
		}

		cmpBaseButton buysell
		{
			string		"_Buy/Sell"
			tooltip		"_Buy/Sell equipment, aircraft and UGV"
			onClick		{ cmd "ui_push market;" }
		}

		cmpBaseButton transfer
		{
			string		"_Transfer"
			tooltip		"_Transfer equipment, aircraft, UGV, aliens and employees to other bases"
			onClick		{ cmd "ui_push transfer;" }
		}

		cmpBaseButton research
		{
			string		"_Research"
			tooltip		"_Research new technology"
			onClick		{ cmd "ui_push research;" }
		}

		cmpBaseButton production
		{
			string		"_Production"
			tooltip		"_Produce equipment, aircraft and UGV"
			onClick		{ cmd "ui_push production;" }
		}

		cmpBaseButton hire
		{
			string		"_Employees"
			tooltip		"_Hire or dismiss employees"
			onClick		{ cmd "ui_push employees;" }
		}

		cmpBaseButton containment
		{
			string		"_Containment"
			tooltip		"_Manage captured aliens"
			onClick		{ cmd "ui_push aliencont;" }
		}

		cmpBaseButton hospital
		{
			string		"_Hospital"
			tooltip		"_Treat wounded soldiers and perform implant surgery"
			onClick		{ cmd "ui_push hospital;" }
		}

		cmpBaseButton summary
		{
			string		"_Base Summary"
			tooltip		"_Information about this base (available storage space, production, research, etc.)"
			onClick		{ cmd "ui_push basesummary;" }
		}
		panel spacer { size "230 10" }

		/*
		 * Bars indicating storage/living/lab/workshop space are added with
		 * confunc show_bld_space
		 */
		panel bld_space
		{
			{
				size	"230 130"
				layout		LAYOUT_TOP_DOWN_FLOW
				layoutMargin	1
			}
		}

		/*
		 * Panel showing how many soldiers, scientists and engineers are in the base
		 */
		panel cur_employees
		{
			{
				size	"230 70"
			}
			image soldier
			{
				tooltip	"_Soldiers"
				src	"icons/bighead_soldier"
				pos		"0 0"
			}
			string soldier_val
			{
				tooltip	"_Soldiers"
				pos		"40 45"
				size	"40 15"
				font			"f_verysmall"
				color			"0 0.8 0 1"
				contentalign	ALIGN_CL
			}
			image scientist
			{
				tooltip	"_Scientists"
				src	"icons/bighead_scientist"
				pos		"80 0"
			}
			string scientist_val
			{
				tooltip	"_Scientists"
				pos		"120 45"
				size	"30 15"
				font			"f_verysmall"
				color			"0 0.8 0 1"
				contentalign	ALIGN_CL
			}
			image worker
			{
				tooltip	"_Workers"
				src	"icons/bighead_worker"
				pos		"160 0"
			}
			string worker_val
			{
				tooltip	"_Workers"
				pos		"200 45"
				size	"30 15"
				font			"f_verysmall"
				color			"0 0.8 0 1"
				contentalign	ALIGN_CL
			}
		}

		/*
		 * Passes information on building space for living quarters, storage
		 * workshop and lab.
		 * param1 = name
		 * param2 = type (building_lab,building_quarters,building_storage,building_workshop,building_amstorage)
		 * param3 = current
		 * param4 = capacity
		 */
		confunc show_bld_space
		{
			/* Don't show building space if building is not completed (max = 0) */
			if (param4 != 0) {
				call *node:parent.bld_space@createChild ( "<2>", "cmpBuildingSpace" )
				*node:parent.bld_space.<2>.label@string = "<1>"
				*node:parent.bld_space.<2>.data@string = <3>/<4>
				*node:parent.bld_space.<2>.data_bar@current = <3>
				*node:parent.bld_space.<2>.data_bar@max = <4>
			}
		}

		/*
		 * Prepare the building space indicators.
		 * Bases which are missing a facility will not call show_bld_space.
		 * This ensures all indicators appear even if there is no bld space.
		 * @TODO: Make anti-matter show 0/0 if it is researched but no bld
		 *        is present.
		 */
		confunc clear_bld_space
		{
			call *node:parent.bld_space@removeAllChild
		}


		/*
		 * Current employees of a particular base
		 * param1 = number of soldiers
		 * param2 = number of scientists
		 * param3 = number of workers
		 */
		confunc current_employees
		{
			*node:parent.cur_employees.soldier_val@string = "<1>"
			*node:parent.cur_employees.scientist_val@string = "<2>"
			*node:parent.cur_employees.worker_val@string = "<3>"
		}
	}
	confunc update_basebutton
	{
		*node:root.base_buttons.<1>@disabled = <2>
		*node:root.base_buttons.<1>@tooltip = <3>
	}

	func onWindowOpened
	{
		cmd "base_init;"
	}
	func onWindowClosed
	{
		cmd "clear_bld_space;"
	}
}
