// ==============
// BASEMANAGEMENT
// ==============

component cmpDefBaseHeader cmpBaseviewHeader
{
	{
		pos "0 0"
	}

	custombutton bt_back
	{
		tooltip		"_Back to geoscape"
		icon		geoscape
	}

	textentry name
	{
		string		*cvar:mn_base_title
		tooltip		"_Name of this base"
		pos			"60 30"
		size		"500 32"
		font		"f_big"
		onChange {
			// stores the value of the cvar in the base structure
			cmd	"base_changename"
		}
	}

	zone wz_base
	{
		pos	"25 25"
		size	"606 46"
		onWheelUp	{ cmd "mn_base_next;" }
		onWheelDown	{ cmd "mn_base_prev;" }
	}

	button bt_edit
	{
		tooltip	"_Rename your base"
		icon	pen
		pos	"570 40"
		size "16 16"
		padding 0
		onClick
		{
			call *node:parent.name@edit
		}
	}

	custombutton prev_base
	{
		tooltip	"_Switch to the previous base"
		icon	prev
		pos		"30 30"
		size	"30 30"
		onClick	{ cmd "mn_base_prev;" }
		onWheelUp	{ cmd "mn_base_next;" }
		onWheelDown	{ cmd "mn_base_prev;" }
	}

	custombutton next_base
	{
		tooltip	"_Switch to the next base"
		icon	next
		pos		"600 30"
		size	"30 30"
		onClick	{ cmd "mn_base_next;" }
		onWheelUp	{ cmd "mn_base_next;" }
		onWheelDown	{ cmd "mn_base_prev;" }
	}


}

	component button cmpBaseButton
	{
		image		"ui/button_green_small"
		size		"230 56"
		color		"0 0.8 0 1"
		selectcolor "1 1 1 1"
		font		"f_menu"
	}

window bases
{
	/** @todo Remove me */
	{ } // empty properties to fix parsing problem with "image" token (behaviour or property?)

	// ==================
	// background
	// ==================

	image back_ul
	{
		src		"ui/base_ul"
		pos			"0 0"
	}

	image back_ur
	{
		src		"ui/base_ur"
		pos			"512 0"
	}

	image back_lr
	{
		src		"ui/base_lr"
		pos			"512 256"
	}

	cmpBaseviewHeader baseHeader {
	}

	basemap thebasemap
	{
		pos			"0 96"
		size		"725 672"
	}

	// ===============
	// buttons
	// ===============
	panel base_buttons
	{
		{
			pos "758 118"
			size "230 640"
			layout		LAYOUT_TOP_DOWN_FLOW
			layoutMargin	6
		}

	cmpBaseButton buildings
	{
		string		"_Buildings"
		tooltip		"_Build base facilities"
		onClick		{ cmd "ui_push buildings;" }
	}

	cmpBaseButton aircraft
	{
		string		"_Aircraft"
		tooltip		"_Aircraft management and crew equipment"
		onClick		{ cmd "ui_push aircraft;" }
	}

	cmpBaseButton buysell
	{
		string		"_Buy/Sell"
		tooltip		"_Buy/Sell equipment, aircraft and UGV"
		onClick		{ cmd "ui_push market;" }
	}

	cmpBaseButton transfer
	{
		string		"_Transfer"
		tooltip		"_Transfer equipment, aircraft, UGV, aliens and employees to other bases"
		onClick		{ cmd "ui_push transfer;" }
	}

	cmpBaseButton research
	{
		string		"_Research"
		tooltip		"_Research new technology"
		onClick		{ cmd "ui_push research;" }
	}

	cmpBaseButton production
	{
		string		"_Production"
		tooltip		"_Produce equipment, aircraft and UGV"
		onClick		{ cmd "ui_push production;" }
	}

	cmpBaseButton hire
	{
		string		"_Employees"
		tooltip		"_Hire or dismiss employees"
		onClick		{ cmd "ui_push employees;" }
	}

	cmpBaseButton containment
	{
		string		"_Containment"
		tooltip		"_Manage captured aliens"
		onClick		{ cmd "ui_push aliencont;" }
	}

	cmpBaseButton hospital
	{
		string		"_Hospital"
		tooltip		"_Treat wounded soldiers and perform implant surgery"
		onClick		{ cmd "ui_push hospital;" }
	}

	cmpBaseButton summary
	{
		string		"_Base Summary"
		tooltip		"_Information about this base (available storage space, production, research, etc.)"
		onClick		{ cmd "ui_push basesummary;" }
	}
}
	confunc update_basebutton
	{
		*node:root.base_buttons.<1>@disabled = <2>
		*node:root.base_buttons.<1>@tooltip = <3>
	}

	func onInit
	{
		cmd "base_init;"
	}
}
