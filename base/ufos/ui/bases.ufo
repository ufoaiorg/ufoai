/*
 * @TODO:
 *   See here: http://ufoai.git.sourceforge.net/git/gitweb.cgi?p=ufoai/ufoai;a=commit;h=6f98e6620c75363f51b7e736052ac80f9ac50b7a
 * - Production
 * - Base summary
 * - Add injured soldiers to employee count list
 * - Icons for UFOPaedia, kill alien (aliencontainment)
 * LATER IMPROVEMENTS:
 * - Build facility: make building preview appear below each option when clicked
 * - Build facility: make building capacity (+) buttons automatically select that building type
 * - Build facility: Make the add building button turn to a warning if it is
 *   too high/low (low is for antimatter only)
 * - Don't show progress bar in alien containment if alien is already researched
 */

window bases
{
	{
		fill		true
		onWindowClosed { cmd "trans_emptyairstorage;" }
	}

	// Left base panel
	BaseContainerPanel base_container
	{
		// no mainBody here
		panel mainBody
		{
			invis true
		}

		// The base map
		panel base_map_container
		{
			{
				pos				"4 30"
				size			"715 670"
				border			"2"
				bordercolor		"0.527 0.6 0.21 1"
			}
			basemap thebasemap
			{
				pos				"1 1"
				size			"715 667"
			}
		}

		// Employee panel
		// @TODO: add support for injured soldiers
		panel cur_employees
		{
			{
				pos		"0 705"
				size	"490 48"
				layout	LAYOUT_LEFT_RIGHT_FLOW
			}
			EmployeeCount scientists
			{
				{
					tooltip	"_Scientists"
					onClick { cmd "ui_push employees 1;" }
				}
				string label { string "_Scientists" }
				button icon { icon "icons/head_scientist" }
			}
			EmployeeCount workers
			{
				{
					tooltip	"_Engineers"
					onClick { cmd "ui_push employees 2;" }
				}
				string label { string "_Engineers" }
				button icon { icon "icons/head_worker" }
			}
			EmployeeCount soldiers
			{
				{
					tooltip	"_Soldiers"
					onClick { cmd "ui_push employees 0;" }
				}
				string label { string "_Soldiers" }
				button icon { icon "icons/head_soldier" }
			}
			EmployeeCount pilots
			{
				{
					tooltip	"_Pilots"
					onClick	{ cmd "ui_push employees 3;" }
				}
				string label { string "_Pilots" }
				button icon { icon "icons/head_pilot" }
			}
		}

		// Base summary, transfers, options, UFOpaedia
		panel buttons
		{
			{
				pos		"495 705"
				size	"224 48"
				layout	LAYOUT_LEFT_RIGHT_FLOW
			}
			button options
			{
				size	"56 48"
				icon		"icons/options"
				tooltip		"_Options"
				background	"icons/circle0"
				onMouseEnter	{ *node:this@background = "icons/circle" }
				onMouseLeave	{ *node:this@background = "icons/circle0" }
				onClick			{ cmd "ui_push options;" }
			}
			button ufopaedia
			{
				size	"56 48"
				icon		"icons/info"
				tooltip		"_UFOpedia"
				background	"icons/circle0"
				onMouseEnter	{ *node:this@background = "icons/circle" }
				onMouseLeave	{ *node:this@background = "icons/circle0" }
				onClick			{ cmd "ui_push ufopedia;" }
			}
			button transfers
			{
				size	"56 48"
				icon		"icons/transfer"
				tooltip		"_Transfer"
				background	"icons/circle0"
				onMouseEnter	{ *node:this@background = "icons/circle" }
				onMouseLeave	{ *node:this@background = "icons/circle0" }
				onClick			{ cmd "ui_push transfer;" }
			}
			button basesummary
			{
				size	"56 48"
				icon		"icons/stats"
				tooltip		"_Base Summary"
				background	"icons/circle0"
				onMouseEnter	{ *node:this@background = "icons/circle" }
				onMouseLeave	{ *node:this@background = "icons/circle0" }
				onClick			{ cmd "ui_push basesummary;" }
			}
		}

	}

	// Main base sections
	panel sections
	{
		{
			pos			"726 5"
			size		"298 758"
			layout		LAYOUT_TOP_DOWN_FLOW
			layoutMargin	2
		}
		BaseSection facilities
		{
			{
				onClick		{ cmd "show_facility <node:this.state@string>;" }
			}
			string label { string "_Facilities" }
			button icon { icon "icons/facilities" }
			data state { string "true" }
			data height { integer 48 }

			panel bld_panel
			{
				{
					pos 			"0 48"
					size 			"298 0"
					layout			LAYOUT_TOP_DOWN_FLOW
					layoutMargin	0
				}
				data height { integer 0 }

				// Current and maximum building capacity for buildings
				// Shows living space, production, lab, storage, antimatter storage
				// OR shows all when facilities tab is clicked on.
				panel bld_space
				{
					{
						size			"298 0"
						layout			LAYOUT_TOP_DOWN_FLOW
						layoutMargin	0
					}
				}
				// Preview of currently selected building
				panel preview
				{
					{
						size		"298 185"
						onClick		{ cmd "building_ufopedia;" }
					}
					image bld_image
					{
						src				*cvar:mn_building_image
						preventratio	true
						pos				"29 0"
						size			"224 90"
						contentalign	ALIGN_UL
						ghost			true
					}
					/*
					button ufopedia
					{
						icon		"icons/bordered_magnifying_glass"
						tooltip		"_UFOpaedia"
						pos			"29 74"
						size		"16 16"
						ghost		true
					}
					*/
					text info
					{
						pos				"29 100"
						size			"224 60"
						lineheight		15
						tabwidth		140
						font			"f_verysmall"
						color			"0.56 0.81 0.76 1"
						dataid			TEXT_BUILDING_INFO
						rows			4
						contentalign	ALIGN_UL
						ghost			true
					}
					string status
					{
						string			*cvar:mn_building_status
						pos				"29 160"
						size			"224 20"
						font			"f_verysmall"
						color			"0.56 0.81 0.76 1"
						contentalign	ALIGN_UL
						ghost			true
					}
				}
			}

			/**
			 * @brief Clear building space indicators in preparation for updating the list
			 */
			confunc clear_bld_space
			{
				call *node:parent.bld_panel.bld_space@removeAllChild
				*node:parent.bld_panel.bld_space@size = "298 0"
				*node:parent.bld_panel.bld_space@height = 0
				*node:parent.bld_panel@size = "298 0"
				*node:parent.bld_panel.height@integer = 0
				*node:parent@size = "298 48"
				*node:parent.height@integer = 48
			}

			/**
			 * @brief Passes information on capacity for available buildings
			 * @param <1> building name
			 * @param <2> building id (building_lab, building_quarters, building_storage, etc.)
			 * @param <3> currently used capacity
			 * @param <4> actual max capacity
			 * @param <5> number of buildings built of this type
			 * @param <6> max number of buildings can be built from this type
			 */
			confunc show_bld_space
			{
				// Set the appropriate size of the panel
				*node:parent.bld_panel.bld_space@height = (<node:parent.bld_panel.bld_space@height> + 30)
				*node:parent.bld_panel.bld_space@size = "298 <node:parent.bld_panel.bld_space@height>"

				// If showing a few facilities, don't show building preview panel
				if (*node:parent.state@string eq "true") {
					*node:parent.bld_panel.height@integer = <node:parent.bld_panel.bld_space@height>
				// Show building preview panel for all facilities
				} else {
					*node:parent.bld_panel.height@integer = (<node:parent.bld_panel.bld_space@height> + 185)
				}
				// Expand parent panels appropriately
				*node:parent.bld_panel@size = "298 <node:parent.bld_panel.height@integer>"
				*node:parent.height@integer = (<node:parent.bld_panel.height@integer> + 48)
				*node:parent@size = "298 <node:parent.height@integer>"

				// Add the capacity bar
				call *node:parent.bld_panel.bld_space@createChild ( "<2>", "BuildingSpace" )
				*node:parent.bld_panel.bld_space.<2>.id@string = "<2>"
				*node:parent.bld_panel.bld_space.<2>.smlicon@icon = "icons/<2>"
				*node:parent.bld_panel.bld_space.<2>@tooltip = "<1>"
				*node:parent.bld_panel.bld_space.<2>.label@string = "<1>"
				*node:parent.bld_panel.bld_space.<2>.data_bar@current = <3>
				*node:parent.bld_panel.bld_space.<2>.data_bar@max = <4>

				// Only show the capacity string if max isn't 0
				if (param4 ne 0) {
					*node:parent.bld_panel.bld_space.<2>.data@string = <3>/<4>
				} elif (param6 > 0) {
					// Show current/max number of buildings of a type if limited
					*node:parent.bld_panel.bld_space.<2>.data@string = <5>/<6>
				}
				// Show/Hide "add" building button
				if (param5 == param6 ) {
					*node:parent.bld_panel.bld_space.<2>.alert@invis = true
				} else {
					*node:parent.bld_panel.bld_space.<2>.alert@invis = false
				}
			}
		}

		BaseSection aircraft
		{
			{
				onClick		{ call *node:this.funcClick }
			}
			string label { string "_Aircraft" }
			button icon { icon "icons/aircraft" }
			func funcClick { cmd "ui_push aircraft_equip;" }

			// Aircraft added as child nodes to this panel
			panel aircraft_list
			{
				{
					pos 			"29 48"
					size 			"250 48"
					invis			true
					layout			LAYOUT_COLUMN
					layoutColumns	5
					layoutMargin	0
				}
			}
		}
		BaseSection soldiers
		{
			{
				onClick		{ call *node:this.funcClick }
			}
			string label { string "_Soldiers" }
			button icon { icon "icons/crouch" }
			func funcClick { cmd "ui_push equipment -1;" }
		}
		BaseSection buysell
		{
			{
				onClick		{ call *node:this.funcClick }
			}
			string label { string "_Market" }
			button icon { icon "icons/market" }
			func funcClick { cmd "ui_push market;" }
		}
		BaseSection research
		{
			{
				onClick		{ call *node:this.funcClick }
			}
			string label { string "_Research" }
			button icon { icon "icons/research" }
			func funcClick { cmd "ui_push research;" }

			// Aircraft added as child nodes to this panel
			panel research_list
			{
				{
					pos 			"29 48"
					size 			"250 28"
					layout			LAYOUT_TOP_DOWN_FLOW
					layoutMargin	0
					ghost			true
				}
			}
		}
		BaseSection production
		{
			{
				onClick		{ call *node:this.funcClick }
			}
			string label { string "_Production" }
			button icon { icon "icons/wrench" }
			func funcClick { cmd "ui_push production;" }

			// Production added as child nodes to this panel
			panel production_list
			{
				{
					pos 			"29 48"
					size 			"250 28"
					layout			LAYOUT_TOP_DOWN_FLOW
					layoutMargin	0
					ghost			true
				}
			}
		}
		BaseSection containment
		{
			{
				onClick		{ call *node:this.funcClick }
			}
			string label { string "_Alien Containment" }
			button icon { icon "icons/aliens" }
			data state { string "true" }
			func funcClick { cmd "ui_push aliencont;" }
		}

		BaseSection hospital
		{
			{
				onClick		{ call *node:this.funcClick }
			}
			string label { string "_Hospital" }
			button icon { icon "icons/hos_bed" }
			data state { string "true" }
			func funcClick { cmd "ui_push hospital;" }
		}

		// Hide/show the other base sections
		func setOthersInvis
		{
			*node:parent.aircraft@invis = <1>
			*node:parent.soldiers@invis = <1>
			*node:parent.buysell@invis = <1>
			*node:parent.research@invis = <1>
			*node:parent.production@invis = <1>
			*node:parent.containment@invis = <1>
			*node:parent.hospital@invis = <1>
		}
	}

	// Back to geoscape
	Geoscape geoscape_button {
		onClick { cmd "ui_pop;" }
	}

	data last_section { string "" }
	/**
	 * @brief Used to enable/disable the overall base buttons
	 * @param <1> button
	 * @param <2> state
	 * @param <3> tooltip
	 * @TODO: handle hospital/transfer
	 * @TODO: some buttons don't have windows
	 * @TODO: Check tooltips only show when disabled
	 */
	confunc update_basebutton
	{
		if (<2> eq "true") {
			if (<1> eq "hire") {
				*node:parent.sections.soldiers.bg@bgcolor = "1 1 1 0.2"
				*node:parent.sections.soldiers@onMouseEnter = { }
				*node:parent.sections.soldiers@onMouseLeave = { }
				*node:parent.sections.soldiers@onClick = { }
				*node:parent.sections.soldiers@tooltip = "<3>"
				*node:parent.sections.soldiers@disabled = true
			} elif (<1> eq "transfer") {
			} else {
				*node:parent.sections.<1>.bg@bgcolor = "1 1 1 0.2"
				*node:parent.sections.<1>@onMouseEnter = { }
				*node:parent.sections.<1>@onMouseLeave = { }
				*node:parent.sections.<1>@onClick = { }
				*node:parent.sections.<1>@tooltip = "<3>"
				*node:parent.sections.<1>@disabled = true
			}
		} else {
			if (<1> eq "hire") {
				*node:parent.sections.soldiers.bg@bgcolor = "0.527 0.6 0.21 0.2"
				*node:parent.sections.soldiers@onMouseEnter = { *node:this.bg@bgcolor = "0.527 0.6 0.21 0.4" *node:this.icon@background = "icons/circle" }
				*node:parent.sections.soldiers@onMouseLeave = { *node:this.bg@bgcolor = "0.527 0.6 0.21 0.2" *node:this.icon@background = "icons/circle0" }
				*node:parent.sections.soldiers@onClick = { call *node:this.funcClick }
				*node:parent.sections.soldiers@tooltip = ""
				*node:parent.sections.soldiers@disabled = false
			} elif (<1> eq "transfer") {
			} else {
				*node:parent.sections.<1>.bg@bgcolor = "0.527 0.6 0.21 0.2"
				*node:parent.sections.<1>@onMouseEnter = { *node:this.bg@bgcolor = "0.527 0.6 0.21 0.4" *node:this.icon@background = "icons/circle" }
				*node:parent.sections.<1>@onMouseLeave = { *node:this.bg@bgcolor = "0.527 0.6 0.21 0.2" *node:this.icon@background = "icons/circle0" }
				*node:parent.sections.<1>@tooltip = ""
				*node:parent.sections.<1>@disabled = false
				*node:parent.sections.<1>@onClick = { call *node:this.funcClick }
			}
		}
	}

	/**
	 * @brief Show the facilities panel
	 * @param <1> true (show) or false (hide)
	 */
	confunc show_facility
	{
		if (<1> eq "true") {
			// Show capacity for all base buildings
			cmd "base_building_space;"

			// Set the state for subsequent clicks
			*node:parent.sections.facilities.state@string = "false"
		} else {
			// Resets list selection
			cmd "reset_building_current 0;"

			// Set the state/invis for subsequent clicks
			*node:parent.sections.facilities.state@string = "true"

			// Show capacity for just main base buildings
			cmd "base_init;"
		}

		// Hide/show the other base sections
		call *node:parent.sections.setOthersInvis(<1>)
	}

	/**
	 * @brief Current employees of a particular base
	 * @param <1> number of soldiers
	 * @param <2> number of pilots
	 * @param <3> number of scientists
	 * @param <4> number of workers
	 */
	confunc current_employees
	{
		*node:parent.base_container.cur_employees.soldiers.data@string = "<1>"
		*node:parent.base_container.cur_employees.pilots.data@string = "<2>"
		*node:parent.base_container.cur_employees.scientists.data@string = "<3>"
		*node:parent.base_container.cur_employees.workers.data@string = "<4>"
	}

	confunc restore_base_state {
		if ( *node:parent.sections.facilities.state@string eq "false" ) {
			cmd "base_building_space;"
		}
	}

	/**
	 * @brief Clears the list of aircraft in the base view that was filled by @c show_aircraft
	 */
	confunc clear_aircraft {
		*node:root.aircraft_count@integer = 0
		*node:parent.sections.aircraft@size	= "298 48"
		*node:parent.sections.aircraft.aircraft_list@invis = true
		call *node:parent.sections.aircraft.aircraft_list@removeAllChild
	}

	/**
	 * @brief The amount of aircraft that are currently visibile in the base view
	 */
	data aircraft_count
	{
		integer 0
	}

	/*
	 * @brief Show the base's aircraft
	 * @param <1> id global aircraft idx
	 * @param <2> name
	 * @param <3> aircraft type
	 * @param <4> status
	 * @param <5> aircraft in base?
	 */
	confunc show_aircraft
	{
		if ( *node:root.aircraft_count@integer < 5 ) {
			*node:parent.sections.aircraft@size	= "298 96"
			call *node:parent.sections.aircraft.aircraft_list@createChild("<1>", "AircraftIcon")
			*node:parent.sections.aircraft.aircraft_list.<1>.icon@tooltip = "<2> (<4>)"
			if (<5> eq 1) {
				*node:parent.sections.aircraft.aircraft_list.<1>.icon@icon = "icons/<3>"
			} else {
				*node:parent.sections.aircraft.aircraft_list.<1>.icon@icon = "icons/<3>_off"
			}
			*node:parent.sections.aircraft.aircraft_list.<1>.aircraft_id@string = <node:root.aircraft_count@integer>
			*node:parent.sections.aircraft.aircraft_list.<1>.icon@onClick = { cmd "ui_push aircraft_equip \"<node:parent.aircraft_id@string>\";" }
			*node:parent.sections.aircraft.aircraft_list@invis = false
			/*
			THEN ADD THIS TO AIRCRAFT onWindowOpened
			if (<1> ne "") {
				*cvar:mn_aircraft_id = <1>
				cmd "ui_aircraft_select \"<1>\";"
			}
			*/
		}
		*node:root.aircraft_count@integer = ( *node:root.aircraft_count@integer + 1 )
	}

	/**
	 * @brief Show a research item in the base overview (not used in default UI)
	 * @param <1> id
	 * @param <2> name
	 * @param <3> number of scientists working on it
	 * @param <4> percent complete
	 */
	confunc clear_research
	{
		*node:parent.sections.research@size	= "298 48"
		call *node:parent.sections.research.research_list@removeAllChild
	}
	confunc show_research
	{
		*node:parent.sections.research@size	= "298 80"
		call *node:parent.sections.research.research_list@createChild("<1>", "ProgressBar")
		*node:parent.sections.research.research_list.<1>.label@string = _<2>
		*node:parent.sections.research.research_list.<1>.data_bar@current = <4>
	}

	/*
	 * @brief Show the production item closest to completion in the base overview (not used in default UI)
	 * @param <1> id
	 * @param <2> name
	 * @param <3> percent complete
	 */
	confunc clear_production
	{
		*node:parent.sections.production@size	= "298 48"
		call *node:parent.sections.production.production_list@removeAllChild
	}
	confunc show_production
	{
		*node:parent.sections.production@size	= "298 80"
		call *node:parent.sections.production.production_list@createChild("<1>", "ProgressBar")
		*node:parent.sections.production.production_list.<1>.label@string = _<2>
		*node:parent.sections.production.production_list.<1>.data_bar@current = <3>
	}

	confunc restore_section
	{
		if ((exists *node:parent.sections.<1>) && (*node:parent.sections.<1>@disabled != true)) {
			call *node:parent.sections.<1>@funcClick
		} elif ((<1> eq "hire") && (*node:parent.sections.soldiers@disabled != true)) {
			cmd "ui_push employees <cvar:mn_hiretype>;"
		}
	}

	confunc base_next_s
	{
		cmd "ui_pop;mn_base_next;"
		*node:parent.last_section@string = "<1>"
	}

	confunc base_prev_s
	{
		cmd "ui_pop;mn_base_prev;"
		*node:parent.last_section@string = "<1>"
	}

	confunc base_select_s
	{
		cmd "ui_pop;mn_base_select <1>;"
		*node:parent.last_section@string = "<2>"
	}

	/**
	 * @brief Reset base status/display info when the player switches to a new base
	 * or comes back from another window
	 */
	func reset_base
	{
		cmd "show_facility false;"
	}

	// Fuzzy screen effect (see assets.ufo)
	fuzzyScreen overlay { }

	func onWindowActivate
	{
		call *node:root.onWindowOpened
	}

	func onWindowOpened
	{
		// Get the parameters needed for this window
		cmd "base_init;"

		// Close all panels and reset base to default position
		call *node:root.reset_base

		// Try to restore the last base section opened
		if (*node:root.last_section@string ne "") {
			cmd "restore_section <node:root.last_section@string>;"
			*node:root.last_section@string = ""
		}
	}
}
