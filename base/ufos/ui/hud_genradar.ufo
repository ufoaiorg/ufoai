// ==================
// GENRADAR HUD
// hud used to generate radar maps
// run the game with "ufo +set vid_mode -1 +set vid_width 1024 +set vid_height 1024"
// use the command "gen_radarmap"
// ==================

window genradarhud extends hud
{
	{
		fill		true
		starlayout	true
	}

	func showinventory
	{
		cmd "ui_push hudinv;"
	}

	// ==================
	// render rectangle
	// ==================
	battlescape render
	{
		pos		"0 0"
		size	"1024 768"
		align	LAYOUTALIGN_FILL
	}

	// ==================
	// generator interface
	// ==================

	confunc gen_radarmap {
		cmd "ui_push genradarhud;"
	}

	panel mapborder {
		bordercolor	"1 0 0 1"
		border		2
	}

	panel control {
		{
			pos		"0 0"
			size	"255 200"
			align	LAYOUTALIGN_TOPLEFT
		}

		button reinit
		{
			pos			"5 5"
			size		"250 30"
			image		"ui/button_green_small"
			font		"f_small"
			color		"0 0.5 0 1"
			selectcolor	"1 1 1 1"
			string		"_Reinit"
			onClick {
				*node:root.mapborder@invis = true
				cmd "r_isometric 1;r_screenshot_format png;centercamera;camsetangles 90 90;camsetzoom 0.5;"
			}
		}

		button preview
		{
			pos			"5 40"
			size		"250 30"
			image		"ui/button_green_small"
			font		"f_small"
			color		"0 0.5 0 1"
			selectcolor	"1 1 1 1"
			string		"_Preview"
			onClick {
				*node:root.control@invis = true
				*node:root.mapborder@invis = true
				cmd "ui_genpreviewradarmap;"
			}
		}

		button genlevel
		{
			pos			"5 75"
			size		"250 30"
			image		"ui/button_green_small"
			font		"f_small"
			color		"0 0.5 0 1"
			selectcolor	"1 1 1 1"
			string		"_Gen this level"
			onClick {
				*node:root.mapborder@invis = true
				cmd "ui_genradarmap;"
			}
		}

		button genalllevels
		{
			pos			"5 110"
			size		"250 30"
			image		"ui/button_green_small"
			font		"f_small"
			color		"0 0.5 0 1"
			selectcolor	"1 1 1 1"
			string		"_Gen all levels"
			onClick {
				*cvar:scr_showcursor = 0
				*node:root.control@invis = true
				*node:root.mapborder@invis = true
				cmd "ui_genallradarmap;"
			}
		}
	}

	confunc	mn_radarhud_setmapborder
	{
		*node:root.mapborder@left = ( <1> - 10 )
		*node:root.mapborder@top = ( <2> - 10 )
		*node:root.mapborder@width = ( <3> + 20 )
		*node:root.mapborder@height = ( <4> + 20 )
		*node:root.mapborder@invis = false
		*node:root.control@invis = false
	}

	confunc	mn_radarhud_reinit
	{
		*node:root.control@invis = false
		*node:root.mapborder@invis = false
		*cvar:scr_showcursor = 1
	}

	func onWindowOpened {
		cmd "mn_radarhud_reinit;"
		cmd "echo Run the game in 1024x1024:;"
		cmd "echo ufo +set vid_mode -1 +set vid_width 1024 +set vid_height 1024;"
		cmd "r_isometric 1;centercamera;camsetangles 90 90;camsetzoom 0.5;"
		*cvar:cl_showactors = 0
	}

	func onWindowClosed {
		*cvar:scr_showcursor = 1
		*cvar:cl_showactors = 1
	}
}
