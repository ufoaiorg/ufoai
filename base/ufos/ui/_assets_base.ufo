// Back to geoscape button
component Geoscape extends button
{
	pos				"-17 -17"
	size			"100 100"
	icon			"icons/earthlrg"
	background		"icons/circlelrg0"
	tooltip			"_Back"
	onMouseEnter	{ *node:this@background = "icons/circlelrg" }
	onMouseLeave	{ *node:this@background = "icons/circlelrg0" }
	// handle onClick in the menu that adds this node
}

// Main panel button for base sections
component BaseSection extends panel
{
	{
		size		"298 48"
		onMouseEnter	{
			*node:this.bg@bgcolor = "0.527 0.6 0.21 0.4"
			*node:this.icon@background = "icons/circle"
		}
		onMouseLeave	{
			*node:this.bg@bgcolor = "0.527 0.6 0.21 0.2"
			*node:this.icon@background = "icons/circle0"
		}
	}
	panel bg
	{
		{
			pos		"0 8"
			size	"298 32"
			bgcolor	"0.527 0.6 0.21 0.2"
			ghost	true
		}
	}
	button icon
	{
		pos			"12 0"
		size 		"48 48"
		background	"icons/circle0"
		ghost		true
		// else the button is disabled
		onClick		{ }
	}
	string label
	{
		pos				"75 8"
		size			"170 32"
		contentalign	ALIGN_CL
		color			"0.56 0.81 0.76 1"
		ghost			true
	}
}

// Stats bar for building space
component BuildingSpace extends panel
{
	{
		size	"298 30"
		onMouseEnter
		{
			*node:this.label@color = "0.56 0.81 0.76 1"
			*node:this.data@color = "0.56 0.81 0.76 1"
			*node:this.alert@icon = "icons/windowplusb_hovered"
		}
		onMouseLeave
		{
			*node:this.label@color = "0.56 0.81 0.76 0.5"
			*node:this.data@color = "0.56 0.81 0.76 0.5"
			*node:this.alert@icon = "icons/windowplusb"
		}
		onClick			{ cmd "buildings_click <node:this.id@string>;" }
	}
	data id {}
	button smlicon
	{
		pos		"16 0"
		size	"17 17"
		ghost	true
	}
	string label
	{
		string			""
		pos				"35 0"
		size			"120 20"
		font			"f_verysmall"
		contentalign	ALIGN_LL
		color			"0.56 0.81 0.76 0.7"
		ghost			true
	}
	string data
	{
		string			""
		pos				"155 0"
		size			"107 20"
		font			"f_verysmall"
		contentalign	ALIGN_LR
		color			"0.56 0.81 0.76 0.7"
		ghost			true
	}
	texture data_bg
	{
		pos			"16 18"
		size		"246 6"
		src			"ui/bar_bg"
		ghost		true
	}
	bar data_bar
	{
		pos			"16 18"
		size		"246 6"
		color		"0.56 0.81 0.76 1"
		current		0
		max			1
		readonly	true
		ghost		true
	}
	button alert
	{
		icon			"icons/windowplusb"
		pos				"266 12"
		size			"18 18"
//		onClick			{ cmd "buildings_click <node:parent.id@string>;" }
		ghost		true
	}
	// <1> the used part
	// <2> the max space/capacity
	func set {
			*node:parent.data_bar@current = <1>
			*node:parent.data_bar@max = <2>
			*node:parent.data@string = <1>/<2>
	}
}

// Small base selector layout
component BaseLayoutMini extends baselayout
{
	pos				"0 0"
	size			"16 16"
	color			"0.56 0.81 0.76 0.5"
	bgcolor			"0.56 0.81 0.76 0.1"
	padding			1
	onMouseEnter	{ *node:this@color = "0.56 0.81 0.76 1" }
	onMouseLeave	{ *node:this@color = "0.56 0.81 0.76 0.5" }
	onClick			{ cmd "ui_pop; mn_base_select <baseid>;" }
}


// Aircraft image/button
component AircraftIcon extends panel
{
	{
		pos		"0 0"
		size		"48 48"
	}
	button icon
	{
		pos		"0 0"
		size	"48 48"
	}
	data aircraft_id { }
}

// Research/Production
component ProgressBar extends panel
{
	{
		pos			"0 0"
		size 		"250 28"
	}
	string label
	{
		pos				"0 0"
		size			"250 20"
		font			"f_verysmall"
		contentalign	ALIGN_LL
		color			"0.56 0.81 0.76 1"
		ghost			true
	}
	texture data_bg
	{
		pos			"0 20"
		size		"224 6"
		src			"ui/bar_bg"
		ghost		true
	}
	bar data_bar
	{
		pos			"0 20"
		size		"224 6"
		color		"0.56 0.81 0.76 1"
		current		0
		max			100
		ghost		true
	}
}


// Employee count
component EmployeeCount extends panel
{
	{
		size	"110 48"
	}
	button icon
	{
		pos		"0 0"
		size	"48 48"
		ghost	true
	}
	string data
	{
		pos				"36 0"
		size			"74 48"
		color			"0.56 0.81 0.76 1"
		font			"f_small_bold"
		contentalign	ALIGN_CL
		ghost			true
	}
	string label
	{
		pos				"36 27"
		size			"74 17"
		color			"0.56 0.81 0.76 1"
		font			"f_verysmall"
		contentalign	ALIGN_UL
		ghost			true
	}
}

// Hospital item (per employee)
component HospitalItem extends panel {
	{
		pos				"0 0"
		size 			"670 28"
		bgcolor			"0.56 0.81 0.76 0.1"
		onMouseEnter	{ *node:this@bgcolor = "0.56 0.81 0.76 0.2" }
		onMouseLeave	{ *node:this@bgcolor = "0.56 0.81 0.76 0.1" }
	}
	data id {
		integer		0
	}
	bar health {
		pos		"0 0"
		size	"670 28"
		color	"0.56 0.81 0.76 0.3"
		current 0
		max		100
		readonly	true
		ghost	true
	}
	string name {
		pos				"30 0"
		size			"280 28"
		color			"0.56 0.81 0.76 1"
		longlines		LONGLINES_PRETTYCHOP
		contentalign	ALIGN_CL
		ghost			true
	}
	string type {
		pos			"550 8"
		size		"100 15"
		color		"0.56 0.81 0.76 1"
		font		"f_verysmall"
		contentalign	ALIGN_CC
		padding		0
		ghost		true
	}

	panel wounds {
		{
			pos 	"310 0"
			size	"80 28"
			ghost		true
		}
		// TODO: use image instead of button â€” when it works with packed icons
		button wound_humanoid_head {
			pos			"0 6"
			size		"16 16"
			invis		true
		}
		button wound_humanoid_torso {
			pos			"19 6"
			size		"16 16"
			invis		true
		}
		button wound_humanoid_arms {
			pos			"38 6"
			size		"16 16"
			invis		true
		}
		button wound_humanoid_legs {
			pos			"57 6"
			size		"16 16"
			invis		true
		}
		button wound_default_body {
			pos			"0 6"
			size		"16 16"
			invis		true
		}
	}

	container implant {
		pos "390 0"
		border			"1"
		bordercolor		"0.56 0.81 0.76 1"
	}

	image icon {
		pos 	"510 0"
		size	"28 28"
	}

	image assigned {
		pos		"1 2"
		size	"24 24"
		src		"icons/icon_aircraftsml"
		invis	true
	}
}

// Research item (items researched in base)
component ResearchItem extends panel
{
	{
		pos				"0 0"
		size 			"713 28"
		bgcolor			"0.56 0.81 0.76 0.1"
		onMouseEnter	{ *node:this@bgcolor = "0.56 0.81 0.76 0.2" }
		onMouseLeave	{ *node:this@bgcolor = "0.56 0.81 0.76 0.1" }
		onClick			{
			cmd "ui_research_select <node:this.id@string>;"
		}
	}
	data id {
		integer		-1
		string		""
	}
	bar status
	{
		pos		"0 0"
		size	"713 28"
		color	"0.56 0.81 0.76 0.3"
		current 0
		max		100
		ghost	true
	}
	button ufopaedia
	{
		icon	"icons/windowinfo"
		tooltip	"_View UFOPaedia entry"
		pos		"5 5"
		size	"18 18"
		onClick {
			cmd "ui_research_select <node:parent.id@string>;"
			cmd "ui_push ufopedia <node:parent.id@string>;"
		}
	}
	string progress_label {
		pos			"615 8"
		size		"40 15"
		color		"0.56 0.81 0.76 1"
		font		"f_verysmall"
		contentalign	ALIGN_CR
		padding		0
		ghost		true
	}
	string title
	{
		pos				"30 0"
		size			"480 28"
		color			"0.56 0.81 0.76 1"
		contentalign	ALIGN_CL
		ghost			true
	}

	panel rsrc_control {
		{
			pos 	"480 0"
			size	"145 32"
		}
		string scientists
		{
			pos				"60 0"
			size			"35 28"
			color			"0.56 0.81 0.76 1"
			contentalign	ALIGN_CL
			ghost			true
		}
		button stop {
			icon		"icons/speed_stop"
			tooltip		"_Stop researching this project"
			pos			"0 5"
			size		"18 18"

			onclick {
				cmd "ui_research_select <node:parent.parent.id@string>;ui_research_stop <node:parent.parent.id@string>;"
			}
		}
		button max {
			icon		"icons/speed_play3"
			tooltip		"_Assign all free scientists on this project"
			pos			"25 4"
			size		"18 18"
			onclick {
				cmd "ui_research_select <node:parent.parent.id@string>; ui_research_max <node:parent.parent.id@string>;"
			}
		}
		button remove_scientist
		{
			icon	"icons/windowminusb"
			tooltip	"_Assign less scientists on this project"
			pos		"100 5"
			size	"18 18"
			onClick			{
				cmd "ui_research_select <node:parent.parent.id@string>; ui_research_change <node:parent.parent.id@string> -1;"
			}
		}
		button add_scientist
		{
			icon	"icons/windowplusb"
			tooltip	"_Assign more scientists on this project"
			pos		"125 5"
			size	"18 18"
			onClick			{
				cmd "ui_research_select <node:parent.parent.id@string>; ui_research_change <node:parent.parent.id@string> 1;"
			}
		}
	}

	// External research item (items researched in other bases)
	panel rsrc_baselink {
		{
			pos 	"480 0"
			size	"145 32"
			invis	true
		}
		string basename
		{
			pos				"0 0"
			tooltip			"_Currently in research on another base"
			size			"145 28"
			color			"1 0.874 0.294 1"
			contentalign	ALIGN_CL
			ghost			true
		}
	}

	panel rsrc_missing {
		{
			pos 	"480 0"
			size	"145 32"
			invis	true
		}
		string note {
			string		"_N/A"
			tooltip		"_An artifact or prerequisite is missing for this topic"
			pos			"0 0"
			size		"145 28"
			color		"1 0.874 0.294 1"
			contentalign	ALIGN_CL

		}
		button show {
			tooltip		"_Expand/Collapse"
			icon		"icons/scroll_bottom"
			pos			"120 4"
			size		"25 25"
			invis		true
		}
	}
	/** @TODO implement research requirements listing */
}

// Market category label
component MarketCategory extends panel
{
	{
		size 			"713 28"
		bgcolor			"0.56 0.81 0.76 0.3"
		onMouseEnter	{ *node:this@bgcolor = "0.56 0.81 0.76 0.4" }
		onMouseLeave	{ *node:this@bgcolor = "0.56 0.81 0.76 0.3" }
	}
	string name
	{
		pos				"30 0"
		size			"400 28"
		color			"0.56 0.81 0.76 1"
		contentalign	ALIGN_CL
		ghost			true
	}
}

// Market category list
component MarketList extends panel
{
	size			"713 0"
	layout			LAYOUT_TOP_DOWN_FLOW
	layoutMargin	2
}
// Market item
component MarketItem extends panel
{
	{
		pos				"0 0"
		size 			"713 28"
		bgcolor			"0.56 0.81 0.76 0.1"
		onMouseEnter	{ *node:this@bgcolor = "0.56 0.81 0.76 0.15" }
		onMouseLeave	{ *node:this@bgcolor = "0.56 0.81 0.76 0.1" }
		onClick			{ cmd "ui_market_select <node:this.id@string>;" }
	}
	data id { }
	data rate { integer 1 }

	// UFOPaedia link for each item
	button ufopaedia
	{
		icon			"icons/windowinfo"
		tooltip			"_View UFOPaedia entry"
		pos				"5 5"
		size			"18	18"
		onClick			{
			cmd "ui_market_showinfo \"<node:parent.id@string>\";"
			cmd "ui_market_openpedia;"
		}
	}
	string name
	{
		pos				"30 0"
		size			"290 28"
		color			"0.56 0.81 0.76 0.7"
		contentalign	ALIGN_CL
		ghost			true
	}
	// Count of items in base
	string base
	{
		pos				"320 0"
		size			"80 28"
		color			"0.56 0.81 0.76 1"
		contentalign	ALIGN_CR
		ghost			true
	}
	// Buy items
	spinner buy {
		pos					"406 6"
		size				"74 16"
		topIcon				"icons/arrowtext_lft"
		mode				SPINNER_ONLY_INC
		delta				1
		shiftincreasefactor	10.0
		onMouseEnter		{ *node:this@topIcon = "icons/arrowtext_lft0" }
		onMouseLeave		{ *node:this@topIcon = "icons/arrowtext_lft" }
		onChange			{
			cmd "ui_market_buy \"<node:parent.id@string>\" <lastdiff>;"
			cmd "ui_market_fill <cvar:ui_market_category>;"
			cmd "ui_market_select \"<node:parent.id@string>\";"
		}
	}
	string buy_price {
		pos				"406 6"
		size			"74 16"
		color			"0 0 0 1"
		font			"f_verysmall_bold"
		contentalign	ALIGN_CR
		ghost			true
	}
	checkbox autosell
	{
		tooltip				"_Lock current stock level"
		current				0
		iconChecked			"icons/windowlock"
		iconUnchecked		"icons/windowlock_light"
		iconIndeterminate	"icons/windowlock_light" // @TODO: What does this do?
		pos					"484 5"
		size				"18 18"
		invis				false
		onMouseEnter		{ *node:this@iconUnchecked = "icons/windowlock" }
		onMouseLeave		{ *node:this@iconUnchecked = "icons/windowlock_light" }
		onClick				{ cmd "ui_market_setautosell \"<node:parent.id@string>\" <node:this@string>;"}
	}
	// Sell items
	spinner sell {
		pos					"508 6"
		size				"74 16"
		topIcon				"icons/arrowtext_rgt"
		mode				SPINNER_ONLY_DEC
		delta				1
		shiftincreasefactor	10.0
		onMouseEnter		{ *node:this@topIcon = "icons/arrowtext_rgt0" }
		onMouseLeave		{ *node:this@topIcon = "icons/arrowtext_rgt" }
		onChange			{
			cmd "ui_market_buy \"<node:parent.id@string>\" <lastdiff>;"
			cmd "ui_market_fill <cvar:ui_market_category>;"
			cmd "ui_market_select \"<node:parent.id@string>\";"
		}
	}
	string sell_price {
		pos				"508 6"
		size			"74 16"
		color			"0 0 0 1"
		font			"f_verysmall_bold"
		contentalign	ALIGN_CL
		ghost			true
	}
	// Count of items in the market
	string market
	{
		pos				"586 0"
		size			"100 28"
		color			"0.56 0.81 0.76 1"
		contentalign	ALIGN_CL
		ghost			true
	}
}

// Base name and container panel
component BaseContainerPanel extends panel
{
	{
		pos				"5 5"
		size			"723 758"
		border			"2"
		bordercolor		"0.56 0.81 0.76 1"
	}

	// @TODO: make next/prev base btns work
	button prev
	{
		icon			"icons/windowprev"
		tooltip			"_Previous base"
		pos				"90 6"
		size			"18	18"
		onClick			{ cmd "ui_pop; mn_base_prev;" }
		onWheelUp		{ cmd "ui_pop; mn_base_next;" }
		onWheelDown		{ cmd "ui_pop; mn_base_prev;" }
	}
	button next
	{
		icon			"icons/windownext"
		tooltip			"_Next base"
		pos				"115 6"
		size			"18	18"
		onClick			{ cmd "ui_pop; mn_base_next;" }
		onWheelUp		{ cmd "ui_pop; mn_base_next;" }
		onWheelDown		{ cmd "ui_pop; mn_base_prev;" }
	}
	// Base name
	textentry name
	{
		string		*cvar:mn_base_title
		pos			"135 0"
		size		"465 32"
		font		"f_small_bold"
		color			"0.56 0.81 0.76 1"
		contentalign	ALIGN_CL
		onChange 	{ cmd "base_changename" } 	// stores the value of the cvar in the base structure
	}
	// Base selector
	panel base_selector
	{
		{
			pos		"440 7"
			size	"160 16"
			layout			LAYOUT_COLUMN
			layoutColumns	8
			layoutMargin	4
		}
		BaseLayoutMini base0 { baseid 0 }
		BaseLayoutMini base1 { baseid 1 }
		BaseLayoutMini base2 { baseid 2 }
		BaseLayoutMini base3 { baseid 3 }
		BaseLayoutMini base4 { baseid 4 }
		BaseLayoutMini base5 { baseid 5 }
		BaseLayoutMini base6 { baseid 6 }
		BaseLayoutMini base7 { baseid 7 }
	}
	string credits
	{
		string		*cvar:mn_credits
		pos			"600 0"
		size		"120 32"
		font		"f_small_bold"
		color			"0.56 0.81 0.76 1"
		contentalign	ALIGN_CR
	}
	panel mainBody
	{
		{
			pos				"4 30"
			size			"715 534"
			border			"2"
			bordercolor		"0.527 0.6 0.21 1"
		}
		// Title bar
		panel header
		{
			{
				pos			"1 1"
				size		"713 32"
				bgcolor		"0.527 0.6 0.21 0.2"
			}
			string title
			{
				string			"Dummy"
				pos				"90 1"
				size			"593 32"
				contentalign	ALIGN_CL
				color			"0.56 0.81 0.76 1"
			}
			button back
			{
				icon			"icons/system_close"
				tooltip			"_Back to base"
				pos				"683 7"
				size			"18	18"
				onClick			{ cmd "ui_pop;" }
			}
		}
	}
}

window BaseMgmtWindow
{
	{ size	"1024 768" }

	BaseContainerPanel base_container {}

	// Back to geoscape
	Geoscape geoscape_button { }

	// Fuzzy screen effect (see assets.ufo)
	fuzzyScreen overlay { }
}
