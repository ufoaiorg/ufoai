// ==================
// MAP MENU
// ==================

component panel cmpGeoscapeDocklet {
	{
		pos		"0 0"
		size	"280 60"
		align	LAYOUTALIGN_TOP
	}

	image bg {
		src			"geoscape/docklet_bg"
		size			"280 60"
		onMouseEnter	{ *node:this@image = "geoscape/docklet_bg_hovered" }
		onMouseLeave	{ *node:this@image = "geoscape/docklet_bg" }
		onClick			{ cmd "map_selectobject <node:parent.type@string> <node:parent.id@string>;" }
	}

	model siteModel {
		pos		"5 5"
		size	"50 50"
		autoscale	true
		ghost	true
		angles	"0 0 150"
	}

	string	message {
		pos		"65 5"
		size	"210 50"
		ghost	true
	}

	/** store the id to find the object */
	string type {
		size	"0 0"
		invis	true
	}

	/** store the id to find the object */
	string id {
		size	"0 0"
		invis	true
	}
}

window geoscape
{
	// ==================
	// background
	// ==================
	{
		fill		true
		starlayout	true
	}

	map themap
	{
		pos				"0 0"
		size			"1024 768"
		onRClick		{ cmd "mn_mapaction_reset;" }
		align			LAYOUTALIGN_FILL
		paddingRight	256
	}

	confunc clean_geoscape_object {
		call *node:root.messages.dock@removeAllChild
	}

	/**
	 * @param<1> type [mission|ufo]
	 * @param<2> id (internal, to identify the docklet object)
	 * @param<3> id (external, to identify the linked object)
	 * @param<4> model
	 * @param<5> text
	 */
	confunc add_geoscape_object
	{
		call *node:root.messages.dock@createChild ( "<1><2>" , "cmpGeoscapeDocklet" )
		*node:root.messages.dock.<1><2>.type@string = <1>
		*node:root.messages.dock.<1><2>.id@string = <2>
		*node:root.messages.dock.<1><2>.siteModel@src = <4>
		*node:root.messages.dock.<1><2>.message@string = <5>
	}

	confunc messagesystem_changemode
	{
		/* one line */
		if ( *cvar:mn_messagesystem == 0 ) {
			*node:root.messages.messagesystem@width = ( *cvar:ui_sys_screenwidth - 256 )
			*node:root.messages.messagesystem@height	= 20
			*node:root.messages.messagesystem.scroll@invis = true
		}
		/* 6 lines */
		if ( *cvar:mn_messagesystem == 1 ) {
			*node:root.messages.messagesystem@width = ( *cvar:ui_sys_screenwidth - 256 )
			*node:root.messages.messagesystem@height	= 120
			*node:root.messages.messagesystem.scroll@invis = true
		}
		/* 12 lines */
		if ( *cvar:mn_messagesystem == 2 ) {
			*node:root.messages.messagesystem@width = ( *cvar:ui_sys_screenwidth - 256 )
			*node:root.messages.messagesystem@height	= 240
			*node:root.messages.messagesystem.scroll@invis = true
		}
		/* full screen */
		if ( *cvar:mn_messagesystem == 3 ) {
			*node:root.messages.messagesystem@width = ( *cvar:ui_sys_screenwidth - 256 )
			*cvar:foo = ( *cvar:ui_sys_screenwidth - 256 )
			*node:root.messages.messagesystem@height = 768
			*node:root.messages.messagesystem.scroll@invis = false
			cmd "game_timestop;"
		}

	}

	// ==================
	// RIGHT PANEL
	// ==================

	panel control {
		{
			pos		"768 0"
			size	"256 768"
			align	LAYOUTALIGN_RIGHT
		}

		image back
		{
			src		"ui/geo_menu"
			pos			"0 0"
		}

		button bt_messagesystem
		{
			// @todo: better -more verbose- tooltip
			tooltip		"_Open up messages"
			padding		0
			icon		change_height
			pos			"6 6"
			size		"23 23"
			onClick	{
				cmd "add mn_messagesystem 1;"
				cmd "mod mn_messagesystem 4;"
				cmd "messagesystem_changemode"
			}
			onRClick	{
				cmd "set mn_messagesystem 3;"
				cmd "messagesystem_changemode"
			}
		}

		// ==================
		// texts
		// ==================

		string daytime
		{
			string		*cvar:mn_mapdaytime
			pos			"31 304"
			size		"195 20"
			textalign	ALIGN_UC
		}

		text description
		{
			dataid		TEXT_STANDARD
			pos			"32 154"
			size		"172 144"
			lineheight	18
			tabwidth	70
			rows		8
			onViewChange {
				*node:parent.description_scroll@fullsize = <fullsize>
				*node:parent.description_scroll@current = <viewpos>
				*node:parent.description_scroll@viewsize = <viewsize>
			}
		}

		vscrollbar description_scroll {
			image		"ui/scrollbar_v_green"
			pos			"205 151"
			height		"144"
			current		0
			viewsize	8
			fullsize	8
			hidewhenunused	true
			onChange {
				*node:parent.description@viewpos = <current>
			}
		}

		// ==================
		// time
		// ==================

		string date
		{
			string		"*cvar:mn_mapdate *cvar:mn_maptime"
			pos			"31 30"
			size		"195 20"
			textalign	ALIGN_UC
			color		"1 1 1 1"
			font		"f_small"
		}

		string txt_credits_caption
		{
			string		"_CREDITS"
			pos			"40 54"
			size		"75 25"
			font		"f_small"
		}

		string txt_credits
		{
			string		*cvar:mn_credits
			tooltip		"_Organisation capital"
			pos			"112 54"
			size		"100 25"
			font		"f_small"
			textalign	ALIGN_UR
		}

		panel time_panel {
			{
				pos		"0 90"
				size	"256 55"
			}

			func faster {
				cmd "game_timefast;"
			}
			func slower {
				cmd "game_timeslow;"
			}

			radiobutton bt_timestop0
			{
				tooltip		"_Pause"
				cvar		*cvar:mn_timelapse_id	value "0"
				icon		speed_stop
				pos			"48 10"
				size		"20 20"
				onChange	{ cmd "game_settimeid <value>;" }
				onWheelUp	{ cmd "game_timefast;" }
				onWheelDown	{ cmd "game_timeslow;" }
			}

			radiobutton bt_timeplay1
			{
				tooltip		"_Time lapse: 5 seconds"
				cvar		*cvar:mn_timelapse_id	value "1"
				icon		speed_dot
				pos			"68 10"
				size		"20 20"
				onChange	{ cmd "game_settimeid <value>;" }
				onWheelUp	{ cmd "game_timefast;" }
				onWheelDown	{ cmd "game_timeslow;" }
			}

			radiobutton bt_timeplay2
			{
				tooltip		"_Time lapse: 5 minutes"
				cvar		*cvar:mn_timelapse_id	value "2"
				icon		speed_play1
				pos			"88 10"
				size		"20 20"
				onChange	{ cmd "game_settimeid <value>;" }
				onWheelUp	{ cmd "game_timefast;" }
				onWheelDown	{ cmd "game_timeslow;" }
			}


			radiobutton bt_timeplay3
			{
				tooltip		"_Time lapse: 20 minutes"
				cvar		*cvar:mn_timelapse_id	value "3"
				icon		speed_dot
				pos			"108 10"
				size		"20 20"
				onChange	{ cmd "game_settimeid <value>;" }
				onWheelUp	{ cmd "game_timefast;" }
				onWheelDown	{ cmd "game_timeslow;" }
			}

			radiobutton bt_timeplay4
			{
				tooltip		"_Time lapse: 1 hour"
				cvar		*cvar:mn_timelapse_id	value "4"
				icon		speed_play2
				pos			"128 10"
				size		"20 20"
				onChange	{ cmd "game_settimeid <value>;" }
				onWheelUp	{ cmd "game_timefast;" }
				onWheelDown	{ cmd "game_timeslow;" }
			}

			radiobutton bt_timeplay5
			{
				tooltip		"_Time lapse: 12 hours"
				cvar		*cvar:mn_timelapse_id	value "5"
				icon		speed_dot
				pos			"148 10"
				size		"20 20"
				onChange	{ cmd "game_settimeid <value>;" }
				onWheelUp	{ cmd "game_timefast;" }
				onWheelDown	{ cmd "game_timeslow;" }
			}


			radiobutton bt_timeplay6
			{
				tooltip		"_Time lapse: 1 day"
				cvar		*cvar:mn_timelapse_id	value "6"
				icon		speed_dot
				pos			"168 10"
				size		"20 20"
				onChange	{ cmd "game_settimeid <value>;" }
				onWheelUp	{ cmd "game_timefast;" }
				onWheelDown	{ cmd "game_timeslow;" }
			}

			radiobutton bt_timeplay7
			{
				tooltip		"_Time lapse: 5 days"
				cvar		*cvar:mn_timelapse_id	value "7"
				icon		speed_play3
				pos			"188 10"
				size		"20 20"
				onChange	{ cmd "game_settimeid <value>;" }
				onWheelUp	{ cmd "game_timefast;" }
				onWheelDown	{ cmd "game_timeslow;" }
			}

			string timelapse
			{
				string		*cvar:mn_timelapse
				pos			"31 32"
				size		"195 20"
				textalign	ALIGN_UC
				color		"0 .78 0 1"
			}

		}

		// geoscape control buttons

		panel geoscape_control {
			{
				pos		"48 340"
				size	"164 164"
			}

			image background {
				pos		"0 0"
				size	"164 164"
				texh	"164 164"
				src	"ui/geo_miniglobe"
			}

			zone panel_scroll_up
			{
				pos			"52 4"
				size		"60 30"
				repeat		true
				clickdelay	100
				onClick		{ cmd "map_scroll down;" }
			}

			zone panel_scroll_down
			{
				pos			"52 134"
				size		"60 30"
				repeat		true
				clickdelay	100
				onClick		{ cmd "map_scroll up;" }
			}

			zone panel_scroll_left
			{
				pos			"2 50"
				size		"30 60"
				repeat		true
				clickdelay	100
				onClick		{ cmd "map_scroll right;" }
			}

			zone panel_scroll_right
			{
				pos			"134 50"
				size		"30 60"
				repeat		true
				clickdelay		100
				onClick		{ cmd "map_scroll left;" }
			}

			zone panel_zoom_in
			{
				pos			"56 50"
				size		"20 60"
				repeat		true
				clickdelay	100
				onClick		{ cmd "map_zoom in" }
			}

			zone panel_zoom_out
			{
				pos			"82 50"
				size		"20 60"
				repeat		true
				clickdelay	100
				onClick		{ cmd "map_zoom out" }
			}
		}

		// ==================
		// buttons
		// ==================

		button bt_messagesettings
		{
			string		"_Message settings"
			tooltip		"_Change message and pause settings"
			color		"0 0.5 0 1"
			font	 	"f_small"
			pos			"41 560"
			size		"175 34"
			selectcolor	"1 1 1 1"
			image		"ui/button_geoscape"

			onClick		{ cmd "ui_push messageoptions;" }
		}

		button bt_mailclient
		{
			visiblewhen "*cvar:mn_upunreadmail != 0"

			string		"_Mailclient"
			tooltip		"_INBOX (with unread mails)"
			color		"1 1 1 1"
			font	 	"f_small"
			pos			"41 595"
			size		"175 34"
			selectcolor	"1 1 1 1"
			image		"ui/button_geoscape"

			onClick		{ cmd "ui_push mailclient;" }
		}

		button bt_mailclient_nomail
		{
			visiblewhen "*cvar:mn_upunreadmail == 0"

			string		"_Mailclient"
			tooltip		"_INBOX"
			color		"0 0.5 0 1"
			font	 	"f_small"
			pos			"41 595"
			size		"175 34"
			selectcolor	"1 1 1 1"
			image		"ui/button_geoscape"

			onClick		{ cmd "ui_push mailclient;" }
		}

		button bt_stats
		{
			string		"_Statistics"
			tooltip		"_Organisation statistics"
			pos			"41 630"
			size		"175 34"
			color		"0 0.5 0 1"
			font	 	"f_small"
			selectcolor	"1 1 1 1"
			image		"ui/button_geoscape"

			onClick		{ cmd "ui_push stats;" }
		}

		button bt_ufopedia
		{
			string		"_UFOpaedia"
			tooltip		"_Open UFOpaedia"
			pos			"41 665"
			size		"175 34"
			color		"0 0.5 0 1"
			font	 	"f_small"
			selectcolor	"1 1 1 1"
			image		"ui/button_geoscape"

			onClick		{ cmd "ui_push ufopedia;" }
		}

		button bt_installation
		{
			string		"_Create installation"
			tooltip		"_Create installation"
			color		"0 0.5 0 1"
			pos			"41 700"
			size		"175 34"
			font	 	"f_small"
			selectcolor	"1 1 1 1"
			image		"ui/button_geoscape"

			onClick		{ cmd "ui_push popup_newinstallation_type;" }
		}

		// ==================
		// small buttons
		// ==================

		image bt_show_overlay
		{
			tooltip		"_Highlight nation borders"
			src		"icons/geo_nations_highlight"
			pos			"40 515"
			texl		"0 0"
			texh		"32 32"
			onClick		{ cmd "map_overlay nations;" }
			onMouseLeave			{ *node:this@texl = "0 0" *node:this@texh = "32 32" }
			onMouseEnter			{ *node:this@texl = "32 0" *node:this@texh = "64 32" }
		}

		image bt_show_radar
		{
			tooltip		"_Show radar range"
			src		"icons/geo_radar"
			pos			"70 515"
			texl		"0 0"
			texh		"32 32"
			onClick		{ cmd "map_overlay radar;" }
			onMouseLeave			{ *node:this@texl = "0 0" *node:this@texh = "32 32" }
			onMouseEnter			{ *node:this@texl = "32 0" *node:this@texh = "64 32" }
		}

		image bt_show_xvi
		{
			visiblewhen "*cvar:mn_xvimap == 1"
			tooltip		"_Show XVI map"
			src		"icons/geo_xvi"
			pos			"100 515"
			texl		"0 0"
			texh		"32 32"
			onClick		{ cmd "map_overlay xvi;" }
			onMouseLeave			{ *node:this@texl = "0 0" *node:this@texh = "32 32" }
			onMouseEnter			{ *node:this@texl = "32 0" *node:this@texh = "64 32" }
		}

		custombutton bt_mapcenter
		{
			tooltip		"_Center objects on the map"
			background	"buttons/blue3"
			icon		"geo_center"
			pos			"145 520"
			size		"28 25"
			onClick		{ cmd "map_center;" }
		}

		custombutton bt_menu
		{
			tooltip		"_Go to Options"
			background	"buttons/blue3"
			icon		"geo_option"
			pos			"190 520"
			size		"28 25"
			onClick		{ cmd "ui_pop;" }
		}

	}

	// ==================
	// Messages
	// ==================

	panel messages {
		{
			align	LAYOUTALIGN_FILL
			layout	LAYOUT_PACK
			ghost	true
			size	"1024 768"
		}

		panel rightMargin {
			align	LAYOUTALIGN_RIGHT
			size	"256 30"
			ghost	true
		}

		panel messagesystem {
			{
				pos		"0 0"
				size	"768 20"
				layout	LAYOUT_BORDER
				align	LAYOUTALIGN_TOP
			}

			vscrollbar scroll {
				align		LAYOUTALIGN_RIGHT
				invis		true
				image		"ui/scrollbar_v_green"
				bgcolor		"0 0 0 .5"
				pos			"749 0"
				height		"768"
				current		0
				viewsize	38
				fullsize	50
				hidewhenunused true
				onChange	{
					*node:parent.message@viewpos = <current>
				}
			}

			messagelist message
			{
				pos			"0 0"
				size		"768 20"
				bgcolor		"0 0 0 .5"
				align		LAYOUTALIGN_MIDDLE
				onMouseEnter {
					if ( *cvar:mn_messagesystem < 2 ) {
						*node:parent@height = 240
					}
				}
				onMouseLeave {
					if ( *cvar:mn_messagesystem < 2 ) {
						cmd "messagesystem_changemode"
					}
				}
				onViewChange {
					*node:parent.scroll@fullsize = <fullsize>
					*node:parent.scroll@current = <viewpos>
					*node:parent.scroll@viewsize = <viewsize>
				}
			}
		}

		panel dock {
			{
				pos				"0 0"
				size			"300 768"
				align			LAYOUTALIGN_LEFT
				layout			LAYOUT_PACK
				ghost			true
				padding			10
				layoutmargin	10
			}

			panel bottomMargin {
				align		LAYOUTALIGN_BOTTOM
				size		"280 30"
			}
		}
	}

	func onInit
	{
		cmd	"music_change geoscape;"
		if ( not exists *cvar:mn_messagesystem ) {
			*cvar:mn_messagesystem = 0
			cmd "messagesystem_changemode;"
		}
	}
}

// ========================================================================
// POPUPS (GEOSCAPE)
// ========================================================================

// ==================
// POPUP INTERCEPT READY
// ==================

window popup_intercept_ready
{
	{
		pos			"264 262"
		size		"496 247"
		dragbutton	true
		image		"ui/popup_alpha_tile"
		string		"_Ready"
		modal		true
	}

	string name_label
	{
		string		"_Aircraft reached desired location - what to do?"
		pos			"26 58"
		size		"450 40"
		longlines	LONGLINES_WRAP
	}

	cmpCancelButton bt_cancel
	{
		pos			"259 188"
		onClick		{ cmd "ui_pop;mn_mapaction_reset;" }
	}

	/**
	 * @todo move the button on middle if cp_mission_autogo_available==0
	 */
	cmpPopupButton bt_go
	{
		string		"_Enter"
		tooltip		"_Enter the battlefield"
		pos			"66 125"
		onClick		{ cmd "game_go;" }
	}

	cmpPopupButton bt_auto_go
	{
		string		"_Auto mission"
		tooltip		"_Automatically finish mission"
		pos			"259 125"
		onClick		{ cmd "cp_mission_autogo;" }
	}

	confunc popup_intercept_ready_update
	{
		if ( *cvar:cp_mission_autogo_available == 1 ) {
			*node:root.bt_auto_go@disabled = false
		} else {
			*node:root.bt_auto_go@disabled = true
		}
	}

	func onInit
	{
		cmd "cp_missionauto_check;popup_intercept_ready_update"
	}
}

// ==================
// POPUP BASE ATTACK
// ==================

window popup_baseattack
{
	{
		pos			"264 262"
		size		"496 304"
		dragbutton	true
		modal		true				// we can't click out
		preventtypingescape true		// we can't use ESC
		image		"ui/popup_alpha_tile"
		string		"_Base is under attack"
	}

	text description
	{
		pos			"26 58"
		dataid		TEXT_POPUP
		size		"450 80"
		lineheight	16
		rows		5
	}

	checkbox equip
	{
		current		1
		pos			"26 145"
		size		"20 18"
		image		"ui/checkbox_blue"
	}
	string equip_str
	{
		string		"_Equip soldiers"
		tooltip		"_Go to soldier equipment screen before the battle"
		pos			"55 145"
		size		"220 20"
	}

	checkbox killaliens
	{
		current		0
		pos			"26 165"
		size		"20 18"
		image		"ui/checkbox_blue"
	}
	string killaliens_str
	{
		string		"_Kill aliens in container"
		pos			"55 165"
		size		"220 20"
	}

	checkbox liftcrafts
	{
		current		0
		disabled	true
		tooltip		"_Not implemented"
		pos			"26 185"
		size		"20 18"
		image		"ui/checkbox_blue"
	}
	string liftcrafts_str
	{
		string		"_Lift aircraft"
		disabled	true
		tooltip		"_Not implemented"
		pos			"55 185"
		size		"220 20"
	}

	checkbox powerdown
	{
		current		0
		disabled	true
		tooltip		"_Not implemented"
		pos			"26 205"
		size		"20 18"
		image		"ui/checkbox_blue"
	}
	string powerdown_str
	{
		string		"_Disable Power Plant"
		disabled	true
		tooltip		"_Not implemented"
		pos			"55 205"
		size		"220 20"
	}

	func listen_automission
	{
		cmd "cp_mission_autogo;"
		cmd "ui_removelistener equipment@onClose <path:this>"
	}

	cmpPopupButton bt_auto_go
	{
		string		"_Auto mission"
		tooltip		"_Automatically finish mission"
		pos			"259 241"
		onClick		{
			if ( *node:root.liftcrafts@current > 0 ) {
				cmd "air_liftall;"
			}
			if ( *node:root.killaliens@current > 0 ) {
				cmd "aliencont_killall;"
			}
			if ( *node:root.powerdown@current > 0 ) {
				cmd "pwr_down;"
			}
			if ( *node:root.equip@current > 0 ) {
				cmd "ui_addlistener equipment@onClose <path:root>.listen_automission;"
				cmd "ui_push equipment;"
			} else {
				cmd "cp_mission_autogo;"
			}
		}
	}

	func listen_mission
	{
		cmd "game_go;"
		cmd "ui_removelistener equipment@onClose <path:this>;"
	}
	cmpPopupButton bt_go
	{
		string		"_Enter"
		tooltip		"_Enter the battlefield"
		pos			"74 241"
		onClick		{
			if ( *node:root.liftcrafts@current > 0 ) {
				cmd "air_liftall;"
			}
			if ( *node:root.killaliens@current > 0 ) {
				cmd "aliencont_killall;"
			}
			if ( *node:root.powerdown@current > 0 ) {
				cmd "pwr_down;"
			}
			if ( *node:root.equip@current > 0 ) {
				cmd "ui_addlistener equipment@onClose <path:root>.listen_mission;"
				cmd "ui_push equipment;"
			} else {
				cmd "game_go;"
			}
		}
	}

	func onInit
	{
		cmd	"cp_missionauto_check;"
	}
}

// ==================
// POPUP NEW BASE
// ==================
window popup_newbase
{
	{
		pos			"264 262"
		size		"496 247"
		dragbutton	true
		modal		true
		image		"ui/popup_alpha_tile"
		string		"_New base"
	}

	string name_label
	{
		string		"_Name:"
		pos			"26 58"
		size		"450 20"
	}
	textentry name
	{
		string		*cvar:mn_base_title
		pos			"46 73"
		size		"420 25"
		font		"f_big"
	}

	button bt_nameedit
	{
		tooltip		"_Rename your base"
		icon		pen
		pos			"26 78"
		size		"16 16"
		padding		0
		onClick		{ call *node:root.name@edit }
	}

	string cost_label
	{
		string		"_Cost:"
		pos			"26 98"
		size		"200 20"
	}
	string cost
	{
		string		*cvar:mn_base_cost
		pos			"26 113"
		size		"200 25"
		font		"f_big"
	}

	cmpCancelButton bt_cancel
	{
		pos			"298 188"
		onClick	{
			cmd "ui_pop;mn_mapaction_reset;"
		}
	}
	cmpPopupButton bt_build
	{
		string		"_Build"
		tooltip		"_Build the base"
		pos			"111 188"
		onClick	{
			cmd "ui_pop;mn_base_build;"
		}
	}
}

// ==================
// POPUP NEW INSTALLATION
// ==================
window popup_newinstallation_type
{
	{
		pos			"264 262"
		size		"496 247"
		dragbutton	true
		closebutton	true
		image		"ui/popup_alpha_tile"
		string		"_New installation"
	}

	string type_label
	{
		string		"_Type"
		pos			"86 50"
		size		"140 20"
		font		"f_normal"
	}
	string buildtime_label
	{
		string		"_Build Time"
		pos			"226 50"
		size		"130 20"
		textalign	ALIGN_UR
		font		"f_normal"
	}
	string cost_label
	{
		string		"_Cost"
		pos			"356 50"
		size		"100 20"
		textalign	ALIGN_UR
		font		"f_normal"
	}

	image bt_highlight
	{
		src		"ui/popup_button_hl"
		invis		true
	}

	image radar_icon
	{
		src		"geoscape/radar"
		pos			"46 78"
	}
	string radar_type
	{
		string		"_Radar tower"
		pos			"86 78"
		size		"170 18"
		font		"f_small"
	}
	string radar_buildtime
	{
		string		*cvar:mn_installation_radartower_buildtime
		pos			"256 78"
		size		"100 20"
		textalign	ALIGN_UR
		font		"f_small"
	}
	string radar_cost
	{
		string		*cvar:mn_installation_radartower_cost
		pos			"356 78"
		size		"100 20"
		textalign	ALIGN_UR
		font		"f_small"
	}
	zone click_radar
	{
		pos			"30 78"
		size		"440 20"
		onMouseEnter			{ *node:root.bt_highlight@invis = false *node:root.bt_highlight@pos = "26 76" }
		onMouseLeave			{ *node:root.bt_highlight@invis = true }
		onClick		{
			*cvar:mn_installation_type = "radartower"
			*cvar:mn_installation_timetobuild = *cvar:mn_installation_radartower_buildtime
			cmd "ui_pop;"
			cmd "mn_installation_select -1;"
		}
	}

	image sam_icon
	{
		src		"geoscape/defence"
		pos			"46 98"
	}
	string sam_type
	{
		string		"_SAM Site"
		pos			"86 98"
		size		"170 18"
		font		"f_small"
	}
	string sam_buildtime
	{
		string		*cvar:mn_installation_samsite_buildtime
		pos			"256 98"
		size		"100 20"
		textalign	ALIGN_UR
		font		"f_small"
	}
	string sam_cost
	{
		string		*cvar:mn_installation_samsite_cost
		pos			"356 98"
		size		"100 20"
		textalign	ALIGN_UR
		font		"f_small"
	}
	zone click_sam
	{
		pos			"30 98"
		size		"440 20"
		onMouseEnter			{ *node:root.bt_highlight@invis = false *node:root.bt_highlight@pos = "26 96" }
		onMouseLeave			{ *node:root.bt_highlight@invis = true }
		onClick		{
			*cvar:mn_installation_type = "samsite"
			*cvar:mn_installation_timetobuild = *cvar:mn_installation_samsite_buildtime
			cmd "ui_pop;"
			cmd "mn_installation_select -1;"
		}
	}

	image ufoyard_icon
	{
		src		"geoscape/ufoyard"
		pos			"46 118"
	}
	string ufoyard_type
	{
		string		"_UFO Yard"
		pos			"86 118"
		size		"170 18"
		font		"f_small"
	}
	string ufoyard_buildtime
	{
		string		*cvar:mn_installation_ufoyard_buildtime
		pos			"256 118"
		size		"100 20"
		textalign	ALIGN_UR
		font		"f_small"
	}
	string ufoyard_cost
	{
		string		*cvar:mn_installation_ufoyard_cost
		pos			"356 118"
		size		"100 20"
		textalign	ALIGN_UR
		font		"f_small"
	}
	zone click_ufoyard
	{
		pos			"30 118"
		size		"440 20"
		onMouseEnter			{ *node:root.bt_highlight@invis = false *node:root.bt_highlight@pos = "26 116" }
		onMouseLeave			{ *node:root.bt_highlight@invis = true }
		onClick		{
			*cvar:mn_installation_type = "ufoyard"
			*cvar:mn_installation_timetobuild = *cvar:mn_installation_ufoyard_buildtime
			cmd "ui_pop;"
			cmd "mn_installation_select -1;"
		}
	}

	//
	// Create a base
	//

	image base_icon
	{
		src		"geoscape/base"
		pos			"46 138"
	}

	string base_type
	{
		string		"_Base"
		pos			"86 138"
		size		"170 18"
		font		"f_small"
	}

	string base_buildtime
	{
		string		"_0 day"
		pos			"256 138"
		size		"100 20"
		textalign	ALIGN_UR
		font		"f_small"
	}
	string base_cost
	{
		string		*cvar:mn_base_cost
		pos			"356 138"
		size		"100 20"
		textalign	ALIGN_UR
		font		"f_small"
	}
	zone click_base
	{
		pos			"30 138"
		size		"440 20"
		onMouseEnter			{ *node:root.bt_highlight@invis = false *node:root.bt_highlight@pos = "26 136" }
		onMouseLeave			{ *node:root.bt_highlight@invis = true }
		onClick		{ cmd "ui_pop; mn_base_select -1;" }
	}

	/* disabled zones - for tooltip */
	zone disabled_installation
	{
		invis		true
		pos			"30 78"
		size		"440 60"
		tooltip		"_Installation limit reached. Build more bases."
	}
	zone disabled_base
	{
		invis		true
		pos			"30 138"
		size		"440 20"
		tooltip		"_Base limit reached."
	}


	//
	// Interaction
	//

	cmpPopupButton bt_close
	{
		string		"_Close"
		tooltip		"_Close this dialogue"
		pos			"297 188"
		onClick		{ cmd "ui_pop;" }
	}

	func onInit {
		if ( *cvar:mn_base_count >= *cvar:mn_base_max ) {
			*node:root.click_base@invis = true
			*node:root.disabled_base@invis = false
			*node:root.base_type@disabled = true
			*node:root.base_buildtime@disabled = true
			*node:root.base_cost@disabled = true
		} else {
			*node:root.click_base@invis = false
			*node:root.disabled_base@invis = true
			*node:root.base_type@disabled = false
			*node:root.base_buildtime@disabled = false
			*node:root.base_cost@disabled = false
		}
		if ( *cvar:mn_installation_count >= *cvar:mn_installation_max ) {
			*node:root.click_ufoyard@invis = true
			*node:root.disabled_installation@invis = false
			*node:root.ufoyard_type@disabled = true
			*node:root.ufoyard_buildtime@disabled = true
			*node:root.ufoyard_cost@disabled = true
			*node:root.click_sam@invis = true
			*node:root.sam_type@disabled = true
			*node:root.sam_buildtime@disabled = true
			*node:root.sam_cost@disabled = true
			*node:root.click_radar@invis = true
			*node:root.radar_type@disabled = true
			*node:root.radar_buildtime@disabled = true
			*node:root.radar_cost@disabled = true
		} else {
			*node:root.click_ufoyard@invis = false
			*node:root.disabled_installation@invis = true
			*node:root.ufoyard_type@disabled = false
			*node:root.ufoyard_buildtime@disabled = false
			*node:root.ufoyard_cost@disabled = false
			*node:root.click_sam@invis = false
			*node:root.sam_type@disabled = false
			*node:root.sam_buildtime@disabled = false
			*node:root.sam_cost@disabled = false
			*node:root.click_radar@invis = false
			*node:root.radar_type@disabled = false
			*node:root.radar_buildtime@disabled = false
			*node:root.radar_cost@disabled = false
		}
	}

}

window popup_newinstallation
{
	{
		pos			"256 284"
		size		"512 300"
		dragbutton	true
		closebutton	true
		modal		true
		image		"ui/popup_alpha_tile"
	}

	string title
	{
		pos			"26 18"
		size		"420 25"
		font		"f_big"
	}

	string name_label
	{
		string		"_Name:"
		pos			"25 60"
		size		"120 25"
		textalign	ALIGN_CL
	}
	textentry name
	{
		string		*cvar:mn_installation_title
		pos			"150 60"
		size		"330 25"
		border		3
		bordercolor	"0 0.5 0 1"
		font		"f_normal"
	}

	string cost_label
	{
		string		"_Cost:"
		pos			"25 90"
		size		"120 20"
	}
	string cost
	{
		pos			"150 90"
		size		"150 20"
	}

	string buildtime_label
	{
		string		"_Time to build:"
		pos			"25 110"
		size		"120 20"
	}
	string buildtime
	{
		string		*cvar:mn_installation_timetobuild
		pos			"150 110"
		size		"150 20"
	}

	text description
	{
		dataid		TEXT_BUILDING_INFO
		pos			"100 140"
		size		"350 80"
		lineheight	20
		onViewChange {
			*node:parent.description_scroll@fullsize = <fullsize>
			*node:parent.description_scroll@current = <viewpos>
			*node:parent.description_scroll@viewsize = <viewsize>
		}
	}
	vscrollbar description_scroll
	{
		image		"ui/scrollbar_v_green"
		pos			"470 140"
		height		"80"
		current		0
		viewsize	4
		fullsize	4
		hidewhenunused	true
		onChange	{ *node:parent.description@viewpos = <current> }
	}

	cmpCancelButton bt_cancel
	{
		pos			"294 240"
		onClick		{ cmd "ui_pop;" }
	}
	cmpPopupButton bt_build
	{
		string		"_Build"
		tooltip		"_Build the radar tower"
		pos			"109 240"
		onClick		{ cmd "ui_pop; mn_installation_build *cvar:mn_installation_type;" }
	}

	func onClose
	{
		cmd "mn_mapaction_reset;"
	}

	func onInit
	{
		if ( *cvar:mn_installation_type eq "radartower" ) {
			*node:root.title@string = "_New radar tower"
			*node:root.cost@string = *cvar:mn_installation_radartower_cost
		} elif ( *cvar:mn_installation_type eq "samsite" ) {
			*node:root.title@string = "_New SAM site"
			*node:root.cost@string = *cvar:mn_installation_samsite_cost
		} elif ( *cvar:mn_installation_type eq "ufoyard" ) {
			*node:root.title@string = "_New UFO yard"
			*node:root.cost@string = *cvar:mn_installation_ufoyard_cost
		}
	}
}

window popup_installationstatus
{
	{
		pos			"264 262"
		size		"496 220"
		dragbutton	true
		image		"ui/popup_alpha_tile"
		string		"_Installation Status"
	}

	string name_label
	{
		string		"_Name:"
		pos			"25 60"
		size		"120 25"
		textalign	ALIGN_CL
	}
	textentry name
	{
		string		*cvar:mn_installation_title
		pos			"150 60"
		size		"310 25"
		border		3
		bordercolor	"0 0.5 0 1"
		font		"f_normal"
		onChange	{ cmd "mn_installation_changename;" }
	}

	string buildtime_label
	{
		string		"_Time to build:"
		pos			"25 110"
		size		"120 20"
	}
	string buildtime
	{
		string		*cvar:mn_installation_timetobuild
		pos			"150 110"
		size		"150 20"
	}

	cmpPopupButton bt_back
	{
		string		"_Close"
		tooltip		"_Close this dialogue"
		pos			"295 150"
		onClick		{ cmd "ui_pop;mn_mapaction_reset;" }
	}
	cmpPopupButton bt_enter
	{
		string		"_Manage"
		tooltip		"_Manage installation"
		pos			"295 110"
		invis		true
		onClick		{ cmd "ui_pop; ui_push *cvar:mn_installation_type;" }
	}

	cmpPopupButton bt_destroy
	{
		string		"_Destroy"
		tooltip		"_Destroy installation"
		pos			"25 150"
		onClick		{ cmd "ui_pop; mn_installation_destroy;" }
	}

	func onInit
	{
		cmd "game_timestop;"

		/* samsite has own menu */
		if ( *cvar:mn_installation_type eq "samsite" ) {
			cmd "ui_pop; ui_push basedefence;"
		}
		/* Show/Hide time-to-build notification and manage button  */
		if ( *cvar:mn_installation_timetobuild eq "-" ) {
			*node:root.buildtime_label@invis = true
			*node:root.buildtime@invis = true
			*node:root.bt_enter@invis = true
		} else {
			*node:root.buildtime_label@invis = false
			*node:root.buildtime@invis = false
			/** @todo Radar and UFO Yard has no manage menu yet */
			/* *node:root.bt_enter@invis = false */

		}
	}
}

// ====================
// POPUP INTERCEPT BASE
// ====================

window popup_intercept
{

	{
		pos			"124 192"
		size		"548 459"
		dragbutton	true
		image		"ui/popup_alpha_tile"
		string		"_Anti-UFO defence"
	}

	tab defence_navigation {
		{
			pos		"18 55"
			size	"515 40"
			cvar	*cvar:mn_defencetab
			onChange {
				cmd "popup_intercept_update;"
			}
		}
		option aircraft {
			label	"_Aircraft interception"
			value	"0"
		}
		option sam {
			label	"_SAM defence"
			value	"1"
		}
	}

	panel aircraft
	{
		{
			pos			"30 100"
			size		"490 290"
		}

		string name_label
		{
			string		"_Aircraft:"
			pos			"6 3"
			size		"140 20"
		}

		string status_label
		{
			string		"_Status:"
			pos			"156 3"
			size		"140 20"
		}

		string craft_base_label
		{
			string		"_Base:"
			pos			"306 3"
			size		"150 20"
		}

		/* tabbed and bolded textlist */
		text ships
		{
			dataid		TEXT_AIRCRAFT_LIST
			pos			"6 29"
			size		"460 260"
			lineheight	20
			tabwidth	150
			mousefx		true
			rows		"13"
			longlines	LONGLINES_PRETTYCHOP
			onViewChange {
				*node:root.aircraft.ships_scroll@fullsize = <fullsize>
				*node:root.aircraft.ships_scroll@current = <viewpos>
				*node:root.aircraft.ships_scroll@viewsize = <viewsize>
			}
			onClick		{ cmd "ships_click <lineselected>;" }
			onRClick	{ cmd "ships_rclick <lineselected>;" }
		}

		vscrollbar ships_scroll {
			image		"ui/scrollbar_v_green"
			pos			"470 29"
			height		"260"
			current		0
			viewsize	13
			fullsize	19
			hidewhenunused	true
			onChange {
				*node:parent.ships@viewpos = <current>
			}
		}
	}

	panel sam
	{
		{
			pos			"30 100"
			size		"490 290"
		}

		string base_label
		{
			string		"_Surface-to-air missile site:"
			pos			"6 3"
			size		"250 20"
		}

		text bases
		{
			dataid		TEXT_BASE_LIST
			pos			"6 29"
			size		"460 260"
			lineheight	20
			tabwidth	175
			mousefx		true
			rows		"13"
			onViewChange {
				*node:root.sam.bases_scroll@fullsize = <fullsize>
				*node:root.sam.bases_scroll@current = <viewpos>
				*node:root.sam.bases_scroll@viewsize = <viewsize>
			}
			onClick	{ cmd "bases_click <lineselected>;" }
		}

		vscrollbar bases_scroll {
			image		"ui/scrollbar_v_green"
			pos			"470 29"
			height		"260"
			current		0
			viewsize	13
			fullsize	19
			hidewhenunused	true
			onChange {
				*node:parent.bases@viewpos = <current>
			}
		}
	}

	cmpCancelButton bt_cancel
	{
		pos			"346 402"
		onClick		{ cmd "ui_pop;mn_mapaction_reset;" }
	}

	confunc popup_intercept_update {
		if ( *cvar:mn_defencetab == 0 ) {
			*node:root.aircraft@invis = false
			*node:root.sam@invis = true
		}
		if ( *cvar:mn_defencetab == 1 ) {
			*node:root.aircraft@invis = true
			*node:root.sam@invis = false
		}
	}

	func onInit {
		*cvar:mn_defencetab = 0
		cmd "popup_intercept_update;"
	}

	func onClose {
		cmd "del mn_defencetab;"
	}
}

// ===================
// POPUP MISSION
// ===================

window popup_mission
{

	{
		pos			"124 192"
		size		"777 402"
		dragbutton	true
		image		"ui/popup_alpha_tile"
		string		"_Select aircraft"
	}

	string name_label
	{
		string		"_Name:"
		pos			"26 48"
		size		"180 25"
	}

	string status_label
	{
		string		"_Status:"
		pos			"216 48"
		size		"180 25"
	}

	string base_label
	{
		string		"_Base:"
		pos			"406 48"
		size		"180 25"
	}

	string ETA_label
	{
		string		"_ETA:"
		pos			"596 48"
		size		"150 25"
	}

	/* tabbed and bolded textlist */
	text ships
	{
		dataid		TEXT_AIRCRAFT_LIST
		pos			"26 74"
		size		"640 260"
		lineheight	20
		tabwidth	190
		mousefx		true
		rows	"13"
		onClick		{ cmd "ships_click <lineselected>;" }
		onRClick	{ cmd "ships_rclick <lineselected>;" }
	}

	cmpCancelButton bt_cancel
	{
		pos			"566 338"
		onClick		{ cmd "ui_pop;mn_mapaction_reset;" }
	}
}

// =====================
// POPUP MULTI SELECTION
// =====================

window popup_multi_selection
{
	{
		pos			"264 262"
		size		"496 247"
		dragbutton	true
		modal		true
		image		"ui/popup_alpha_tile"
		string		"_Select item"
	}

	string type_label
	{
		string		"_Type:"
		pos			"26 48"
		size		"150 20"
		font		"f_normal"
	}

	string name_label
	{
		string		"_Name:"
		pos			"176 48"
		size		"150 20"
		font		"f_normal"
	}

	text multi_select
	{
		dataid		TEXT_MULTISELECTION
		pos			"26 78"
		size		"420 100"
		lineheight	20
		tabwidth	150
		rows		5
		mousefx		true
		onClick		{ cmd "multi_select_click <lineselected>;" }
		onViewChange	{
			*node:root.multi_select_scroll@fullsize = <fullsize>
			*node:root.multi_select_scroll@current = <viewpos>
			*node:root.multi_select_scroll@viewsize = <viewsize>
		}
	}
	vscrollbar multi_select_scroll {
		image		"ui/scrollbar_v_green"
		pos			"446 78"
		height		"100"
		current		0
		viewsize	8
		fullsize	8
		hidewhenunused true
		onChange	{
			*node:root.multi_select@viewpos = <current>
		}
	}

	cmpCancelButton bt_cancel
	{
		pos			"266 188"
		onClick		{ cmd "ui_pop;mn_mapaction_reset;" }
	}
}


// =====================
// POPUP AIRCRAFT
// =====================

window popup_aircraft
{
	{
		pos			"264 262"
		size		"496 247"
		dragbutton	true
		image		"ui/popup_alpha_tile"
		string		"_Aircraft"
	}

	string action_label
	{
		string		"_Action:"
		pos			"26 48"
		size		"150 25"
		font		"f_normal"
	}

	string description_label
	{
		string		"_Description:"
		pos			"176 48"
		size		"200 25"
		font		"f_normal"
	}

	text popup_aircraft_action
	{
		dataid		TEXT_POPUP
		pos			"26 78"
		size		"420 100"
		lineheight	20
		tabwidth	150
		rows		5
		mousefx		true
		onClick		{ cmd "popup_aircraft_action_click <lineselected>;" }
		onViewChange	{
			*node:root.popup_aircraft_action_scroll@fullsize = <fullsize>
			*node:root.popup_aircraft_action_scroll@current = <viewpos>
			*node:root.popup_aircraft_action_scroll@viewsize = <viewsize>
		}
	}
	vscrollbar popup_aircraft_action_scroll {
		image		"ui/scrollbar_v_green"
		pos			"446 78"
		height		"100"
		current		0
		viewsize	8
		fullsize	8
		hidewhenunused true
		onChange	{
			*node:root.popup_aircraft_action@viewpos = <current>
		}
	}

	cmpCancelButton	bt_cancel
	{
		pos			"297 191"
		onClick		{ cmd "ui_pop;" }
	}
}

// ==================
// MENU FOR GEOSCAPE OPTIONS
// ==================

// TODO: Use this menu or remove it

window geoscape_options {
	{
		pos			"256 256"
		size		"350 300"
		dragbutton	true
		closebutton	true
		image		"ui/popup_alpha_tile"
		string		"_Geoscape options"
	}

	panel option1 {
		{
			pos			"30 100"
			size		"320 20"
		}

		string label {
			pos			"0 0"
			size		"250 20"
			string		"_Geoscape projection mode:"
		}

		selectbox value {
			{
				pos			"250 0"
				size		"55 20"
				cvar		*cvar:cl_3dmap
			}
			option mode2d {
				label	"_2D"
				value	"0"
			}
			option mode3d {
				label	"_3D"
				value	"1"
			}
		}
	}

	panel option2 {
		{
			pos			"30 130"
			size		"320 20"
		}
		checkbox value
		{
			current		*cvar:cl_geoscape_overlay_fooooo
			pos			"0 0"
			size		"20 18"
			image		"ui/checkbox_blue"
		}
		string label {
			pos			"20 0"
			size		"200 20"
			string		"_Highlight nation borders"
			onClick		{ cmd "map_overlay nations;" }
		}
	}

	todo clickonlabel {
			pos			"300 130"
			string		"Checkbox don't work, click on the label"
	}

	panel option3 {
		{
			pos			"30 160"
			size		"320 20"
		}
		checkbox value
		{
			current		*cvar:cl_geoscape_overlay_fooooo
			pos			"0 0"
			size		"20 18"
			image		"ui/checkbox_blue"
		}
		string label {
			pos			"20 0"
			size		"200 20"
			string		"_Show radar range"
			onClick		{ cmd "map_overlay radar;" }
		}
	}

	panel option4 {
		{
			visiblewhen "*cvar:mn_xvimap == 1"
			pos			"30 190"
			size		"320 20"
		}
		checkbox value
		{
			current		*cvar:cl_geoscape_overlay_fooooo
			pos			"0 0"
			size		"20 18"
			image		"ui/checkbox_blue"
		}
		string label {
			pos			"20 0"
			size		"200 20"
			string		"_Show XVI map"
			onClick		{ cmd "map_overlay xvi;" }
		}
	}

	func onInit {
		*cvar:cl_geoscape_overlay_fooooo = 0
	}
}
