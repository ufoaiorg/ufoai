// ============================
// BATTLESCAPE PHYSDATA WINDOW
// ============================
component cmpWhumanoid_head extends button {
	color "1 0 0 1"
	pos			"0 0"
	size		"15 15"
}
component cmpWhumanoid_torso extends button {
	color "1 0 0 1"
	pos			"0 15"
	size		"15 15"
}
component cmpWhumanoid_arms extends button {
	color "1 0 0 1"
	pos			"15 0"
	size		"15 15"
}
component cmpWhumanoid_legs extends button {
	color "1 0 0 1"
	pos			"15 15"
	size		"15 15"
}
component cmpWdefault_body extends button {
	color "1 0 0 1"
	pos			"7 7"
	size		"15 15"
}

component cmpPhysNamedTbar extends cmpDefNamedTbar {
	/** @todo Remove me */
	{ } // empty properties to fix parsing problem with "image" token (behaviour or property?)

	image bdr {
		pos "3 18"
	}
	tbar	bar {
		pos "5 18"
	}
	string	val {
		pos "140 0"
	}
}


window physdata
{
	{
		pos			"256 96"
		size		"512 512"
	}

	// ==================
	// background
	// ==================

	image bar_physdata_deco
	{
		src		"hud/bar_phys_deco"
		pos		"0 0"
	}

	image bar_physdata_mini
	{
		src		"hud/bar_phys_mini"
		pos		"0 0"
	}

	string actorname_shadow
	{
		string	*cvar:mn_name
		color	"0 0 0 1"
		pos		"27 26"
		size	"165 20"
	}
	string actorname
	{
		string	*cvar:mn_name
		pos		"26 25"
		size	"165 20"
	}

	// ==================
	// model
	// ==================

	model actor
	{
		{
			src			*cvar:mn_body
			skin		*cvar:mn_body_skin
			anim		"stand2"
			angles		"0 70 90"
			pos			"27 20"
			size		"191 322"
			scale		"5 5 5"
		}

		model head
		{
			src			*cvar:mn_head
			skin		*cvar:mn_head_skin
			tag			"tag_head"
		}

		model right_rweapon
		{
			src			*cvar:mn_rweapon
			tag			"tag_rweapon"
		}

		model right_lweapon
		{
			src			*cvar:mn_lweapon
			tag			"tag_lweapon"
		}
	}

	// ==================
	// abilities
	// ==================

		image pwr_bar_full
			{
				pos "224 50"
				src		"ui/bar_attribute_disabled"
			}
		cmpPhysNamedTbar pwr {
			{
				pos "220 32"
			}
			tbar bar   { current *cvar:mn_vpwr }
			string lbl { string "_Strength:" }
			string val { string *cvar:mn_tpwr }
		}

		image spd_bar_full
			{
				pos "224 82"
				src		"ui/bar_attribute_disabled"
			}
		cmpPhysNamedTbar spd {
			{
				pos "220 64"
			}
			tbar bar   { current *cvar:mn_vspd }
			string lbl { string "_Speed:" }
			string val { string *cvar:mn_tspd }
		}

		image acc_bar_full
			{
				pos "224 114"
				src		"ui/bar_attribute_disabled"
			}
		cmpPhysNamedTbar acc {
			{
				pos "220 96"
			}
			tbar bar   { current *cvar:mn_vacc }
			string lbl { string "_Accuracy:" }
			string val { string *cvar:mn_tacc }
		}

		image mnd_bar_full
			{
				pos "224 146"
				src		"ui/bar_attribute_disabled"
			}
		cmpPhysNamedTbar mnd {
			{
				pos "220 128"
			}
			tbar bar   { current *cvar:mn_vmnd }
			string lbl { string "_Mind:" }
			string val { string *cvar:mn_tmnd }
		}

		image cls_bar_full
			{
				pos "224 184"
				src		"ui/bar_skill_disabled"
			}
		cmpPhysNamedTbar cls {
			{
				pos "220 166"
			}
			tbar bar   { current *cvar:mn_vcls image "ui/bar_skill" }
			string lbl { string "_Close Combat:" }
			string val { string *cvar:mn_tcls }
		}

		image hvy_bar_full
			{
				pos "224 216"
				src		"ui/bar_skill_disabled"
			}
		cmpPhysNamedTbar hvy {
			{
				pos "220 198"
			}
			tbar bar   { current *cvar:mn_vhvy image "ui/bar_skill" }
			string lbl { string "_Heavy Weapons:" }
			string val { string *cvar:mn_thvy }
		}

		image ass_bar_full
			{
				pos "224 248"
				src		"ui/bar_skill_disabled"
			}
		cmpPhysNamedTbar ass {
			{
				pos "220 230"
			}
			tbar bar   { current *cvar:mn_vass image "ui/bar_skill" }
			string lbl { string "_Assault Weapons:" }
			string val { string *cvar:mn_tass }
		}

		image snp_bar_full
			{
				pos "224 280"
				src		"ui/bar_skill_disabled"
			}
		cmpPhysNamedTbar snp {
			{
				pos "220 262"
			}
			tbar bar   { current *cvar:mn_vsnp image "ui/bar_skill" }
			string lbl { string "_Sniper Rifles:" }
			string val { string *cvar:mn_tsnp }
		}

		image exp_bar_full
			{
				pos "224 314"
				src		"ui/bar_skill_disabled"
			}
		cmpPhysNamedTbar exp {
			{
				pos "220 296"
			}
			tbar bar   { current *cvar:mn_vexp image "ui/bar_skill" }
			string lbl { string "_High-Explosives:" }
			string val { string *cvar:mn_texp }
		}

	// these cvars do just exist in campaign mode, otherwise strings are empty
	string	missions_lbl { pos "37 445" size "180 20" string *cvar:mn_missions_label_string }
	string	missions_val { pos "240 445" size "165 20" string *cvar:mn_chrmis contentalign ALIGN_UR }

	string	kills_lbl { pos "37 465" size "180 20" string *cvar:mn_kills_label_string }
	string	kills_val { pos "240 465" size "165 20" string *cvar:mn_chrkillalien contentalign ALIGN_UR }

	// drag controls extended over the whole window, LMB:drag, RMB:close
	controls drag {
		tooltip	"_LMB:Drag/RMB:Close\nWheel:Next/Previous Soldier"
		pos		"10 10"
		size	"492 492"
		OnRClick { cmd "ui_pop;" }
		onWheelUp	{ cmd "actor_next;" }
		onWheelDown	{ cmd "actor_prev;" }
	}

	// ==================
	// rank
	// ==================

	image	rank_img {
		pos "25 70"
		size "64 64"
		src *cvar:mn_chrrank_img
		tooltip *cvar:mn_chrrank
		OnRClick { cmd "ui_pop;" }
	}

	// ==================
	// health and morale
	// ==================

	string hp_val_shadow
	{
		color "0 0 0 1"
		font		"f_normal"
		string		*cvar:mn_hp
		pos			"416 370"
		size		"50 20"
		contentalign	ALIGN_UC
	}
	string hp_val
	{
		tooltip		"_Health"
		font		"f_normal"
		string		*cvar:mn_hp
		pos			"415 369"
		size		"50 20"
		contentalign	ALIGN_UC
		OnRClick { cmd "ui_pop;" }
	}
	ekg hp
	{
		tooltip		"_EKG"
		src		"hud/phys_ekg"
		pos			"78 367"
		size		"324 32"
	}

	string morale_val_shadow
	{
		color "0 0 0 1"
		font		"f_normal"
		string		*cvar:mn_morale
		pos			"416 410"
		size		"50 20"
		contentalign	ALIGN_UC
	}
	string morale_val
	{
		tooltip		"_Morale"
		font		"f_normal"
		string		*cvar:mn_morale
		pos			"415 409"
		size		"50 20"
		contentalign	ALIGN_UC
		OnRClick { cmd "ui_pop;" }
	}
	ekg morale
	{
		tooltip		"_Morale"
		src		"hud/phys_mor"
		pos			"78 406"
		size		"324 32"
		scrollspeed	0.12
		scale		2.0
	}

	panel wounds
	{
		pos		"40 365"
		size		"30 30"
	}
	// ==================
	// buttons
	// ==================

	button button_exit
	{
		tooltip		"_Close"
		background	"buttons/large_blue"
		pos			"428 458"
		size		"64 37"
		icon		"icons/return"
		onClick		{ cmd "ui_pop;" }
	}

	// <1> Body part type (id)
	// <2> Severity of bleeding, if 0 all wounds are treated
	// (Note: Robots don't bleed, this just means they have serious damage in this part)
	// <3> Tooltip

	confunc actor_wounds {
		call *node:root.wounds@createChild("wound_<1>", "cmpW<1>")
		if (<2> > 0) {
			*node:root.wounds.wound_<1>@icon = "icons/message_debug"
		} else {
			*node:root.wounds.wound_<1>@icon = "icons/message_info"
		}
		*node:root.wounds.wound_<1>@tooltip = "<3>"
	}

	confunc updateactorwounds {
		call *node:root.wounds@removeAllchild
		cmd "hud_updateactorwounds;"
	}

	func onWindowOpened {
		*cvar:physdat_window_open = 1
		cmd "ui_addcvarlistener cl_selected updateactorwounds;"
		cmd "updateactorwounds;"
		// if we are in campaign mode, we need these cvars
		if ( *cvar:mn_base_count > 0 ) {
			*cvar:mn_missions_label_string = "_Missions Performed:"
			*cvar:mn_kills_label_string = "_Enemies Killed:"
		} else {
			if ( exists *cvar:mn_chrmis ) { delete *cvar:mn_chrmis }
			if ( exists *cvar:mn_chrkillalien ) { delete *cvar:mn_chrkillalien }
		}
		// the ability to hide the deco is needed for the MiniHUD only
		if ( *cvar:mn_minihud_enabled == 1 ) {
			if ( *cvar:mn_minihud_deco == 0 ) { *node:root.bar_physdata_deco@invis = true }
			else { *node:root.bar_physdata_deco@invis = false }
		}
		// the inventory window will automatically be closed if open
		if ( *cvar:inventory_window_open == 1 ) { cmd "ui_close hudinv;" }
	}

	func onWindowClosed {
		cmd "ui_removecvarlistener cl_selected updateactorwounds;"
		delete *cvar:physdat_window_open
		// these cvars exist in campaign mode only
		if ( *cvar:mn_base_count > 0 ) {
			delete *cvar:mn_missions_label_string
			delete *cvar:mn_kills_label_string
		}
	}
}

// ==================
// HUD PHYSDATA
// ==================

window hudphysdat extends physdata
{
}
