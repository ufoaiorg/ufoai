// ==================
// MARKET MENU
// ==================

component panel cmpMarketItem
{
	{
		size "425 24"
		onClick	{ cmd "mn_buysell <num> <lastdiff>; market_click <num>;" }
//		onWheel		{ cmd "sync_market_scroll <lineselected> <viewpos>;market_scroll <viewpos>;" }
	}
	zone wz_buysell
	{
		pos			"302 0"
		size		"120 24"
		onWheelUp	{ cmd "mn_buy <node:parent@num>;" }
		onWheelDown	{ cmd "mn_sell <node:parent@num>;" }
	}
	spinner bt_buysell
	{
		tooltip		"_Buy and sell item"
		image		"ui/spinner_blue"
		pos			"346 0"
		onChange	{ cmd "mn_buysell <node:parent@num> <lastdiff>; market_click <node:parent@num>;" }
	}
	checkbox bt_autosell
	{
		current		0
		image		"ui/checkbox_blue"
//		pos			"30 180"
		pos			"0 0"
		size		"16 16"
		onChange	{ cmd "buy_autosell <node:parent@num>;" }
	}

}

window market extends objectinfo
{
	/** @todo Remove me */
	{ } // empty properties to fix parsing problem with "image" token (behaviour or property?)

	// ==================
	// background
	// ==================

	image background
	{
		src		"background/base_bg"
	}

	cmpBaseHeader baseHeader {
	}

	// ==================
	// tab
	// ==================

	panel tab_background {
		pos		"24 96"
		size	"986 56"
		image	"ui/panel_tab"
	}

	tab market_navigation {
		{
			pos		"47 106"
			size	"938 39"
			cvar	*cvar:mn_itemtype
			onChange {
				cmd "buy_type <cvar:mn_itemtype>;"
			}
		}

		option primary {
			label	"_Primary"
			value	"primary"
		}
		option secondary {
			label	"_Secondary"
			value	"secondary"
		}
		option heavy {
			label	"_Heavy"
			value	"heavy"
		}
		option misc {
			label	"_Misc"
			value	"misc"
		}
		option armour {
			label	"_Armour"
			value	"armour"
		}
		option craftitem {
			label	"_Craft items"
			value	"craftitem"
		}
		option aircraft {
			label	"_Aircraft"
			value	"aircraft"
		}
		option dummy {
			label	"_Other"
			value	"dummy"
		}
		option ugvitem {
			label	"_UGV items"
			value	"ugvitem"
		}
	}

	// ==================
	// lists
	// ==================

	rows lines {
		pos			"28 175"
		size		"530 568"
		color1		"0 0.08 0 1"
		color2		"0 0.16 0 1"
		lineheight	25
	}

	panel verticalline {
		pos		"451 175"
		size	"4 565"
		bgcolor	"0.03 0.42 0.03 1"
	}

	panel itemlist {
		pos		"12 160"
		size	"553 597"
		image	"ui/panel_grey_green_large2"
	}

	textlist market
	{
		dataid		TEXT_MARKET_NAMES
		color		"0 .78 0 1"
		rows		"22"
		lineheight	"25"
		pos			"55 177"
		size		"265 550"
		onChange	{ cmd "sync_market_scroll <lineselected> <viewpos>;market_click <lineselected>;" }
		onWheel		{ cmd "sync_market_scroll <lineselected> <viewpos>;market_scroll <viewpos>;" }
	}

	textlist market_storage
	{
		dataid		TEXT_MARKET_STORAGE
		color		"0 .78 0 1"
		textalign	ALIGN_CR
		pos			"330 177"
		rows		"22"
		size		"40 550"
		lineheight	"25"
		onChange	{ cmd "sync_market_scroll <lineselected> <viewpos>;market_click <lineselected>;" }
		onWheel		{ cmd "sync_market_scroll <lineselected> <viewpos>;market_scroll <viewpos>;" }
	}

	textlist market_market
	{
		dataid		TEXT_MARKET_MARKET
		color		"0 .78 0 1"
		textalign	ALIGN_CR
		pos			"406 177"
		rows		"22"
		size		"40 550"
		lineheight	"25"
		onChange	{ cmd "sync_market_scroll <lineselected> <viewpos>;market_click <lineselected>;" }
		onWheel		{ cmd "sync_market_scroll <lineselected> <viewpos>;market_scroll <viewpos>;" }
	}

	textlist market_prices
	{
		dataid		TEXT_MARKET_PRICES
		color		"0 .78 0 1"
		textalign	ALIGN_CR
		rows		"22"
		size		"80 550"
		lineheight	"25"
		pos			"462 177"
		onChange	{ cmd "sync_market_scroll <lineselected> <viewpos>;market_click <lineselected>;" }
		onWheel		{ cmd "sync_market_scroll <lineselected> <viewpos>;market_scroll <viewpos>;" }
	}

	/**
	 * @param <1> item selected
	 * @param <2> scroll position
	 */
	confunc sync_market_scroll {
		*node:root.market@lineselected = <1>
		*node:root.market_storage@lineselected = <1>
		*node:root.market_market@lineselected = <1>
		*node:root.market_prices@lineselected = <1>
		*node:root.market@viewpos = <2>
		*node:root.market_storage@viewpos = <2>
		*node:root.market_market@viewpos = <2>
		*node:root.market_prices@viewpos = <2>
	}

	// ==================
	// buy/sell/autosell buttons
	// ==================

	/**
	 * @note correct current and max values of spinners will be set via confunc
	 * @sa (confunc) buy_updateitem
	 * @sa BS_UpdateItem
	 */
	cmpMarketItem item0
	{
		pos			"30 180"
	}
	cmpMarketItem item1
	{
		pos			"30 205"
	}
	cmpMarketItem item2
	{
		pos			"30 230"
	}
	cmpMarketItem item3
	{
		pos			"30 255"
	}
	cmpMarketItem item4
	{
		pos			"30 280"
	}
	cmpMarketItem item5
	{
		pos			"30 305"
	}
	cmpMarketItem item6
	{
		pos			"30 330"
	}
	cmpMarketItem item7
	{
		pos			"30 355"
	}
	cmpMarketItem item8
	{
		pos			"30 380"
	}
	cmpMarketItem item9
	{
		pos			"30 405"
	}
	cmpMarketItem item10
	{
		pos			"30 430"
	}
	cmpMarketItem item11
	{
		pos			"30 455"
	}
	cmpMarketItem item12
	{
		pos			"30 480"
	}
	cmpMarketItem item13
	{
		pos			"30 505"
	}
	cmpMarketItem item14
	{
		pos			"30 530"
	}
	cmpMarketItem item15
	{
		pos			"30 555"
	}
	cmpMarketItem item16
	{
		pos			"30 580"
	}
	cmpMarketItem item17
	{
		pos			"30 605"
	}
	cmpMarketItem item18
	{
		pos			"30 630"
	}
	cmpMarketItem item19
	{
		pos			"30 655"
	}
	cmpMarketItem item20
	{
		pos			"30 680"
	}
	cmpMarketItem item21
	{
		pos			"30 705"
	}

	panel basedescription {
		{
			pos		"588 167"
			size	"410 115"
			image	"ui/panel_grey_green"
		}

		// ==================
		// storage capacity
		// ==================

		string txt_storage_caption
		{
			visiblewhen "*cvar:mn_itemtype ne \"aircraft\""	// for everything except aircraft
			string 		"_Storage (used/all):"
			pos			"62 50"
			size		"200 20"
			font		"f_small"
		}

		string txt_storage_value
		{
			visiblewhen "*cvar:mn_itemtype ne \"aircraft\""	// for everything except aircraft
			string 		*cvar:mn_bs_storage
			pos			"267 50"
			size		"100 20"
			font		"f_small"
		}
	}

	confunc listen_openpedia {
		cmd "market_click <node:root.market@lineselected>;"
		cmd "ui_removelistener ufopedia@onWindowClose <path:this>;"
	}

	panel itemdescription {
		{
			pos		"588 303"
			size	"410 455"
			image	"ui/panel_grey_green"
		}

		// ==================
		// item description
		// ==================

		string itemname
		{
			visiblewhen "*cvar:mn_itemtype ne \"aircraft\""	// for everything except aircraft
			string		*cvar:mn_itemname
			pos			"22 35"
			size		"365 20"
			textalign	ALIGN_UC
		}
		string aircraftname
		{
			visiblewhen "*cvar:mn_itemtype eq \"aircraft\""	// Only for aircraft.
			string		*cvar:mn_aircraftname
			pos			"22 35"
			size		"365 20"
			textalign	ALIGN_UC
		}

		custombutton ufopedia_link
		{
			icon		"icons/bordered_magnifying_glass"
			tooltip		"_UFOpaedia"
			pos			"370 37"
			size		"16 16"
			onClick		{
				cmd "ui_addlistener ufopedia@onWindowClose <path:root>.listen_openpedia;"
				cmd "market_openpedia;"
			}
		}

		item itemmodel
		{
			src			*cvar:mn_item
			pos			"12 55"
			size		"385 200"
			angles		"-10 160 70"
			autoscale	true
		}

		text description
		{
			dataid		TEXT_ITEMDESCRIPTION
			pos			"32 257"
			size		"336 120"
			rows		"8"
			lineheight	15
			tabwidth	168
			font		"f_verysmall"
			onViewChange {
				*node:root.itemdescription.description_scroll@fullsize = <fullsize>
				*node:root.itemdescription.description_scroll@current = <viewpos>
			}
			onWheel {
				*node:root.itemdescription.description_scroll@current = <viewpos>
			}
		}
		vscrollbar description_scroll {
			image		"ui/scrollbar_v_green"
			pos			"370 257"
			height		"120"
			current		0
			viewsize	8
			fullsize	8
			hidewhenunused true
			onChange {
				*node:root.itemdescription.description@viewpos = <current>
			}
		}

		// ======================
		// useable weapon or ammo
		// ======================

		string header_item
		{
			string	"_With:"
			pos		"32 390"
			size	"100 16"
			font	"f_small"
		}

		custombutton action_dec
		{
			pos		"142 390"
			icon	"icons/prev"
			size	"16 16"
			onClick	{ cmd "mn_decreaseitem;" }
		}

		string item_name
		{
			string	*cvar:mn_linkname
			pos		"162 390"
			size	"190 16"
			textalign	ALIGN_CC
			font	"f_small"
		}

		custombutton action_inc
		{
			pos		"356 390"
			icon	"icons/next"
			size	"16 16"
			onClick	{ cmd "mn_increaseitem;" }
		}

		// ==================
		// firemode
		// ==================

		string header_firemode
		{
			string	"_Firemode:"
			pos		"32 414"
			size	"100 16"
			font	"f_small"
		}

		custombutton firemode_dec
		{
			tooltip "_Previous firemode"
			pos		"142 414"
			icon	"icons/prev"
			size	"16 16"
			onClick	{ cmd "mn_decreasefiremode;" }
		}

		string firemode_name
		{
			string	*cvar:mn_firemodename
			pos		"162 414"
			size	"190 16"
			textalign	ALIGN_CC
			font	"f_small"
		}

		custombutton firemode_inc
		{
			tooltip "_Next firemode"
			pos		"356 414"
			icon	"icons/next"
			size	"16 16"
			onClick	{ cmd "mn_increasefiremode;" }
		}

	}

	// =========================
	// buy/sell/autosell actions
	// =========================

	/**
	 * @brief checks an autosell checkbox and makes it visible
	 * @param[in] <1> row of item
	 */
	confunc buy_autoselle {
		*node:root.item<1>.bt_autosell@invis = false
		*node:root.item<1>.bt_autosell@current = 1
		*node:root.item<1>.bt_autosell@tooltip = "_Item autosell enabled"
	}
	/**
	 * @brief unchecks an autosell checkbox and makes it visible
	 * @param[in] <1> row of item
	 */
	confunc buy_autoselld {
		*node:root.item<1>.bt_autosell@invis = false
		*node:root.item<1>.bt_autosell@current = 0
		*node:root.item<1>.bt_autosell@tooltip = "_Item autosell disabled"
	}
	/**
	 * @brief makes an autosell checkbox invisible
	 * @param[in] <1> row of item
	 */
	confunc buy_autoselli {
		*node:root.item<1>.bt_autosell@invis = true
	}

	/**
	 * @brief sets the selected line of the buylist
	 * @param[in] <1> row of the item
	 */
	confunc buy_selectitem {
		*node:root.market@lineselected = <1>
	}

	/**
	 * @brief sets the correct values for spinner controls
	 * @param[in] <1> row of the item
	 * @param[in] <2> quantity of the item in the base
	 * @param[in] <3> min value allowed
	 * @param[in] <4> max value allowed
	 */
	confunc buy_updateitem {
		*node:root.item<1>.bt_buysell@current = <2>
		*node:root.item<1>.bt_buysell@min = <3>
		*node:root.item<1>.bt_buysell@max = <4>
	}

	/**
	 * @brief hides the buy/sell spinner control
	 * @param[in] <1> row of the item
	 */
	confunc buy_hide {
		*node:root.item<1>.bt_buysell@invis = true
	}
	/**
	 * @brief shows the buy/sell spinner control
	 * @param[in] <1> row of the item
	 */
	confunc buy_show {
		*node:root.item<1>.bt_buysell@invis = false
	}

	// ==================
	// init
	// ==================

	func onWindowOpened
	{
		cmd "buy_type *cvar:mn_itemtype;"
		*cvar:i = 0
		while ( *cvar:i < 22 ) {
			*node:root.item<cvar:i>@num = <cvar:i>
			*cvar:i = ( *cvar:i + 1 )
		}
		delete *cvar:i
	}
}
