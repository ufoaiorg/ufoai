// ==============
// UFOPEDIA MENU
// ==============

window ufopedia extends objectinfo
{
	{
		pos			"12 34"
		size		"1000 700"
		dragbutton	true
		closebutton	true
		image		"ui/popup_alpha_tile"
		color		"1.0 0.75 0 1"
		string		*cvar:mn_uptitle
		modal		true				// we can't click out
	}

	// ==================
	// summary
	// ==================

	panel summary
	{
		{
			pos		"22 91"
			size	"307 588"
			image	"ui/panel_green"
			layout	LAYOUT_BORDER
			padding	10
		}

		optiontree list
		{
			cvar		*cvar:up_summary_selection
			dataid		OPTION_UFOPEDIA
			align		LAYOUTALIGN_MIDDLE
			font		"f_small"
			color		"0 .78 0 1"
			onChange
			{
				cmd "ufopedia_click <cvar:up_summary_selection>;"
				*node:root.content.ufopedia_scroll@current = 0
				*node:root.content.ufopedia@viewpos = 0
				cmd "update_ufopedia_layout;"
			}
			onViewChange
			{
				*node:parent.list_scroll@current = <viewpos>
				*node:parent.list_scroll@fullsize = <fullsize>
				*node:parent.list_scroll@viewsize = <viewsize>
			}
		}

		vscrollbar list_scroll
		{
			image		"ui/scrollbar_v_green"
			align		LAYOUTALIGN_RIGHT
			current		0
			viewsize	16
			fullsize	50
			onChange	{ *node:parent.list@viewpos = <current> }
		}
	}

	panel content
	{
		{
			pos		"343 55"
			size	"655 625"
		}

		panel navigation_panel
		{
			pos		"0 32"
			size	"637 593"
			image	"ui/panel_green_tab"
		}

		tab navigation
		{
			{
				cvar	*cvar:ufopedia_content
				pos		"1 0"
				size	"628 43"
				onChange { cmd "update_ufopedia_layout;" }
			}
			option description
			{
				label	"_Description"
				value	"description"
			}
			option preresearch
			{
				label	"_Pre-Research Report"
				value	"preresearch"
			}
			option stats
			{
				label	"_Statistics"
				value	"stats"
			}
			option bigpicture
			{
				label	"_The Big Picture"
				value	"bigpicture"
			}
		}

		// ==================
		// deco
		// ==================

		image image_top
		{
			src		*cvar:mn_upimage_top
			pos			"420 42"
			size		"185 422"
			preventratio	true
		}
		image image_top2
		{
			src		*cvar:mn_upimage_top2
			pos			"420 42"
			size		"185 422"
			preventratio	true
		}
		panel decoration_panel
		{
			pos		"420 42"
			size	"185 187"
			image	"ui/panel_green_slim"
		}
		model mdl_top
		{
			src			*cvar:mn_upmodel_top
			angles		"0 0 90"
			pos			"422 44"
			size		"182 182"
			autoscale	true
			omega		"0 10 0"
			rotatewithmouse	true
		}
		panel decoration_panel2
		{
			pos		"420 230"
			size	"185 135"
			image	"ui/panel_green_slim"
		}
		model mdl_bottom
		{
			src			*cvar:mn_upmodel_bottom
			angles		"0 0 90"
			pos			"422 290"
			size		"60 60"
			autoscale	true
			omega		"0 -10 0"
			rotatewithmouse	true
		}
		model mdl_bottom2
		{
			src			*cvar:mn_upmodel_bottom
			angles		"0 0 90"
			pos			"483 240"
			size		"60 60"
			autoscale	true
			omega		"0 0 10"
			rotatewithmouse	true
		}
		model mdl_bottom3
		{
			src			*cvar:mn_upmodel_bottom
			angles		"0 0 90"
			pos			"544 290"
			size		"60 60"
			autoscale	true
			omega		"0 10 0"
			rotatewithmouse	true
		}
		panel main_panel_small
		{
			pos		"8 42"
			size	"410 573"
			image	"ui/panel_green_slim"
		}
		panel main_panel_large
		{
			pos		"8 42"
			size	"597 573"
			image	"ui/panel_green_slim"
		}
		model mdl_bigpicture
		{
			src			*cvar:mn_upmodel_top
			angles		"0 25 90"
			pos			"20 43"
			size		"571 571"
			autoscale	true
			omega		"0 -11 0"
			rotatewithmouse	true
		}
		image image_bigpicture
		{
			src		*cvar:mn_upimage_top
			pos			"180 43"
			size		"251 571"
			preventratio	true
		}

/*	// for future purposes
		panel decoration_panel3
		{
			pos		"420 366"
			size	"185 99"
			image	"ui/panel_green_slim"
		}
*/
		// ==================
		// text and info
		// ==================

		text2 ufopedia
		{
			dataid		TEXT_UFOPEDIA
			pos			"15 47"
			size		"410 570"
			font		"f_small"
			lineheight	20
			tabwidth	100
			onViewChange
			{
				*node:parent.ufopedia_scroll@fullsize = <fullsize>
				*node:parent.ufopedia_scroll@current = <viewpos>
				*node:parent.ufopedia_scroll@viewsize = <viewsize>
			}
		}

		vscrollbar ufopedia_scroll
		{
			image		"ui/scrollbar_v_green"
			pos			"609 42"
			height		"573"
			current		0
			viewsize	26
			fullsize	50
			onChange	{ *node:parent.ufopedia@viewpos = <current> }
		}

		text description
		{
			dataid		TEXT_ITEMDESCRIPTION
			pos			"15 47"
			size		"405 570"
			lineheight	20
			tabwidth	260
			rows		28
			onViewChange	{
				*node:parent.description_scroll@fullsize = <fullsize>
				*node:parent.description_scroll@current = <viewpos>
				*node:parent.description_scroll@viewsize = <viewsize>
			}
		}

		vscrollbar description_scroll
		{
			image		"ui/scrollbar_v_green"
			pos			"406 45"
			height		"573"
			current		0
			viewsize	20
			fullsize	20
			hidewhenunused true
			onChange	{ *node:parent.description@viewpos = <current> }
		}
		panel requirement
		{
			{
				pos		"420 466"
				size	"185 149"
				image	"ui/panel_green_slim"
			}
			string label
			{
				pos "8 6"
				size "120 20"
				color		"1.0 0.75 0 1"
				font	"f_small"
				string "_Requirement(s):"
			}
			textlist techtree
			{
				dataid		TEXT_UFOPEDIA_REQUIREMENT
				pos			"15 40"
				size		"156 92"
				font		"f_verysmall"
				color		"0 .78 0 1"
				selectcolor	"1 1 1 1"
				onChange		{ cmd "techtree_click <lineselected>;" }
				onViewChange
				{
					*node:parent.techtree_scroll@fullsize = <fullsize>
					*node:parent.techtree_scroll@current = <viewpos>
					*node:parent.techtree_scroll@viewsize = <viewsize>
				}
			}

			vscrollbar techtree_scroll {
				image		"ui/scrollbar_v_green"
				invis		true
				pos			"142 7"
				height		"117"
				current		0
				hidewhenunused true
				onChange	{ *node:parent.techtree@viewpos = <current> }
			}
		}
	}

	// ==================
	// item description
	// ==================

	panel itemdescription
	{
		{
			pos		"360 530"
			size	"350 60"
		}

		// ======================
		// useable weapon or ammo
		// ======================

		string header_item
		{
			string	"_With:"
			pos		"10 0"
			size	"100 16"
			font	"f_small"
		}

		custombutton action_dec
		{
			pos		"120 0"
			icon	"icons/prev"
			size	"16 16"
			onClick	{ cmd "mn_decreaseitem;" }
		}

		string item_name
		{
			string	*cvar:mn_linkname
			pos		"140 0"
			size	"190 16"
			textalign	ALIGN_CC
			font	"f_small"
		}

		custombutton action_inc
		{
			pos		"334 0"
			icon	"icons/next"
			size	"16 16"
			onClick	{ cmd "mn_increaseitem;" }
		}

		// ==================
		// firemode
		// ==================

		string header_firemode
		{
			string	"_Firemode:"
			pos		"10 20"
			size	"100 16"
			font	"f_small"
		}

		custombutton firemode_dec
		{
			tooltip "_Previous firemode"
			pos		"120 20"
			icon	"icons/prev"
			size	"16 16"
			onClick	{ cmd "mn_decreasefiremode;" }
		}

		string firemode_name
		{
			string	*cvar:mn_firemodename
			pos		"140 20"
			size	"190 16"
			textalign	ALIGN_CC
			font	"f_small"
		}

		custombutton firemode_inc
		{
			tooltip "_Next firemode"
			pos		"334 20"
			icon	"icons/next"
			size	"16 16"
			onClick	{ cmd "mn_increasefiremode;" }
		}
	}

	confunc update_ufopedia_layout
	{
		// is right column not empty?
		*cvar:mn_rightpanel = 0
		if ( *cvar:mn_upimage_top ne "" ) {
			*cvar:mn_rightpanel = 1
		}
		if ( *cvar:mn_upimage_top2 ne "" ) {
			*cvar:mn_rightpanel = 1
		}
		if ( *cvar:mn_upmodel_top ne "" ) {
			*cvar:mn_rightpanel = 1
			cmd "del mn_upimage_top2;"
			*node:root.content.decoration_panel@invis = false
		}
		if ( *cvar:mn_upmodel_bottom ne "" ) {
			*cvar:mn_rightpanel = 1
			*node:root.content.decoration_panel2@invis = false
		}

		if ( *cvar:mn_uprequirement ne "" ) { *cvar:mn_rightpanel = 1 }

		// invalidate techtree selection
		*node:root.content.requirement.techtree@lineselected = -1

		// update tabs
		if ( *cvar:mn_uppreavailable == 1 ) {
			*node:root.content.navigation.preresearch@invis = false
		} else {
			*node:root.content.navigation.preresearch@invis = true
			// update the tab if the hidden one is selected
			if ( *cvar:ufopedia_content eq "preresearch" ) {
				*cvar:ufopedia_content = "description"
			}
		}

		// we do show the bigpicture tab for models and pictures
		if ( *cvar:mn_upmodel_top ne "" ) {
			*node:root.content.navigation.bigpicture@invis = false
		}
		if ( *cvar:mn_upimage_top ne "" ) {
			*node:root.content.navigation.bigpicture@invis = false
		}

/* TODO: make this work:
	// we do not want to show the bigpicture tab for ammo models,
	// but here we are just not showing it if the tech displayed has an ammo model
		if ( *cvar:mn_upmodel_bottom ne "" ) {
			if ( *cvar:mn_upmodel_top ne "" ) {
				*node:root.content.navigation.bigpicture@invis = true
			}
		}
*/

		if ( *cvar:mn_upmetadata == 1 ) {
			*node:root.content.navigation.stats@invis = false
		} else {
			*node:root.content.navigation.stats@invis = true
			// update the tab if the hidden one is selected
			if ( *cvar:ufopedia_content eq "stats" ) {
				*cvar:ufopedia_content = "description"
			}
		}

		if ( *cvar:ufopedia_content eq "description" ) {
			*node:root.content.ufopedia@invis = false
			*node:root.content.ufopedia_scroll@invis = false
			*node:root.content.description@invis = true
			*node:root.content.description_scroll@invis = true
			*node:root.content.requirement@invis = false
			*node:root.itemdescription@invis = true
			*node:root.content.image_top@invis = false
			*node:root.content.image_top2@invis = true
			*node:root.content.image_bigpicture@invis = true
			*node:root.content.mdl_bigpicture@invis = true

			if ( *cvar:mn_rightpanel == 1 ) {
				*node:root.content.ufopedia@width = 400
				*node:root.content.main_panel_small@invis = false
				*node:root.content.main_panel_large@invis = true
			} else {
				*node:root.content.ufopedia@width = 583
				*node:root.content.main_panel_small@invis = true
				*node:root.content.main_panel_large@invis = false
				*node:root.content.navigation.bigpicture@invis = true
			}
			*cvar:mn_uppretext = 0
			cmd "mn_upupdate;"
		} elif ( *cvar:ufopedia_content eq "preresearch" ) {
			*node:root.content.ufopedia@invis = false
			*node:root.content.ufopedia_scroll@invis = false
			*node:root.content.description@invis = true
			*node:root.content.description_scroll@invis = true
			*node:root.content.requirement@invis = false
			*node:root.itemdescription@invis = true
			*node:root.content.image_top@invis = true
			*node:root.content.image_top2@invis = false
			*node:root.content.image_bigpicture@invis = true
			*node:root.content.mdl_bigpicture@invis = true
			if ( *cvar:mn_rightpanel == 1 ) {
				// special pre-research cases
				if ( *cvar:mn_upimage_top eq "techs/bloodspider_autopsy" ) {
					*cvar:mn_upimage_top2 = "techs/bloodspider_dead"
					delete *cvar:mn_upimage_top
				}
				if ( *cvar:mn_upimage_top eq "techs/shevaar_autopsy" ) {
					*cvar:mn_upimage_top2 = "techs/shevaar_dead"
					delete *cvar:mn_upimage_top
				}
				if ( *cvar:mn_upimage_top eq "techs/taman_autopsy" ) {
					*cvar:mn_upimage_top2 = "techs/taman_dead"
					delete *cvar:mn_upimage_top
				}
				if ( *cvar:mn_upimage_top eq "techs/ortnok_autopsy" ) {
					*cvar:mn_upimage_top2 = "techs/ortnok_dead"
					delete *cvar:mn_upimage_top
				}
				if ( *cvar:mn_upimage_top ne "" ) {
					*cvar:mn_upimage_top2 = *cvar:mn_upimage_top
				}
				*node:root.content.ufopedia@width = 400
				*node:root.content.main_panel_small@invis = false
				*node:root.content.main_panel_large@invis = true
			} else {
				*node:root.content.ufopedia@width = 583
				*node:root.content.main_panel_small@invis = true
				*node:root.content.main_panel_large@invis = false
				*node:root.content.navigation.bigpicture@invis = true
			}
			*cvar:mn_uppretext = 1
			cmd "mn_upupdate;"
		} elif ( *cvar:ufopedia_content eq "stats" ) {
			*node:root.content.ufopedia@invis = true
			// just because it looks better
			*node:root.content.ufopedia_scroll@invis = false
			*node:root.content.description@invis = false
			*node:root.content.description_scroll@invis = true
			*node:root.content.requirement@invis = false
			*node:root.itemdescription@invis = false
			*node:root.content.main_panel_small@invis = false
			*node:root.content.main_panel_large@invis = true
			*node:root.content.image_top@invis = false
			*node:root.content.image_top2@invis = true
			*node:root.content.image_bigpicture@invis = true
			*node:root.content.mdl_bigpicture@invis = true
		} elif ( *cvar:ufopedia_content eq "bigpicture" ) {
			*node:root.content.ufopedia@invis = true
			// just because it looks better
			*node:root.content.ufopedia_scroll@invis = false
			*node:root.content.description@invis = true
			*node:root.content.description_scroll@invis = true
			*node:root.content.requirement@invis = true
			*node:root.itemdescription@invis = true
			*node:root.content.main_panel_small@invis = true
			*node:root.content.main_panel_large@invis = false
			*node:root.content.decoration_panel@invis = true
			*node:root.content.decoration_panel2@invis = true
			*node:root.content.image_top@invis = true
			*node:root.content.image_top2@invis = true
			*node:root.content.image_bigpicture@invis = false
			*node:root.content.mdl_bigpicture@invis = false
		}

		// we only show requirements if they are needed
		if ( *cvar:mn_uprequirement eq "" ) {
			*node:root.content.requirement@invis = true
		}
		// we only show deco panels if they are needed
		if ( *cvar:mn_upmodel_top eq "" ) {
			*node:root.content.decoration_panel@invis = true
		}
		if ( *cvar:mn_upmodel_bottom eq "" ) {
			*node:root.content.decoration_panel2@invis = true
		}
		// we only show the image if it is needed
		if ( *cvar:mn_upimage_top eq "" ) {
			*node:root.content.image_top@invis = true
		}
		if ( *cvar:mn_upimage_top2 eq "" ) {
			*node:root.content.image_top2@invis = true
		}
		cmd "del mn_rightpanel;"
	}

	confunc mn_up_empty
	{
		*node:root.content@invis = true
	}

	confunc mn_up_article
	{
		*node:root.content@invis = false
	}

	func init_summary {
		cmd "mn_upcontent;"
		*cvar:ufopedia_content = "description"
		*node:root.summary.list@viewpos = 0
		*node:root.summary.list_scroll@current = 0
		*cvar:up_summary_selection = ""
	}

	func onWindowOpened
	{
		call *node:root.init_summary
		cmd "update_ufopedia_layout;"
	}

	func onWindowClosed
	{
		delete *cvar:up_summary_selection
	}
}
