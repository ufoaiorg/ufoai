// ==================
// UFOPEDIA MENU
// ==================

window ufopedia
{
	{
		pos			"10 50"
		size		"1000 619"
		dragbutton	true
		closebutton	true
		image		ui/popup_alpha_tile
		string		*cvar:mn_uptitle
		modal		true				// we can't click out
	}

	todo todoweaponinfo {
		pos		"370 45"
		string	"Weapon info are missing???"
	}

	// ==================
	// summary
	// ==================

	panel summary {
		{
			pos		"27 95"
			size	"307 501"
			image	"ui/panel_green"
			layout	LAYOUT_BORDER
			padding	10
		}

		optiontree list
		{
			cvar		*cvar:up_summary_selection
			dataid		OPTION_UFOPEDIA
			align		LAYOUTALIGN_MIDDLE
			font		f_small
			color		"0 .78 0 1"
			selectcolor	"1 1 1 1"
			onChange	{
				cmd "ufopedia_click <cvar:up_summary_selection>"
				*node:(abstractscrollbar)root.content.ufopedia_scroll@current 0
				*node:(text)root.content.ufopedia@viewpos 0
				cmd "update_ufopedia_layout"
			}
			onViewChange	{
				*node:(abstractscrollbar)parent.list_scroll@current <viewpos>
				*node:(abstractscrollbar)parent.list_scroll@fullsize <fullsize>
				*node:(abstractscrollbar)parent.list_scroll@viewsize <viewsize>
			}
		}

		vscrollbar list_scroll {
			image		ui/scrollbar_v_green
			align		LAYOUTALIGN_RIGHT
			height		"484"
			current		0
			viewsize	16
			fullsize	50
			onChange	{ *node:parent.list@viewpos <current> }
		}
		
	}

	panel content {
		{
			pos		"350 55"
			size	"638 545"
		}

		panel navigation_panel {
			pos		"0 32"
			size	"630 510"
			image	"ui/panel_green_tab"
		}

		tab navigation {
			cvar	*cvar:ufopedia_content
			pos		"1 0"
			size	"622 43"
			
			option description {
				label	"_Description"
				value	"description"
			}
			option preresearch {
				label	"_Pre research text"
				value	"preresearch"
			}
			option stats {
				label	"_Statistics"
				value	"stats"
			}
			onChange {
				cmd "update_ufopedia_layout"
			}
		}

		// ==================
		// deco
		// ==================

		pic image_top
		{
			image		*cvar:mn_upimage_top
			pos			"432 65"
			size		"163 298"
			preventratio	true
		}

		model mdl_top
		{
			model		*cvar:mn_upmodel_top
			angles		"0 0 120"
			pos			"433 64"
			size		"151 151"
			autoscale	true
			rotatewithmouse	true
		}

		model mdl_bottom
		{
			model		*cvar:mn_upmodel_bottom
			angles		"0 0 120"
			pos			"451 246"
			size		"120 120"
			autoscale	true
			rotatewithmouse	true
		}

		// ==================
		// text and info
		// ==================

		text ufopedia
		{
			dataid		TEXT_UFOPEDIA
			pos			"14 55"
			size		"406 473"
			font		f_small
			lineheight	18
			tabwidth	100
			rows		26
			onViewChange	{
				*node:(abstractscrollbar)parent.ufopedia_scroll@fullsize <fullsize>
				*node:(abstractscrollbar)parent.ufopedia_scroll@current <viewpos>
				*node:(abstractscrollbar)parent.ufopedia_scroll@viewsize <viewsize>
			}
		}

		vscrollbar ufopedia_scroll {
			image		ui/scrollbar_v_green
			pos			"603 50"
			height		"473"
			current		0
			viewsize	26
			fullsize	50
			onChange	{ *node:parent.ufopedia@viewpos <current> }
		}

		text description
		{
			dataid		TEXT_UFOPEDIA_METADATA
			pos			"10 61"
			size		"412 400"
			lineheight	20
			tabwidth	230
			rows		20
			onViewChange	{
				*node:(abstractscrollbar)parent.description_scroll@fullsize <fullsize>
				*node:(abstractscrollbar)parent.description_scroll@current <viewpos>
				*node:(abstractscrollbar)parent.description_scroll@viewsize <viewsize>
			}
		}

		vscrollbar description_scroll {
			image		ui/scrollbar_v_green
			pos			"425 61"
			height		"400"
			current		0
			viewsize	20
			fullsize	20
			hidewhenunused true
			onChange	{ *node:parent.description@viewpos <current> }
		}

		panel requirement {
			{
				pos		"430 373"
				size	"166 128"
				image	"ui/panel_green_slim"
			}

			string label {
				pos "5 5"
				size "120 20"
				font	f_small
				string "_Requirement:"
			}

			textlist techtree
			{
				dataid		TEXT_UFOPEDIA_REQUIREMENT
				pos			"5 31"
				size		"156 92"
				font		f_verysmall
				color		"0 .78 0 1"
				selectcolor	"0 .78 0 1"
				onChange		{ cmd "techtree_click <lineselected>" }
				onViewChange	{
					*node:(abstractscrollbar)parent.techtree_scroll@fullsize <fullsize>
					*node:(abstractscrollbar)parent.techtree_scroll@current <viewpos>
					*node:(abstractscrollbar)parent.techtree_scroll@viewsize <viewsize>
				}
			}

			vscrollbar techtree_scroll {
				image		ui/scrollbar_v_green
				invis		true
				pos			"142 7"
				height		"117"
				current		0
				hidewhenunused true
				onChange	{ *node:parent.techtree@viewpos <current> }
			}
		}
	}

	// ==================
	// weapon info
	// @todo never displayed??? can we fix something
	// ==================

	pic weapon_dec
	{
		visiblewhen "*cvar:mn_changeweapon == 1"
		tooltip		"_Previous weapon chambered for this ammunition"
		image		ui/buttons_small
		pos			"270 704"
		texl		"0 0"
		texh		"16 16"
		mousefx		true
		onClick		{ cmd "mn_decreaseweapon;" }
	}

	pic ammo_dec
	{
		visiblewhen "*cvar:mn_changeweapon == 2"
		tooltip		"_Previous ammunition compatible with this weapon"
		image		ui/buttons_small
		pos			"270 704"
		texl		"0 0"
		texh		"16 16"
		mousefx		true
		onClick		{ cmd "mn_decreaseweapon;" }
	}

	pic weapon_inc
	{
		visiblewhen "*cvar:mn_changeweapon == 1"
		tooltip		"_Next weapon chambered for this ammunition"
		image		ui/buttons_small
		pos			"510 704"
		texl		"16 0"
		texh		"32 16"
		mousefx		true
		onClick		{ cmd "mn_increaseweapon;" }
	}

	pic ammo_inc
	{
		visiblewhen "*cvar:mn_changeweapon == 2"
		tooltip		"_Next ammunition compatible with this weapon"
		image		ui/buttons_small
		pos			"510 704"
		texl		"16 0"
		texh		"32 16"
		mousefx		true
		onClick		{ cmd "mn_increaseweapon;" }
	}

	string header_weapon
	{
		visiblewhen "*cvar:mn_displayweapon == 1"
		string		"_With weapon:"
		pos			"115 704"
		size		"150 20"
		textalign	cl
		font		f_small
	}

	string header_ammo
	{
		visiblewhen "*cvar:mn_displayweapon == 2"
		string		"_With ammo:"
		pos			"115 704"
		size		"150 20"
		textalign	cl
		font		f_small
	}


	string weapon_name
	{
		visiblewhen "*cvar:mn_displayweapon > 0"
		string		*cvar:mn_researchedlinkname
		pos			"285 704"
		size		"230 20"
		textalign	uc
		font		f_small
	}

	zone weapon_name_click
	{
		visiblewhen "*cvar:mn_displayweapon > 0"
		pos			"285 702"
		size		"230 20"
		onClick		{ cmd "mn_upgotoresearchedlink;" }
	}

	/** Firemodes */
	string header_firemode
	{
		visiblewhen "*cvar:mn_displayfiremode == 1"
		string		"_Firemode:"
		pos			"115 724"
		size		"150 20"
		textalign	cl
		font		f_small
	}

	pic firemode_dec
	{
		visiblewhen "*cvar:mn_changefiremode == 1"
		tooltip		"_Previous firemode"
		image		ui/buttons_small
		pos			"270 724"
		texl		"0 0"
		texh		"16 16"
		mousefx		true
		onClick		{ cmd "mn_decreasefiremode;" }
	}

	string firemode_name
	{
		visiblewhen "*cvar:mn_displayfiremode == 1"
		string		*cvar:mn_firemodename
		pos			"285 724"
		size		"230 20"
		textalign	cc
		font		f_small
	}

	pic firemode_inc
	{
		visiblewhen "*cvar:mn_changefiremode == 1"
		tooltip		"_Next firemode"
		image		ui/buttons_small
		pos			"510 724"
		texl		"16 0"
		texh		"32 16"
		mousefx		true
		onClick		{ cmd "mn_increasefiremode;" }
	}

	confunc update_ufopedia_layout {

		// is right column is not empty?
		*cvar:mn_rightpanel = 0
		if ( *cvar:mn_upimage_top ne "" ) { *cvar:mn_rightpanel = 1 } 
		if ( *cvar:mn_upmodel_top ne "" ) { *cvar:mn_rightpanel = 1 } 
		if ( *cvar:mn_upmodel_bottom ne "" ) { *cvar:mn_rightpanel = 1 } 
		if ( *cvar:mn_uprequirement ne "" ) { *cvar:mn_rightpanel = 1 } 

		// invalidate techtree selection
		*content.requirement.techtree@lineselected = -1

		// update tab
		if ( *cvar:mn_uppreavailable == 1 ) {
			cmd "mn_updateoption <path:root>.content.navigation preresearch display" 
		} else {
			cmd "mn_updateoption <path:root>.content.navigation preresearch hide"
			// update the tab if the hiden one is selected
			if ( *cvar:ufopedia_content eq "preresearch" ) {
				*cvar:ufopedia_content = "description"
			} 
		}
		if ( *cvar:mn_upmetadata == 1 ) {
			cmd "mn_updateoption <path:root>.content.navigation stats display" 
		} else {
			cmd "mn_updateoption <path:root>.content.navigation stats hide"
			// update the tab if the hiden one is selected
			if ( *cvar:ufopedia_content eq "stats" ) {
				*cvar:ufopedia_content = "description"
			}
		}

		if ( *cvar:ufopedia_content eq "description" ) {
			*content.ufopedia@invis = false
			*content.ufopedia_scroll@invis = false
			*content.description@invis = true
			*content.description_scroll@invis = true
			*content.requirement@invis = false
			if ( *cvar:mn_rightpanel == 1 ) {
				*content.ufopedia@width = 406
			} else {
				*content.ufopedia@width = 583
			}
			cmd "set mn_uppretext 0; mn_upupdate;"
		} elif ( *cvar:ufopedia_content eq "preresearch" ) {
			*content.ufopedia@invis = false
			*content.ufopedia_scroll@invis = false
			*content.description@invis = true
			*content.description_scroll@invis = true
			*content.requirement@invis = false
			if ( *cvar:mn_rightpanel == 1 ) {
				*content.ufopedia@width = 406
			} else {
				*content.ufopedia@width = 583
			}
			cmd "set mn_uppretext 1; mn_upupdate;"
		} elif ( *cvar:ufopedia_content eq "stats" ) {
			*content.ufopedia@invis = true
			*content.ufopedia_scroll@invis = true
			*content.description@invis = false
			*content.description_scroll@invis = false
			*content.requirement@invis = true
		}

		// we only show requirements if there are needed
		if ( *cvar:mn_uprequirement eq "" ) {
			*content.requirement@invis = true
		}

		cmd "del mn_rightpanel"
	}

	confunc mn_up_empty
	{
		*node:root.content@invis = true
	}

	confunc mn_up_article
	{
		*node:root.content@invis = false
	}

	func init_summary {
		cmd "mn_upcontent;"
		*cvar:ufopedia_content = "description"
		*node:root.summary.list@viewpos = 0
		*node:root.summary.list_scroll@current = 0
		*cvar:up_summary_selection = ""
	}

	func onInit
	{
		call *node:root.init_summary
		cmd "update_ufopedia_layout"
	}

	func onClose
	{
		cmd "mn_fireinit ufopedia;"
		cmd "del up_summary_selection"
	}
}
