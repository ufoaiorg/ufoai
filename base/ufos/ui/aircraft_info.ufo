// ==================
// MAIN AIRCRAFT MENU
// ==================

window aircraft extends iaircraft
{
	// ==================
	// background
	// ==================

	panel info_background {
		pos		"461 444"
		size	"561 320"
		image	"ui/panel_grey_green_large"
	}

	// ==================
	// aircraft
	// ==================
	model aircraft
	{
		model		*cvar:mn_aircraft_model
		pos			"478 107"
		size		"529 260"
		origin		"-45 25 0"
		angles		"160 20 -120"
		autoscale	true
	}

	string aircraft_name
	{
		string		*cvar:mn_aircraftname
		pos			"505 480"
		size		"317 21"
		textalign	ul
		padding		0
	}

	string aircraft_status
	{
		string		*cvar:mn_aircraftstatus
		pos			"770 510"
		size		"229 20"
		textalign	ul
	}

	custombutton aircraft_return
	{
		pos			"726 510"
		image		"ui/button_pack1"
		size		"16 16"
	}

	//	icons below the aircraft

	// ==================
	// additional stuff
	// ==================

	string team_members
	{
		string		"_Team Members:"
		pos			"502 544"
		size		"498 20"
		textalign	cl
		padding		0
	}

	string team_hired
	{
		string		*cvar:mn_hired
		pos			"722 544"
		size		"110 20"
		textalign	cl
		padding		0
	}

	text aircraftinfo
	{
		pos			"505 580"
		size		"495 160"
		lineheight	20
		tabwidth	220
		dataid		TEXT_AIRCRAFT_INFO
		rows		8
	}

	confunc aircraft_checkbuttons
	{
		if ( *cvar:mn_aircraftinbase > 0 ) {
			*aircraft_navigation@disabled = false

			*aircraft_return@tooltip = "_Start Aircraft"
			*aircraft_return@onClick = { cmd "aircraft_start;" }
			*aircraft_return@texl = "0 32"
		} else {
			*aircraft_navigation@disabled = true

			*aircraft_return@tooltip = "_Call back"
			*aircraft_return@onClick = { cmd "aircraft_return;" }
			*aircraft_return@texl = "0 0"
		}

		// absent from base
		if ( *cvar:mn_equipsoldierstate == 1 ) {
			*aircraft_navigation@disabled = true
		}
		// no soldiers
		elif ( *cvar:mn_equipsoldierstate == 2 ) {
			*aircraft_navigation@disabled = false
		}
		// normal
		elif ( *cvar:mn_equipsoldierstate == 3 ) {
			*aircraft_navigation@disabled = false
		}
	}

	/**
	 * @note iaircraft interface
	 */
	confunc aircraft_change
	{
		call *node:root.aircraft_checkbuttons
		*aircraft_list@lineselected = <1>
	}

	func onInit
	{
		*cvar:mn_aircrafttab = "aircraft"
		cmd "mn_select_aircraft <cvar:mn_aircraft_id>;"
		cmd "aircraft_update_list;"
	}
}
