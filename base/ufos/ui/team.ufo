// ==============================
// MULTIPLAYER TEAM CREATION MENU
// ==============================

component teamSoldier extends panel {
	{
		size		"260 25"
	}

	data id {
		string		""
	}

	checkbox_green selected {
		pos			"0 2"
		size		"20 20"
		onChange {
			cmd "team_soldierlist_select <node:parent.id@integer>;"
			cmd "game_toggleactor <node:parent.id@integer> <current>;"
		}
	}

	string name {
		pos			"20 0"
		color		"0 .78 0 1"
		size		"260 25"
		contentalign	ALIGN_CL
		onClick {
			cmd "team_soldierlist_select <node:parent.id@integer>;"
		}
	}
}

window game_team_edit extends objectinfo
{
	{
		background	"background/equipment_bg"
	} // empty properties to fix parsing problem with "image" token (behaviour or property?)

	string s_backpack {
		pos "852 25"
		contentalign ALIGN_UC
		string	"_Backpack"
		color "0 0.6 0 1"
		size "160 25"
	}

	string s_left
	{
		pos "852 150"
		contentalign ALIGN_UC
		string	"_Left Hand"
		color "0 0.6 0 1"
		size "160 25"
	}

	string s_right
	{
		pos "530 150"
		contentalign ALIGN_UC
		string	"_Right Hand"
		color "0 0.6 0 1"
		size "160 25"
	}

	string s_implant
	{
		pos "530 76"
		contentalign ALIGN_UC
		string	"_Implant"
		color "0 0.6 0 1"
		size "160 25"
	}

	string s_headgear
	{
		pos "620 76"
		contentalign ALIGN_UC
		string	"_Headgear"
		color "0 0.6 0 1"
		size "160 25"
	}

	string s_holster
	{
		pos "852 245"
		contentalign ALIGN_UC
		string	"_Holster"
		color "0 0.6 0 1"
		size "160 25"
	}

	string s_belt
	{
		pos "530 245"
		contentalign ALIGN_UC
		string	"_Belt"
		color "0 0.6 0 1"
		size "160 25"
	}

	// ==================
	// Soldier List
	// ==================
	data selected {
	}

	panel soldierlist {
		pos		"55 15"
		size	"260 320"
		padding		6
		layout		LAYOUT_TOP_DOWN_FLOW
		wheelscrollable	true

		onViewChange {
			*node:parent.soldierlist_scroll@fullsize = <fullsize>
			*node:parent.soldierlist_scroll@current = <viewpos>
			*node:parent.soldierlist_scroll@viewsize = <viewsize>
		}
	}
	vscrollbar soldierlist_scroll {
		image		"ui/scrollbar_v_green"
		pos			"317 18"
		height		"320"
		current		0
		viewsize	8
		fullsize	50

		onChange {
			*node:parent.soldierlist@viewpos = <current>
		}
	}

	confunc team_soldierlist_clear {
		call *node:parent.soldierlist@removeAllChild
	}
	confunc team_soldierlist_add {
		call *node:parent.soldierlist@createChild("member<1>", "teamSoldier")
		*node:parent.soldierlist.member<1>.id@integer = <1>
		*node:parent.soldierlist.member<1>.selected@current = <2>
		*node:parent.soldierlist.member<1>.name@string = <3>

		if ( *node:root.selected@integer < 0 ) {
			cmd "team_soldierlist_select <1>;"
			*node:root.selected@integer = <1>
		}
	}
	confunc team_soldierlist_select {
		if ( *node:root.selected@integer >= 0 ) {
			*node:parent.soldierlist.member<node:root.selected@integer>.name@color = "0 .78 0 1"
		}
		*node:root.selected@integer = <1>
		*cvar:mn_ucn = <1>
		if ( *node:root.selected@integer >= 0 ) {
			*node:parent.soldierlist.member<1>.name@color = "1 1 1 1"
			cmd "game_actorselect <node:root.selected@integer>;"
			*node:root.load_warnings@integer = 0
			cmd "mn_updateactorload team_actorload;"
		}
	}

	// ==================
	// category buttons
	// ==================

	confunc team_update_item_list {
		if ( *cvar:mn_equiptype == 0 ) {
			*node:root.equip@filter = FILTER_S_PRIMARY
			*node:root.equip_ammo@filter = FILTER_S_PRIMARY
		}
		elif ( *cvar:mn_equiptype == 1 ) {
			*node:root.equip@filter = FILTER_S_SECONDARY
			*node:root.equip_ammo@filter = FILTER_S_SECONDARY
		}
		elif ( *cvar:mn_equiptype == 2 ) {
			*node:root.equip@filter = FILTER_S_HEAVY
			*node:root.equip_ammo@filter = FILTER_S_HEAVY
		}
		elif ( *cvar:mn_equiptype == 3 ) {
			*node:root.equip@filter = FILTER_S_MISC
			*node:root.equip_ammo@filter = FILTER_S_MISC
		}
		elif ( *cvar:mn_equiptype == 4 ) {
			*node:root.equip@filter = FILTER_S_ARMOUR
			*node:root.equip_ammo@filter = FILTER_S_ARMOUR
		}
		elif ( *cvar:mn_equiptype == 5 ) {
			*node:root.equip@filter = FILTER_S_IMPLANT
			*node:root.equip_ammo@filter = FILTER_S_IMPLANT
		}
		*node:root.equip@viewpos = 0
		*node:root.equip_ammo@viewpos = 0
	}

	tab equip_type
	{
		{
			pos		"400 365"
			size	"505 40"
			cvar	*cvar:mn_equiptype
			onChange {
				cmd "team_update_item_list;"
			}
		}
		option primary {
			label	"_Primary"
			value	"0"
		}
		option secondary {
			label	"_Secondary"
			value	"1"
		}
		option heavy {
			label	"_Heavy"
			value	"2"
		}
		option misc {
			label	"_Misc"
			value	"3"
		}
		option armour {
			label	"_Armour"
			value	"4"
		}
	}

	// ==================
	// item description
	// ==================

	panel itemdescription {
		{
			pos		"30 405"
			size	"350 336"
		}

		string itemname
		{
			string	*cvar:mn_itemname
			pos		"10 6"
			size	"330 20"
			contentalign	ALIGN_UC
		}

		item itemmodel
		{
			pos 		"0 30"
			size 		"350 100"
			src			*cvar:mn_item
			angles		"0 180 90"
			autoscale	true
		}

		text description
		{
			pos			"10 140"
			size		"330 150"
			lineheight	20
			tabwidth	190
			rows		"7"
			dataid		TEXT_ITEMDESCRIPTION
			onViewChange {
				*node:root.itemdescription.description_scroll@fullsize = <fullsize>
				*node:root.itemdescription.description_scroll@current = <viewpos>
			}
			onWheel {
				*node:root.itemdescription.description_scroll@current = <viewpos>
			}
		}
		vscrollbar description_scroll {
			image		"ui/scrollbar_v_green"
			pos			"325 140"
			height		"150"
			current		0
			viewsize	7
			fullsize	7
			hidewhenunused true
			onChange {
				*node:root.itemdescription.description@viewpos = <current>
			}
		}

		// ======================
		// useable weapon or ammo
		// ======================

		string header_item
		{
			string	"_With:"
			pos		"10 290"
			size	"100 16"
			font	"f_small"
		}

		button action_dec
		{
			pos		"120 290"
			icon	"icons/prev"
			size	"16 16"
			onClick	{ cmd "mn_decreaseitem;" }
		}

		string item_name
		{
			string	*cvar:mn_linkname
			pos		"140 290"
			size	"190 16"
			contentalign	ALIGN_CC
			font	"f_small"
		}

		button action_inc
		{
			pos		"334 290"
			icon	"icons/next"
			size	"16 16"
			onClick	{ cmd "mn_increaseitem;" }
		}

		// ==================
		// firemode
		// ==================

		string header_firemode
		{
			string	"_Firemode:"
			pos		"10 314"
			size	"100 16"
			font	"f_small"
		}

		button firemode_dec
		{
			tooltip "_Previous firemode"
			pos		"120 314"
			icon	"icons/prev"
			size	"16 16"
			onClick	{ cmd "mn_decreasefiremode;" }
		}

		string firemode_name
		{
			string	*cvar:mn_firemodename
			pos		"140 314"
			size	"190 16"
			contentalign	ALIGN_CC
			font	"f_small"
		}

		button firemode_inc
		{
			tooltip "_Next firemode"
			pos		"334 314"
			icon	"icons/next"
			size	"16 16"
			onClick	{ cmd "mn_increasefiremode;" }
		}
	}

	// ==================
	// actor stats
	// ==================
	panel actor_statpanel {
		{
			pos		"30 405"
			size	"350 336"
		}

		cmpDefNamedTbar pwr {
			{
				pos "4 9"
			}
			tbar bar   { current *cvar:mn_vpwr }
			string lbl { string "_Strength:" }
			string val { string *cvar:mn_tpwr }
		}

		cmpDefNamedTbar spd {
			{
				pos "4 44"
			}
			tbar bar   { current *cvar:mn_vspd }
			string lbl { string "_Speed:" }
			string val { string *cvar:mn_tspd }
		}

		cmpDefNamedTbar acc {
			{
				pos "4 79"
			}
			tbar bar   { current *cvar:mn_vacc }
			string lbl { string "_Accuracy:" }
			string val { string *cvar:mn_tacc }
		}

		cmpDefNamedTbar mnd {
			{
				pos "4 114"
			}
			tbar bar   { current *cvar:mn_vmnd }
			string lbl { string "_Mind:" }
			string val { string *cvar:mn_tmnd }
		}

		cmpDefNamedTbar cls {
			{
				pos "4 157"
			}
			tbar bar   { current *cvar:mn_vcls image "ui/bar_skill" }
			string lbl { string "_Close Combat:" }
			string val { string *cvar:mn_tcls }
		}

		cmpDefNamedTbar ass {
			{
				pos "4 192"
			}
			tbar bar   { current *cvar:mn_vass image "ui/bar_skill" }
			string lbl { string "_Assault Weapons:" }
			string val { string *cvar:mn_tass }
		}

		cmpDefNamedTbar snp {
			{
				pos "4 227"
			}
			tbar bar   { current *cvar:mn_vsnp image "ui/bar_skill" }
			string lbl { string "_Sniper Rifles:" }
			string val { string *cvar:mn_tsnp }
		}

		cmpDefNamedTbar exp {
			{
				pos "4 262"
			}
			tbar bar   { current *cvar:mn_vexp image "ui/bar_skill" }
			string lbl { string "_High-Explosives:" }
			string val { string *cvar:mn_texp }
		}

	}

	// ==================
	// switch
	// ==================

	// @todo merge with confunc weapon_stats?
	func team_weapon_stats {
		if ( *cvar:mn_itemname ne "" ) {
			*node:root.itemdescription@invis = false
			*node:root.actor_statpanel@invis = true
		} else {
			// switch back to actor view
			*cvar:mn_infotype = 0
		}
	}

	// @todo merge with confunc actor_stats?
	func team_actor_stats {
		*node:root.itemdescription@invis = true
		*node:root.actor_statpanel@invis = false
	}

	tab info {
		{
			pos		"20 365"
			size	"355 40"
			cvar	*cvar:mn_infotype
			onChange {
				if ( *cvar:mn_infotype == 0 ) {
					call *node:root.team_actor_stats
				} elif ( *cvar:mn_infotype == 1 ) {
					call *node:root.team_weapon_stats
				}
			}
		}
		option abilities {
			label	"_Soldier Abilities"
			value	"0"
		}
		option weaponinfo {
			label	"_Object Info"
			value	"1"
		}
	}

	// ==================
	// inventory
	// ==================

	string txt_actorname
	{
		string	*cvar:mn_name
		pos		"544 24"
		size	"300 20"
	}

	model body
	{
		{
			src		*cvar:mn_body
			skin	*cvar:mn_body_skin
			anim	"stand_menu"
			angles	"0 70 90"
			scale	"4 4 4"

			pos		"676 69"
			size	"197 258"
			origin	"0 -30 0"
		}

		model head
		{
			src		*cvar:mn_head
			skin	*cvar:mn_head_skin
			tag		"tag_head"
			scale	"1.0 1.0 1.0"
		}
	}

	/**
	 * @sa CL_InitSkin_f
	 * @sa team_initskin
	 */
	selectbox skins {
		pos		"660 290"
		size	"120 40"
		cvar	*cvar:mn_body_skin
		onChange { cmd "team_changeskin;" }
		dataid	OPTION_MULTIPLAYER_SKINS
	}
	/** @todo add team_changeskinteam callback */

	// ==================
	// Containers (see inventory.ufo for the definition)
	// ==================

	container implant	{ pos "548 104"	onSelect { cmd "object_update <lastselectedid> team_update_item_list;" } }
	container right		{ pos "548 178"	onSelect { cmd "object_update <lastselectedid> team_update_item_list;" } }
	container left		{ pos "872 178"	onSelect { cmd "object_update <lastselectedid> team_update_item_list;" } }
	container backpack	{ pos "872 54"	onSelect { cmd "object_update <lastselectedid> team_update_item_list;" } }
	container belt		{ pos "569 273"	onSelect { cmd "object_update <lastselectedid> team_update_item_list;" } }
	container holster	{ pos "898 270"	onSelect { cmd "object_update <lastselectedid> team_update_item_list;" } }
	container headgear	{ pos "678 104"	onSelect { cmd "object_update <lastselectedid> team_update_item_list;" } }

	container armour	{
		pos "735 100"
		color "1 1 1 0"		// active, but display it transparent
		onChange { cmd "game_actorselect <node:root.selected@integer>;" }
		onSelect { cmd "object_update <lastselectedid> team_update_item_list;" }
	}

	baseinventory equip		{
		pos						"422 415"
		size					"207 317"
		displayweapon			true
		displayammoofweapon		true
		displayunavailableitem	true
		columns					1
		onViewChange {
			*node:root.equip_scroll@current = <viewpos>
			*node:root.equip_scroll@viewsize = <viewsize>
			*node:root.equip_scroll@fullsize = <fullsize>
		}
		onSelect { cmd "object_update <lastselectedid> team_update_item_list 0;" }
		onChange { cmd "mn_updateactorload team_actorload <node:root.load_warnings@integer>;" }
	}

	baseinventory equip_ammo	{
		pos				"702 418"
		size			"207 317"
		displayammo		true
		columns			2
		onViewChange {
			*node:root.equip_ammo_scroll@current = <viewpos>
			*node:root.equip_ammo_scroll@viewsize = <viewsize>
			*node:root.equip_ammo_scroll@fullsize = <fullsize>
		}
		onSelect { cmd "object_update <lastselectedid> team_update_item_list 0;" }
		onChange { cmd "mn_updateactorload team_actorload <node:root.load_warnings@integer>;" }
	}

	vscrollbar equip_scroll {
		image	"ui/scrollbar_v_green"
		pos "629 405"	height	"339"
		hidewhenunused true
		onChange {
			*node:root.equip@viewpos = <current>
		}
	}

	vscrollbar equip_ammo_scroll {
		image	"ui/scrollbar_v_green"
		pos "892 405"	height	"339"
		hidewhenunused true
		onChange {
			*node:root.equip_ammo@viewpos = <current>
		}
	}

	confunc team_actorload {
		*node:root.bt_load@string = "<1>"
		*node:root.bt_load@tooltip = "<2>"
		*node:root.load_warnings@integer = "<4>"
		if ( <3> > 0 ) { *node:root.bt_load@color = "0 1 0 1" }
		elif ( <3> < 0 ) { *node:root.bt_load@color = "1 1 0 1" }
		else { *node:root.bt_load@color = "1 1 1 1" }
		if ( <4> > 0 ) { *node:root.bt_load@flash = true }
		else { *node:root.bt_load@flash = false }
	}

	button bt_load
	{
		pos "604 311"
		contentalign ALIGN_CL
		string	"_0/0 Kg"
		color "1 1 1 1"
		flashcolor "1 0 0 1"
		size "285 22"
		onClick { cmd "mn_updateactorload team_actorload;" }
	}

	data load_warnings {
		integer 0
	}

	// ==================
	// additional stuff
	// ==================

	button bt_go
	{
		tooltip	"_Done"
		background	"buttons/large_blue"
		pos		"940 720"
		size	"64 37"
		icon "icons/return"
		onClick	{ cmd "ui_pop;" }
	}

	func onWindowOpened {
		call *node:root.team_actor_stats
		*cvar:mn_infotype = 0
		*cvar:mn_equiptype = 0
		*node:root.selected@integer = -1
		cmd "team_initskin;team_update_item_list;game_teamupdate;"
	}

	func onWindowClosed
	{
		cmd "game_saveteamstate;"
	}
}

// ==========================
// SAVE POPUP
// ==========================

/**
 * @todo The ui_pop into the change event doesn't work - the workaround is the func onWindowClosed game_teamcomments call
 */
window game_team_save_popup
{
	{
		pos			"264 262"
		size		"360 200"
		dragbutton	true
		closebutton	true
		modal		true
		background		"ui/popup_alpha_tile"
		string		"_Save your team"
	}

	textentry name
	{
		background		"ui/button_green_verysmall"
		color		"0 .78 0 1"
		selectcolor	"1 1 1 1"
		contentalign	ALIGN_CL
		padding		8
		font		"f_verysmall"
		pos			"50 53"
		size		"256 34"
		string		*cvar:mn_teamname
	}

	button save
	{
		pos			"50 90"
		size		"256 30"
		string		"_Save"
		onClick		{ cmd "game_saveteam \"<node:root.name@string>\";ui_pop;game_teamcomments;" }
		font	"f_menusmall"
		color	"0 0.5 0 1"
		selectcolor "1 1 1 1"
		string	"_Save"
		background	"ui/button_green_small"
	}
}

// ==========================
// GENERATE TEAM POPUP
// ==========================

window game_team_new_popup
{
	{
		pos			"264 262"
		size		"360 200"
		dragbutton	true
		closebutton	true
		modal		true
		background		"ui/popup_alpha_tile"
		string		"_Generate a team"
	}

	selectbox team_box
	{
		color			"0.6 0.6 0.6 0.3"
		cvar			*cvar:cl_teamdef
		dataid			OPTION_TEAMDEFS
		pos				"50 53"
		size			"256 34"
	}

	button button_new
	{
		pos			"50 90"
		size		"256 30"
		string		"_Generate Team"
		tooltip		"_Generate a new team with the selected race"
		onClick		{ cmd "game_autoteam *cvar:cl_equip;ui_pop; ui_push game_team_edit;" }
		font		"f_menusmall"
		color		"0 0.5 0 1"
		selectcolor "1 1 1 1"
		string		"_Generate Team"
		background	"ui/button_green_small"
	}
}
