/*
 * @TODO:
 * - Scientists: Need new code in the callbacks to get hired/unhired scientists and to hire/fire them.
 * - Clicking on another base name should take the player to that base (see research_changebase).
 * -- Maybe we need an icon that suggests clicking will switch bases.
 */

window research extends BaseMgmtWindow
{
	{
		fill		true
	}
	// Base name and container panel
	BaseContainerPanel base_container
	{
		panel mainBody
		{
			{ size "715 584" }
			panel header
			{
				{}
				string title
				{
					string			"_Research"
				}
			}

			data selected {
				string	""
			}

			string topics {
				string	"_Topics"
				pos		"45 62"
				size	"365 25"
				font	"f_verysmall"
				color	"0.56 0.81 0.76 1"
			}

			// Unassigned scientists
			button idle_scientists
			{
				icon	"icons/head_scientist"
				pos		"522 40"
				size	"48 48"
				ghost	true
			}
			string idle_scientists_count
			{
				string	"0"
				pos		"560 49"
				size	"100 25"
				font	"f_small_bold"
				color	"0.56 0.81 0.76 1"
				contentalign	ALIGN_CL
			}
			string idle_scientists_label
			{
				string	"_idle scientists"
				pos		"560 62"
				size	"100 23"
				font	"f_verysmall"
				color	"0.56 0.81 0.76 1"
				contentalign	ALIGN_CL
			}

			// List of research techs
			panel techs {
				pos				"1 80"
				size			"713 420"
				layout			LAYOUT_TOP_DOWN_FLOW
				layoutMargin	2
				wheelscrollable	true
				onViewChange {
					*node:parent.techs_scroll@fullsize = <fullsize>
					*node:parent.techs_scroll@current = <viewpos>
					*node:parent.techs_scroll@viewsize = <viewsize>
				}
			}
			vscrollbar techs_scroll {
				image		"ui/scrollbar_v"
				pos			"670 50"
				height		"480"
				current		0
				viewsize	21
				fullsize	21
				hidewhenunused true
				onChange {
					*node:parent.techs@viewpos = <current>
				}
			}

			confunc ui_techlist_clear {
				call *node:parent.techs@removeAllchild
			}
			confunc ui_research_select {
				if ( *node:parent.selected@string ne "" ) {
					*node:parent.techs.tech_<node:parent.selected@string>.title@color = "0.56 0.81 0.76 1"
					*node:parent.techs.tech_<node:parent.selected@string>.rsrc_control.scientists@color = "0.56 0.81 0.76 1"
				}
				*node:parent.selected@string = "<1>"
				*node:parent.techs.tech_<node:parent.selected@string>.title@color = "1 1 1 1"
				*node:parent.techs.tech_<node:parent.selected@string>.rsrc_control.scientists@color = "1 1 1 1"
				cmd "ui_research_getdetails <1>;"
			}
			/**
			 * @brief Add research techs to the list for this base
			 * @param <1> number of item in research tech list
			 */
			confunc ui_techlist_add {
				call *node:parent.techs@createChild("tech_<1>", "ResearchItem")

				*node:parent.techs.tech_<1>.id@string = "<1>"
				*node:parent.techs.tech_<1>.title@string = "<2>"
				*node:parent.techs.tech_<1>.status@current = <4>

				if ( <4> > 0 ) {
					*node:parent.techs.tech_<1>.progress_label@string = "<4>%"
				}

				if ( paramCount >= 5 ) {
					if ( "<5>" eq "base" ) {
						*node:parent.techs.tech_<1>.id@integer = <6>
						*node:parent.techs.tech_<1>.rsrc_baselink.basename@string = "<7> (<3>)"
						*node:parent.techs.tech_<1>.rsrc_baselink@invis = false
						*node:parent.techs.tech_<1>.rsrc_control@invis = true
					}
					if ( "<5>" eq "missing" ) {
						*node:parent.techs.tech_<1>.rsrc_missing@invis = false
						*node:parent.techs.tech_<1>.rsrc_control@invis = true
					}
				} else {
					*node:parent.techs.tech_<1>.rsrc_control.scientists@string = "<3>"
					*node:parent.techs.tech_<1>.rsrc_control@invis = false
				}

				if ( *node:parent.selected@string eq "" ) {
					call *node:parent.ui_research_select ( "<1>" )
				}
			}
			confunc ui_research_update_topic {
				*node:parent.techs.tech_<1>.rsrc_control.scientists@string = "<2>"
			}
		}

		// Base scientists/lab stats
		panel base_stats
		{
			{
				pos				"4 629"
				size			"715 125"
				border			"2"
				bordercolor		"0.527 0.6 0.21 1"
			}
			panel scientists
			{
				{
					pos		"5 0"
					size	"350 84"
				}
				button scientist
				{
					icon	"icons/head_scientist"
					pos		"0 0"
					size	"48 48"
				}
				string scientists_count
				{
					string	"_Scientists hired"
					pos		"40 9"
					size	"300 25"
					font	"f_small_bold"
					color	"0.56 0.81 0.76 1"
					contentalign	ALIGN_CL
				}
				texture data_bg
				{
					pos			"43 29"
					size		"282 8"
					src			"ui/bar_bg"
					ghost		true
				}
				bar data_bar
				{
					pos			"43 29"
					size		"282 8"
					color		"0.56 0.81 0.76 1"
					current		0
					max			1
					ghost		true
				}
				MainMenu3Btn hire
				{
					string			"_HIRE"
					pos				"250 42"
					size			"75 26"
					font			"f_small_bold"
					color			"0 0 0 0.9"
					selectcolor		"0 0 0 1"
					onClick			{ cmd "ui_push employees 1;" }
				}
				MainMenuExitBtn fire
				{
					string			"_FIRE"
					pos				"165 42"
					size			"75 26"
					font			"f_small_bold"
					color			"0 0 0 0.9"
					selectcolor		"0 0 0 1"
					onClick			{ cmd "ui_push employees 1;" }
				}
			}
			panel lab_space
			{
				{
					pos		"365 0"
					size	"350 84"
				}
				button labs
				{
					icon	"icons/research"
					pos		"0 0"
					size	"48 48"
				}
				string labs_space_current
				{
					string	"_Laboratory space"
					pos		"40 9"
					size	"200 25"
					font	"f_small_bold"
					color	"0.56 0.81 0.76 1"
					contentalign	ALIGN_CL
				}
				texture data_bg
				{
					pos			"43 29"
					size		"282 8"
					src			"ui/bar_bg"
					ghost		true
				}
				bar data_bar
				{
					pos			"43 29"
					size		"282 8"
					color		"0.56 0.81 0.76 1"
					current		0
					max			1
					ghost		true
				}
				MainMenu3Btn build_lab
				{
					string			"_BUILD LAB"
					pos				"175 42"
					size			"150 26"
					font			"f_small_bold"
					color			"0 0 0 0.9"
					selectcolor		"0 0 0 1"
					onClick			{ cmd "ui_pop; build_facility true;" }
				}
			}
		}

		/**
		 * @brief Switch to another reserch base
		 * @param[in] <1> New base id
		 * @param[in] <2> Selected item row
		 */
		confunc research_changebase {
			cmd "ui_pop;ui_pop;mn_base_select <1>;ui_push research;"
			cmd "research_select <2>;"
		}

		// Intended to make an empty line.
		confunc research_hide { }

		// Intended to set number of unassigned scientists in bases.
		confunc research_baseinfo { }

		// Other unused confuncs for items that can't be researched.
		confunc research_missingitem { }
		confunc research_missingitemtitle { }
		confunc research_unresearchableitem { }
		confunc research_unresearchableitemtitle { }

		// Called on selected item (highlight selected)
		confunc research_selected { }

		// Set in cp_research_callbacks.c to show research status at the moment
		// Under research, paused, not started yet.
		confunc research_running { }
		confunc research_paused { }
		confunc research_normal { }

		// Intended to update values for an item, called with ui_research_fill
		confunc research_updateitem { }
	}

	// Research item display panel
	panel res_item
	{
		{
			pos		"739 0"
			size	"277 768"
		}
		image img
		{
			pos				"0 80"
			size			"277 632"
			preventratio	true
			contentalign	ALIGN_UC
		}
		model mdl
		{
			angles			"-30 135 45"
			pos				"0 80"
			size			"277 277"
			autoscale		true
		}
		string label
		{
			pos				"0 80"
			size			"277 30"
			color			"0.56 0.81 0.76 1"
			bgcolor			"0 0 0 0.6"
			contentalign	ALIGN_CC
		}
		confunc	ui_research_details
		{
			*node:parent.label@string = "<1>"
			*node:parent.img@src = "<2>"
			*node:parent.mdl@src = "<3>"
		}
	}

	// Back to geoscape
	Geoscape geoscape_button {
		onClick	{
			cmd "ui_pop; ui_pop;"
		}
	}

	// Laboratory count
	BuildingSpace laboratoy
	{
		{ pos "739 5" }

		button smlicon { icon	"icons/building_lab" }
		string label { string	"_Laboratory" }
		button alert { invis true }
	}

	// Fuzzy screen effect (see assets.ufo)
	fuzzyScreen overlay { }

	/**
	 * @brief Passes information on capacity scientists and laboratory space
	 * @param <1> scientists free
	 * @param <2> scientists all
	 * @param <3> labspace free
	 * @param <4> labspace all
	 */
	confunc ui_research_update_caps {
		*node:root.base_container.mainBody.idle_scientists_count@string = "<1>"
		*node:root.base_container.base_stats.scientists.data_bar@current = (<2> - <1>)
		*node:root.base_container.base_stats.scientists.data_bar@max = <2>
		*node:root.base_container.base_stats.lab_space.data_bar@current = (<4> - <3>)
		*node:root.base_container.base_stats.lab_space.data_bar@max = <4>
	}

	// Clear all research items
	func clear_all
	{
		call *node:root.base_container.mainBody.techs@removeAllChild
	}

	func onWindowOpened
	{
		*node:root.base_container.mainBody.selected@string = ""
		cmd "ui_research_fill;"
	}

	func onWindowClosed
	{
		call *node:root.clear_all
	}
}
