// =============
// OPTIONS MENU
// =============
window options
{
	{
		noticepos	"512 110"
	}
	// ===========
	// BACKGROUND
	// ===========
	image background
	{
		src		"background/options_bg"
	}
	// ============
	// HIDE PANELS
	// ============
	func hidepanels
	{
		*node:root.options_overall@invis = true
		*node:root.options_video@invis = true
		*node:root.options_gameplay@invis = true
		*node:root.options_keys@invis = true
		*node:root.options_sound@invis = true
	}
	// =================================================
	// MAIN OPTIONS MENU (OVERALL/VIDEO/SOUND/GAMEPLAY)
	// =================================================
	cmpMenuButton button_overall
	{
		tooltip		"_Overall game settings"
		string		"_Overall"
		pos			"80 170"
		onClick
		{
			call *node:root.hidepanels
			*node:root.options_overall@invis = false
		}
	}
	cmpMenuButton button_video
	{
		tooltip		"_Video settings"
		string		"_Video"
		pos			"80 240"
		onClick
		{
			call *node:root.hidepanels
			*node:root.options_video@invis = false
		}
	}
	cmpMenuButton button_sound
	{
		tooltip		"_Sound settings"
		string		"_Audio"
		pos			"80 310"
		onClick
		{
			call *node:root.hidepanels
			*node:root.options_sound@invis = false
		}
	}
	cmpMenuButton button_gameplay
	{
		tooltip		"_Battlescape camera, gameplay and input settings"
		string		"_Gameplay"
		pos			"80 380"
		onClick
		{
			call *node:root.hidepanels
			*node:root.options_gameplay@invis = false
		}
	}
	cmpMenuButton button_back
	{
		tooltip		"_Leave the options menu"
		string		"_Back"
		pos			"80 470"
		onClick		{ cmd "ui_pop;" }
	}

	// ===============================
	// MAIN OVERALL GAME OPTIONS MENU
	// ===============================
	panel options_overall
	{
		{
			pos			"415 0"
			size		"608 768"
		}

		string overallsettings
		{
			string		"_Overall Game Settings"
			pos			"55 67"
			size		"465 35"
			font		"f_menubig"
			contentalign	ALIGN_CC
			color		"0 0.5 0 1"
		}
		// ======================
		// PLAYER'S NAME SUBMENU
		// ======================
		string current_name
		{
			string 		"_Name"
			pos			"62 110"
			size		"256 20"
			color		"1.0 0.75 0 1"
		}
		// =====================
		// CHANGE PLAYER'S NAME
		// =====================
		textentry slot_name
		{
			string		*cvar:cl_name
			image		"ui/button_green_verysmall"
			color 		"0 .78 0 1"
			selectcolor	"1 1 1 1"
			contentalign	ALIGN_CL
			padding		8
			font		"f_verysmall"
			pos			"72 135"
			size		"192 30"
		}
		// =================
		// LANGUAGE SUBMENU
		// =================
		string language_str
		{
			string		"_Language Settings"
			pos			"287 110"
			size		"312 20"
			color		"1.0 0.75 0 1"
		}
		optionlist select_language
		{
			tooltip		"_Available languages"
			dataid		OPTION_LANGUAGES
			pos			"297 135"
			cvar		*cvar:s_language
			image		"ui/panel_green_slim"
			size		"185 250"
			color		"0 .78 0 1"
			selectcolor	"1 1 1 1"
			padding		6
			contentalign	ALIGN_CC
			onViewchange	{
				*node:root.options_overall.select_language_scroll@current = <viewpos>
				*node:root.options_overall.select_language_scroll@fullsize = <fullsize>
				*node:root.options_overall.select_language_scroll@viewsize = <viewsize>
			}
		}
		vscrollbar select_language_scroll {
			image		"ui/scrollbar_v_green"
			pos			"485 135"
			height		"250"
			current		0
			viewsize	8
			fullsize	22
			onChange		{
				*node:root.options_overall.select_language@viewpos = <current>
			}
		}
		// =========================
		// GEOSCAPE OPTIONS SUBMENU
		// =========================
		string geoscape_options
		{
			string 		"_Geoscape Options"
			pos			"62 415"
			size		"256 20"
			color		"1.0 0.75 0 1"
		}
		// ==================
		// GEOSCAPE 2D/3DMAP
		// ==================
		string m3dmap_str
		{
			tooltip		"_3D or 2D geoscape. To enable this option you have to start a new campaign first."
			string		"_3D Geoscape:"
			pos			"72 440"
			size		"250 20"
		}
		checkbox m3dmap
		{
			current		*cvar:cl_3dmap
			pos			"485 440"
			size		"20 18"
			image		"ui/checkbox_blue"
		}
		// =====================================
		// GEOSCAPE CAMERA MINIMUM/MAXIMUM ZOOM
		// =====================================
		string geocamzoommin_str
		{
			tooltip		"_The minimum/maximum zoom of the geoscape camera"
			string		"_Min/Max Geo-Cam Zoom:"
			pos			"72 465"
			size		"270 20"
		}
		selectbox geocamzoom_min
		{
			{
				tooltip		"_Minimum zoom level of the geoscape camera"
				cvar		*cvar:cl_mapzoommin
				pos			"343 465"
				size		"75 20"
				contentalign	ALIGN_CR
			}
			option geocamzoom_min000
			{
				label	"0.00"
				value	"0"
			}
			option geocamzoom_min020
			{
				label	"0.20"
				value	"0.20"
			}
			option geocamzoom_min040
			{
				label	"0.40"
				value	"0.40"
			}
			option geocamzoom_min060
			{
				label	"0.60"
				value	"0.60"
			}
			option geocamzoom_min080
			{
				label	"0.80"
				value	"0.80"
			}
			// default
			option geocamzoom_min100
			{
				label	"_1.00"
				value	"1.0"
			}
		}
		selectbox geocamzoom_max
		{
			{
				tooltip		"_Maximum zoom level of the geoscape camera"
				cvar		*cvar:cl_mapzoommax
				pos			"418 465"
				size		"75 20"
			}
			option geocamzoom_max20
			{
				label	"_2.0"
				value	"2.0"
			}
			option geocamzoom_max40
			{
				label	"_4.00"
				value	"4.0"
			}
			// default
			option geocamzoom_max60
			{
				label	"_6.00"
				value	"6.0"
			}
			option geocamzoom_max80
			{
				label	"_8.00"
				value	"8.0"
			}
			option geocamzoom_max100
			{
				label	"_10.00"
				value	"10.0"
			}
			option geocamzoom_max120
			{
				label	"_12.00"
				value	"12.0"
			}
		}
		// =====================
		// SEVERAL TIPS SUBMENU
		// =====================
		string several_tips
		{
			string 		"_Several Tips"
			pos			"62 520"
			size		"256 20"
			color		"1.0 0.75 0 1"
		}
		// ===================
		// MOUSEOVER TOOLTIPS
		// ===================
		string tooltip_str
		{
			tooltip		"_Enable mouseover tooltips"
			string		"_Tooltips:"
			pos			"72 545"
			size		"250 20"
		}
		checkbox tooltip
		{
			current		*cvar:mn_show_tooltips
			pos			"485 545"
			size		"20 18"
			image		"ui/checkbox_blue"
		}

		// ================
		// CURSOR TOOLTIPS
		// ================
		string c_tooltip_str
		{
			tooltip		"_Enable tooltips for the cursor"
			string		"_Cursor Tooltips:"
			pos			"72 570"
			size		"250 20"
		}
		checkbox c_tooltip
		{
			current		*cvar:cl_show_cursor_tooltips
			pos			"485 570"
			size		"20 18"
			image		"ui/checkbox_blue"
		}
		// ===============
		// TIP OF THE DAY
		// ===============
		string tipoftheday_string
		{
			string		"_Show 'Tip of the Day':"
			pos			"72 595"
			size		"250 20"
			font		"f_small"
		}
		checkbox tipoftheday
		{
			current		*cvar:cl_showTipOfTheDay
			pos			"485 595"
			size		"20 18"
			image		"ui/checkbox_blue"
		}
		// =====================
		// MENU OPTIONS SUBMENU
		// =====================
		string menu_options
		{
			string 		"_Menu Options"
			pos			"62 650"
			size		"256 20"
			color		"1.0 0.75 0 1"
		}
		// ================
		// PRECACHE MODELS
		// ================
		string precache_str
		{
			string		"_Precache Models:"
			pos			"72 675"
			size		"250 20"
		}
		checkbox precache
		{
			tooltip		"_Precaching of models means having faster menus."
			current		*cvar:cl_precache
			pos			"485 675"
			size		"20 18"
			image		"ui/checkbox_blue"
		}
	}
	// ========================
	// MAIN VIDEO OPTIONS MENU
	// ========================
	panel options_video
	{
		{
			pos			"415 0"
			size		"608 768"
		}

		string videosettings
		{
			string		"_Video Settings"
			pos			"55 67"
			size		"465 35"
			font		"f_menubig"
			contentalign	ALIGN_CC
			color		"0 0.5 0 1"
		}

		tab video_tabs {
			{
				pos		"47 100"
				size	"483 39"
				cvar	*cvar:mn_videotab
				onChange {
					if ( *cvar:mn_videotab eq "general" ) {
						*node:root.options_video.options_general_video@invis = false
						*node:root.options_video.options_advanced_video@invis = true
						*node:root.options_video.options_other_video@invis = true
					} elif ( *cvar:mn_videotab eq "advanced" ) {
						*node:root.options_video.options_general_video@invis = true
						*node:root.options_video.options_advanced_video@invis = false
						*node:root.options_video.options_other_video@invis = true
					} else {
						*node:root.options_video.options_general_video@invis = true
						*node:root.options_video.options_advanced_video@invis = true
						*node:root.options_video.options_other_video@invis = false
					}
				}
			}
			option general {
				label	"_General"
				value	"general"
			}
			option advanced {
				label	"_Advanced"
				value	"advanced"
			}
			option other {
				label	"_Other"
				value	"other"
			}
		}

		panel options_general_video
		{
			{
				pos			"45 145"
				size		"498 680"
			}
			// ============================
			// RESOLUTION SETTINGS SUBMENU
			// ============================
			string resolution_settings
			{
				string 		"_Resolution and Display Settings"
				pos			"22 10"
				size		"256 20"
				color		"1.0 0.75 0 1"
			}

			// ===========
			// RESOLUTION
			// ===========
			string res_str
			{
				tooltip		"_The resolution (x*y) in pixel which UFO:AI should use."
				string		"_Resolution:"
				pos			"32 35"
				size		"240 20"
			}
			// ===============================
			// SELECT SCREEN RESOLUTION RATIO
			// ===============================
			selectbox select_ratio
			{
				{
					tooltip		"_Display only resolutions using this screen ratio."
					cvar		*cvar:mn_vidratio
					pos			"215 37"
					size		"100 20"
					onChange {	cmd "cl_setratiofilter <cvar:mn_vidratio>"	}
				}
				option all
				{
					label	"_All ratios"
					value	"all"
				}
				option r5_4
				{
					label	"5:4"
					value	"1.25"
				}
				option r4_3
				{
					label	"4:3"
					value	"1.3333333"
				}
				option r3_2
				{
					label	"3:2"
					value	"1.5"
				}
				option r16_10
				{
					label	"16:10"
					value	"1.6"
				}
				option r16_9
				{
					label	"16:9"
					value	"1.7777777"
				}
				option custom
				{
					label	"_Others"
					value	"custom"
				}
			}
			// =========================
			// SELECT SCREEN RESOLUTION
			// =========================
			selectbox select_res
			{
				tooltip		"_Select your desired screen resolution."
				cvar		*cvar:vid_mode
				pos			"319 37"
				size		"125 20"
				dataid		OPTION_VIDEO_RESOLUTIONS
			}
			// ===========
			// FULLSCREEN
			// ===========
			string fullscreen_str
			{
				string		"_Fullscreen:"
				pos			"32 60"
				size		"240 20"
			}
			checkbox fullscreen
			{
				tooltip		"_Play UFO:AI in fullscreen."
				current		*cvar:vid_fullscreen
				pos			"445 60"
				size		"20 18"
				image		"ui/checkbox_blue"
			}
			// ===============
			// STRETCH SCREEN
			// ===============
			string stretch_str
			{
				string		"_Stretch the Screen with a 4:3 Ratio:"
				pos			"32 85"
				size		"370 20"
			}
			checkbox stretch
			{
				current		*cvar:vid_strech
				pos			"445 85"
				size		"20 18"
				image		"ui/checkbox_blue"
			}
			// =====================================
			// MONITOR CALIBRATION CONTROLS SUBMENU
			// =====================================
			string calibration_settings
			{
				tooltip		"_Calibrate your display here."
				string 		"_Monitor Calibration Controls"
				pos			"22 140"
				size		"256 20"
				color		"1.0 0.75 0 1"
			}
			// ======
			// GAMMA
			// ======
			string gamma_str
			{
				string		"_Gamma:"
				pos			"32 165"
				size		"170 20"
			}
			image gamma_rahmen
			{
				src		"ui/bar_rahmen"
				pos			"204 167"
			}
			bar gamma_bar
			{
				current		*cvar:vid_gamma
				pos			"213 172"
				size		"238 6"
				color		"0 0.8 0 1"
				max			3.0
				onClick		{ }
			}
			// ============================
			// SCREENSHOT SETTINGS SUBMENU
			// ============================
			string screenshot_settings
			{
				tooltip		"_The default key to make screenshots is F12."
				string 		"_Screenshot Settings"
				pos			"22 320"
				size		"256 20"
				color		"1.0 0.75 0 1"
			}
			// ========================
			// JPEG SCREENSHOT QUALITY
			// ========================
			string jpeg_quality_str
			{
				string		"_jpeg Quality:"
				pos			"32 345"
				size		"170 20"
			}
			image jpeg_quality_rahmen
			{
				src		"ui/bar_rahmen"
				pos			"204 347"
			}
			bar jpeg_quality_bar
			{
				current		*cvar:r_screenshot_jpeg_quality
				pos			"213 352"
				size		"238 6"
				color		"0 0.8 0 1"
				max			100
				onClick		{ }
			}
			// ==================
			// SCREENSHOT FORMAT
			// ==================
			string screenshot_format_str
			{
				tooltip		"_The file format used for compressing and saving your screenshots."
				string		"_Screenshot Format:"
				pos			"32 370"
				size		"170 20"
			}
			selectbox screenshot_format
			{
				{
					cvar		*cvar:r_screenshot_format
					pos			"319 370"
					size		"125 20"
				}
				option screenshot_format_jpeg
				{
					label	"jpg"
					value	"jpg"
				}
				option screenshot_format_png
				{
					label	"png"
					value	"png"
				}
				option screenshot_format_tga
				{
					label	"tga"
					value	"tga"
				}
			}
			// ==============================
			// OTHER DISPLAY OPTIONS SUBMENU
			// ==============================
			string other_display_settings
			{
				string 		"_Other Display Settings"
				pos			"22 440"
				size		"256 20"
				color		"1.0 0.75 0 1"
			}

			// ============================
			// SHOW FRAMES/SECOND RENDERED
			// ============================
			string fps_str
			{
				string		"_Show FPS:"
				pos			"32 465"
				size		"240 20"
			}
			checkbox fps
			{
				tooltip		"_Display the number of rendered frames per second."
				current		*cvar:cl_fps
				pos			"445 465"
				size		"20 18"
				image		"ui/checkbox_blue"
			}

			// ================
			// SHOW INTROVIDEO
			// ================
			string intro_video
			{
				tooltip		"_Do not play the intro movie when starting the game."
				string		"_Hold Back Intro Video:"
				pos			"32 490"
				size		"240 20"
			}
			checkbox intro
			{
				tooltip		"_Default is to play the intro video just once at the first game start."
				current		*cvar:cl_introshown
				pos			"445 490"
				size		"20 18"
				image		"ui/checkbox_blue"
			}
		}

		// =======================
		// ADVANCED VIDEO OPTIONS
		// =======================
		panel options_advanced_video
		{
			{
				pos			"45 145"
				size		"498 680"
			}

			// =========================
			// TEXTURE SETTINGS SUBMENU
			// =========================
			string texture_settings
			{
				string 		"_Texture Settings"
				pos			"22 10"
				size		"256 20"
				color		"1.0 0.75 0 1"
			}
			// =======================
			// TEXTURE RESOLUTION CAP
			// =======================
			string texres_str
			{
				tooltip		"_Sets the maximum size for textures and caps resolutions higher than that value."
				string		"_Resolution Cap:"
				pos			"32 35"
				size		"250 20"
			}
			selectbox texres_box
			{
				{
					tooltip		"_Low means a maximum of 256x256 pixel texture resolution. Extreme means up to 8192x8192 pixel."
					cvar		*cvar:r_maxtexres
					pos			"319 37"
					size		"125 20"
				}
				option low_value {
					label	"_Low"
					value	"256"
				}
				option medium_value {
					label	"_Medium"
					value	"512"
				}
				option normal_value {
					label	"_Normal"
					value	"1024"
				}
				option high_value {
					label	"_High"
					value	"2048"
				}
				option excellent_value {
					label	"_Excellent"
					value	"4096"
				}
				option extrem_value {
					label	"_Extreme"
					value	"8192"
				}
			}

			// =======================
			// TEXTURE FILTERING
			// =======================
			string texmode_str
			{
				tooltip		"_Sets the texture filtering and mipmapping method."
				string		"_Filtering:"
				pos			"32 60"
				size		"250 20"
			}
			selectbox texmode_box
			{
				{
					cvar		*cvar:r_texturemode
					pos			"169 62"
					size		"275 20"
				}

				option low_value {
					label	"_No filtering, no mipmaps"
					value	"GL_NEAREST"
				}
				option medium_value {
					label	"_Bilinear filtering, no mipmaps"
					value	"GL_LINEAR"
				}
				option normal_value {
					label	"_No filtering, mipmaps"
					value	"GL_NEAREST_MIPMAP_NEAREST"
				}
				option high_value {
					label	"_Bilinear filtering, mipmaps"
					value	"GL_LINEAR_MIPMAP_NEAREST"
				}
				option excellent_value {
					label	"_Trilinear filtering, mipmaps"
					value	"GL_NEAREST_MIPMAP_LINEAR"
				}
				option extrem_value {
					label	"_Bilinear filtering, trilinear filtering, mipmaps"
					value	"GL_LINEAR_MIPMAP_LINEAR"
				}
			}

			// ====================
			// TEXTURE COMPRESSION
			// ====================
			string compression_str
			{
				tooltip		"_Enable texture compression for better memory performance, disable for better rendering quality."
				string		"_Compression:"
				pos			"32 85"
				size		"250 20"
			}
			checkbox compression
			{
				current		*cvar:r_ext_texture_compression
				pos			"445 85"
				size		"20 18"
				image		"ui/checkbox_blue"
			}
			// ============
			// TEXTURE LOD
			// ============
			string lod_str
			{
				tooltip		"_Texture Level-of-Detail. Enabling this might result in some blurry textures if you zoom out a little bit."
				string		"_LoD:"
				pos			"32 110"
				size		"250 20"
			}
			checkbox lod
			{
				current		*cvar:r_texture_lod
				pos			"445 110"
				size		"20 18"
				image		"ui/checkbox_blue"
			}
			// ======================
			// ANISOTROPIC FILTERING
			// ======================
			string aniso_str
			{
				tooltip		"_The degree or ratio of anisotropic filtering. High ratios will make distant textures appear more sharp. Very high ratios are only recommended when playing in high resolutions."
				string		"_Anisotropy Level:"
				pos			"32 135"
				size		"250 20"
			}
			string aniso_value
			{
				string		*cvar:r_anisotropic
				pos			"420 135"
				size		"100 20"
			}
			/** @todo update max with GL max anisotropic */
			spinner aniso_change
			{
				tooltip		"_Very high values are only recommended when playing in very high screen resolutions."
				current		*cvar:r_anisotropic
				image		"ui/spinner_blue"
				pos			"448 135"
				min			0
				max			16
				delta		1
			}

			// ========================
			// OPENGL SETTINGS SUBMENU
			// ========================
			string opengl_settings
			{
				string 		"_OpenGL Settings"
				pos			"22 165"
				size		"256 20"
				color		"1.0 0.75 0 1"
			}
			// =============
			// GLSL SHADERS
			// =============
			string glsl_str
			{
				tooltip		"_OpenGL GLSL Shaders are available only if your graphics-card driver fully supports OpenGL 2.0+ and will enhance UFO:AI using advanced shading techniques."
				string		"_GLSL Shaders:"
				pos			"32 190"
				size		"240 20"
			}
			// THIS IS DISABLED UNTIL OUR SHADERS SUPPORTS THIS - see https://sourceforge.net/tracker/index.php?func=detail&aid=3341366&group_id=157793&atid=805242
			/*
			selectbox glsl_ver
			{
				{
					tooltip	"_Sets the OpenGL Shader Version.  See: http://pastebin.com/QjmRus4S"
					cvar	*cvar:r_glsl_version
					pos	"342 190"
					size	"100 20"
				}
				option low_value
				{
					label   "1.10"
					value   "1.10"
				}
				option medium_value
				{
					label   "1.30"
					value   "1.30"
				}
				option normal_value
				{
					label   "1.50"
					value   "1.50"
				}
				option high_value
				{
					label   "3.30"
					value   "3.30"
				}
				option excellent_value
				{
					label   "4.00"
					value   "4.00"
				}
				option extrem_value
				{
					label   "4.10"
					value   "4.10"
				}
			}*/
			checkbox glsl
			{
				tooltip		"_Enable OpenGL GLSL Shaders. Disable this if you experience graphical problems. ATTENTION: May require restart to take effect."
				current		*cvar:r_programs
				pos			"445 190"
				size		"20 18"
				image		"ui/checkbox_blue"
			}
			// ===============
			// VERTEX BUFFERS
			// ===============
			string vbo_str
			{
				tooltip		"_Vertex Buffers are submitting extensible arrays of vertex attributes to be mapped into the GPU for fast drawing."
				string		"_Vertex Buffers:"
				pos			"32 215"
				size		"240 20"
			}
			checkbox vbo
			{
				current		*cvar:r_vertexbuffers
				pos			"445 215"
				size		"20 18"
				image		"ui/checkbox_blue"
			}
			// ==================================================================
			// MULTISAMPLE BUFFERS AND MULTISAMPLE BUFFERS SWAP CONTROL (V-SYNC)
			// ==================================================================
			string samplebuf_swapcontrol_str
			{
				tooltip		"_OpenGL multisample buffers provide a mechanism to antialias all GL primitives. / The Swap Interval selects when a buffer swap will occur. The value '0' means that the swap will occur immediately (also known as 'v-sync off')."
				string		"_Multisample Buffers / Swap Interval:"
				pos			"32 240"
				size		"320 20"
			}
			string samplebuf_value
			{
				tooltip		"_Choose between 0-4 Multisample Buffers. Higher values will provide better antialiasing and will enhance picture quality."
				string		*cvar:r_multisample
				pos			"360 240"
				size		"100 20"
			}
			spinner samplebuf_change
			{
				current		*cvar:r_multisample
				image		"ui/spinner_blue"
				pos			"388 240"
				min			0
				max			4
				delta		1
			}
			string swapcontrol_value
			{
				tooltip		"_Set to '0' to turn v-sync off (faster). Set to '1' or higher to turn v-sync on (better quality)."
				string		*cvar:r_swapinterval
				pos			"420 240"
				size		"100 20"
			}
			spinner swapinterval_change
			{
				current		*cvar:r_swapinterval
				image		"ui/spinner_blue"
				pos			"448 240"
				min			0
				max			2
				delta		1
			}
			// ==============================
			// POSTPROCESSING SHADER EFFECTS
			// ==============================
			string postprocess_str
			{
				tooltip		"_Post-processing shader effects like glowing textures (only available if GLSL Shaders are turned on)."
				string		"_Post-Processing Shader Effects:"
				pos			"32 265"
				size		"370 20"
			}
			checkbox postprocess
			{
				tooltip		"_Enable post processing shader effects. ATTENTION: May require restart to take effect."
				current		*cvar:r_postprocess
				pos			"445 265"
				size		"20 18"
				image		"ui/checkbox_blue"
			}
			// ==================================
			// LIGHT AND SHADOW SETTINGS SUBMENU
			// ==================================
			string light_shadow_settings
			{
				string 		"_Light and Shadow"
				pos			"22 295"
				size		"256 20"
				color		"1.0 0.75 0 1"
			}
			// =======================================================
			// REALTIME LIGHTING AND MAXIMUM NUMBER OF DYNAMIC LIGHTS
			// =======================================================
			string light_dynamiclights_str
			{
				tooltip		"_Realtime lighting of the 3D world in UFO:AI. / Maximum number of dynamic hardware accelerated lights allowed."
				string		"_Realtime Lighting / Max Dynamic Lights:"
				pos			"32 320"
				size		"320 20"
			}
			checkbox light
			{
				current		*cvar:r_lights
				pos			"385 320"
				size		"20 18"
				image		"ui/checkbox_blue"
			}
			string dynamiclights_value
			{
				string		*cvar:r_dynamic_lights
				pos			"420 320"
				size		"100 20"
			}
			spinner dynamiclights_change
			{
				tooltip		"_Change the number of allowed hardware accelerated dynamic lights. ATTENTION: Realtime lighting has to be turned on for this setting to work."
				current		*cvar:r_dynamic_lights
				image		"ui/spinner_blue"
				pos			"448 320"
				min			0
				max			8
				delta		1
			}
			// ====================
			// NORMAL-/BUMPMAPPING
			// ====================
			string bumpmapping_str
			{
				tooltip		"_Also known as bumpmapping."
				string		"_Texture Normalmapping:"
				pos			"32 345"
				size		"240 20"
			}
			checkbox bumpmapping
			{
				tooltip		"_Enable normal-/bumpmapping. ATTENTION: Realtime lighting must be turned on for normalmapping to work."
				current		*cvar:r_bumpmap
				pos			"445 345"
				size		"20 18"
				image		"ui/checkbox_blue"
			}
			// ========
			// SHADOWS
			// ========
			/*
			string shadows_str
			{
				tooltip		"_At the moment these are just drop-shadows."
				string		"_Shadows:"
				pos			"32 370"
				size		"240 20"
			}
			checkbox shadows
			{
				tooltip		"_Enable shadows."
				current		*cvar:r_shadows
				pos			"445 370"
				size		"20 18"
				image		"ui/checkbox_blue"
			}
			*/
			// ===============================
			// LIGHTMAP RESOLUTION BLOCK SIZE
			// ===============================
			string lightmapres_str
			{
				tooltip		"_The lightmap block size defines the resolution of the lightmap. Higher block size means using more detailed lightmaps."
				string		"_Lightmap Block Size:"
				pos			"32 395"
				size		"240 20"
			}
			selectbox lightmapres_box
			{
				{
					tooltip		"_Low means a maximum lightmap resolution of 512x512 pixel. High means up to 2048x2048 pixel."
					cvar		*cvar:r_maxlightmap
					pos			"319 397"
					size		"125 20"
				}
				option low_value {
					label	"_Low"
					value	"256"
				}
				option medium_value {
					label	"_Medium"
					value	"512"
				}
				option normal_value {
					label	"_Normal"
					value	"1024"
				}
				option high_value {
					label	"_High"
					value	"2048"
				}
			}
			// ===============
			// LIGHTMAP SCALE
			// ===============
			string lightmap_str
			{
				tooltip		"_Changing this would scale the lightmap brightness."
				string		"_Lightmap Scale:"
				pos			"32 420"
				size		"150 20"
			}
			image lightmap_rahmen
			{
				src		"ui/bar_rahmen"
				pos			"204 422"
			}
			bar lightmap_bar
			{
				current		*cvar:r_modulate
				pos			"213 427"
				size		"238 6"
				color		"0 .8 0 1"
				max			4.0
				onClick		{ }
			}
			// ========================
			// GEOSCAPE 3DMAP LIGHTING
			// ========================
			string m3dmapambient_str
			{
				tooltip		"_Amount of light emitting from the sun and shining on the earth."
				string		"_Geo Ambient Lighting:"
				pos			"32 445"
				size		"200 20"
			}
			image m3dmapambient_rahmen
			{
				src		"ui/bar_rahmen"
				pos			"204 447"
			}
			bar m3dmapambient_bar
			{
				current		*cvar:cl_3dmapAmbient
				pos			"213 452"
				size		"238 6"
				color		"0 .8 0 1"
				min			-1
				max			3
				onClick		{ }
			}
		}

		panel options_other_video
		{
			{
				pos			"45 145"
				size		"498 680"
			}

			// =======================
			// OTHER SETTINGS SUBMENU
			// =======================
			string othergfx_settings
			{
				string 		"_Other"
				pos			"22 10"
				size		"256 20"
				color		"1.0 0.75 0 1"
			}
			// =======
			// FLARES
			// =======
			string flares_str
			{
				tooltip		"_Flares are used to display light reflection effects. Flare visibility is calculated according to the angle of the surface emitting the light."
				string		"_Flares:"
				pos			"32 35"
				size		"240 20"
			}
			checkbox flares
			{
				current		*cvar:r_flares
				pos			"445 35"
				size		"20 18"
				image		"ui/checkbox_blue"
			}
			// ========
			// CORONAS
			// ========
			string coronas_str
			{
				tooltip		"_Coronas are soft, alpha-blended, rounded polygons."
				string		"_Coronas:"
				pos			"32 60"
				size		"240 20"
			}
			checkbox coronas
			{
				current		*cvar:r_coronas
				pos			"445 60"
				size		"20 18"
				image		"ui/checkbox_blue"
			}
			// ====
			// FOG
			// ====
			string fog_str
			{
				tooltip		"_Fog does not relate to any weather effect here, but when enabled distant textures on the battlescape are faded out."
				string		"_Fog:"
				pos			"32 85"
				size		"240 20"
			}
			checkbox fog
			{
				current		*cvar:r_fog
				pos			"445 85"
				size		"20 18"
				image		"ui/checkbox_blue"
			}
			// =========================
			// WEATHER PARTICLE EFFECTS
			// =========================
			string ptlweather_str
			{
				tooltip		"_Weather effects like rain or snow."
				string		"_Weather Effects:"
				pos			"32 110"
				size		"240 20"
			}
			checkbox ptlweather
			{
				current		*cvar:cl_particleweather
				pos			"445 110"
				size		"20 18"
				image		"ui/checkbox_blue"
			}
			// ================
			// MATERIAL SYSTEM
			// ================
			string materialsys_str
			{
				tooltip		"_The material system is used to display texture animations, effects like rotating or stretching or scrolling textures and manipulate rendering through applying special material properties like specularity or hardness to defined textures."
				string		"_Material System:"
				pos			"32 135"
				size		"240 20"
			}
			checkbox materialsys
			{
				current		*cvar:r_materials
				pos			"445 135"
				size		"20 18"
				image		"ui/checkbox_blue"
			}

			button video_low
			{
				tooltip	"_Switch to low video settings"
				pos		"32 200"
				size	"120 40"
				string	"_Low"
				image	"ui/button_green_small"
				color	"0 0.5 0 1"
				selectcolor "1 1 1 1"
				onClick
				{
					cmd "exec video-low.cfg;"
				}
			}
			button video_medium
			{
				tooltip	"_Switch to medium video settings"
				pos		"162 200"
				size	"120 40"
				string	"_Medium"
				image	"ui/button_green_small"
				color	"0 0.5 0 1"
				selectcolor "1 1 1 1"
				onClick
				{
					cmd "exec video-medium.cfg;"
				}
			}
			button video_high
			{
				tooltip	"_Switch to very high video settings"
				pos		"292 200"
				size	"120 40"
				string	"_High"
				image	"ui/button_green_small"
				color	"0 0.5 0 1"
				selectcolor "1 1 1 1"
				onClick
				{
					cmd "exec video-high.cfg;"
				}
			}
		}
	}

	// ========================
	// MAIN SOUND OPTIONS MENU
	// ========================
	panel options_sound
	{
		{
			pos			"415 0"
			size		"608 768"
		}
		// ===============
		// AUDIO SETTINGS
		// ===============
		string soundsettings
		{
			string		"_Audio Settings"
			pos			"55 67"
			size		"465 35"
			font		"f_menubig"
			contentalign	ALIGN_CC
			color		"0 0.5 0 1"
		}
		// =======================
		// VOLUME CONTROL SUBMENU
		// =======================
		string volume_str
		{
			string		"_Volume Control"
			pos			"62 110"
			size		"240 20"
			color		"1.0 0.75 0 1"
		}
		// =====================
		// ENABLE/DISABLE AUDIO
		// =====================
		string sound_init_str
		{
			tooltip		"_Enable or disable sound."
			string		"_Enable Audio:"
			pos			"72 135"
			size		"240 20"
		}
		checkbox sound_init
		{
			current		*cvar:snd_init
			pos			"485 135"
			size		"20 18"
			image		"ui/checkbox_blue"
		}
		// =============
		// MUSIC VOLUME
		// =============
		string music_str
		{
			string		"_Music:"
			pos			"72 160"
			size		"150 20"
		}
		image music_rahmen
		{
			src		"ui/bar_rahmen"
			pos			"244 162"
		}
		bar music_bar
		{
			current		*cvar:snd_music_volume
			pos			"253 167"
			size		"238 6"
			color		"0 .8 0 1"
			max			128
			onClick		{ }
		}

		// =====================
		// SOUND EFFECTS VOLUME
		// =====================
		string effects_str
		{
			string		"_Sound Effects:"
			pos			"72 185"
			size		"170 20"
		}
		image effects_rahmen
		{
			src		"ui/bar_rahmen"
			pos			"244 187"
		}
		bar effects_bar
		{
			current		*cvar:snd_volume
			pos			"253 192"
			size		"238 6"
			color		"0 .8 0 1"
			max			1.0
			onClick		{ }
		}
		// ============
		// MENU SOUNDS
		// ============
		// on/off only at the moment
		//
		string menusounds_str
		{
			string		"_Menu Sounds:"
			pos			"72 210"
			size		"170 20"
		}
		checkbox menu_sounds_checkbox
		{
			tooltip		"_Turn the menu sounds on or off."
			current		*cvar:ui_sounds
			pos			"485 210"
			size		"20 18"
			image		"ui/checkbox_blue"
		}
		//image menu_sounds_rahmen
		//{
		//	src		"ui/bar_rahmen"
		//	pos			"244 212"
		//}
		//bar menu_sounds_bar
		//{
		//	tooltip		"_You can just turn on/off the menu sounds at the moment."
		//	current		*cvar:mn_sounds
		//	pos			"253 217"
		//	size		"238 6"
		//	color		"0 .8 0 1"
		//	max			1.0
		//	onClick		{ }
		//}
		// ============================
		// OTHER AUDIO OPTIONS SUBMENU
		// ============================
		string other_sound_opt
		{
			string		"_Advanced Audio Options"
			pos			"62 265"
			size		"240 20"
			color		"1.0 0.75 0 1"
		}
		// ==================
		// AUDIO SAMPLE RATE
		// ==================
		string rate_str
		{
			tooltip		"_The sample rate frequency in kHz. Use higher values for better sound quality."
			string		"_Audio Sample Rate:"
			pos			"72 290"
			size		"240 20"
		}
		selectbox rate_box
		{
			{
				cvar		*cvar:snd_rate
				pos			"359 295"
				size		"125 20"
			}
			option low_value
			{
				label	"11025"
				value	"11025"
			}
			option medium_value
			{
				label	"22050"
				value	"22050"
			}
			option normal_value
			{
				label	"44100"
				value	"44100"
			}
			option high_value
			{
				label	"48000"
				value	"48000"
			}
		}
		button rate_test_pic
		{
			tooltip		"_Restart the sound-engine and apply your settings."
			icon		"icons/bordered_magnifying_glass"
			pos			"486 297"
			size		"16 16"
			padding		0
			onClick		{ cmd "snd_restart;" }
		}
	}
	// ========================================
	// MAIN CAMERA/GAMEPLAY/INPUT OPTIONS MENU
	// ========================================
	panel options_gameplay
	{
		{
			pos			"415 0"
			size		"608 768"
		}
		// ==================
		// GAMEPLAY SETTINGS
		// ==================
		string gameplaysettings_str
		{
			string		"_Gameplay Settings"
			pos			"55 67"
			size		"465 35"
			font		"f_menubig"
			contentalign	ALIGN_CC
			color		"0 0.5 0 1"
		}
		// ====================================
		// BATTLESCAPE CAMERA SETTINGS SUBMENU
		// ====================================
		string camera_str
		{
			string		"_Battlescape Camera Settings"
			pos			"62 110"
			size		"240 20"
			color		"1.0 0.75 0 1"
		}
		// ================================
		// BATTLESCAPE CAMERA SCROLL SPEED
		// ================================
		string camspeed_str
		{
			string		"_Scroll Speed:"
			pos			"72 135"
			size		"170 20"
		}
		image camspeed_rahmen
		{
			src		"ui/bar_rahmen"
			pos			"244 137"
		}
		bar camspeed_bar
		{
			current		*cvar:cl_cammovespeed
			pos			"253 142"
			size		"238 6"
			color		"0 .8 0 1"
			min			5
			max			3000
			onClick		{ }
		}
		// ============================
		// BATTLESCAPE CAMERA ROTATION
		// ============================
		string camrot_str
		{
			string		"_Rotation Speed:"
			pos			"72 160"
			size		"170 20"
		}
		image camrot_rahmen
		{
			src		"ui/bar_rahmen"
			pos			"244 162"
		}
		bar camrot_bar
		{
			current		*cvar:cl_camrotspeed
			pos			"253 167"
			size		"238 6"
			color		"0 .8 0 1"
			min			5
			max			1000
			onClick		{ }
		}
		// =======================================================
		// BATTLESCAPE CAMERA MOVE DECELERATION (cl_cammoveaccel)
		// =======================================================
		string cammovedecel_str
		{
			tooltip		"_The move deceleration of the battlescape camera. Lower this value if you want smoother camera movement, increase it if you want cameramovement to decelerate faster or stop immediately."
			string		"_Move Deceleration:"
			pos			"72 185"
			size		"170 20"
		}
		image cammovedecel_rahmen
		{
			src		"ui/bar_rahmen"
			pos			"244 187"
		}
		bar cammovedecel_bar
		{
			current		*cvar:cl_cammoveaccel
			pos			"253 192"
			size		"238 6"
			color		"0 .8 0 1"
			min			0
			max			3000
			onClick		{ }
		}
		// ============================================
		// BATTLESCAPE CAMERA CAMPITCH MINIMUM/MAXIMUM
		// ============================================
		string campitch_str
		{
			string		"_Minimum/Maximum Shift Pitch:"
			pos			"72 210"
			size		"250 20"
		}
		selectbox campitch_min
		{
			{
				tooltip		"_ATTENTION: Setting minimum camera pitch shift below 15 degrees might complicate selecting and commanding your soldiers."
				cvar		*cvar:cl_campitchmin
				pos			"343 210"
				size		"75 20"
				contentalign	ALIGN_CR
			}
			option minus90degrees
			{
				label	"- 90"
				value	"-90"
			}
			option minus45degrees
			{
				label	"- 45"
				value	"-45"
			}
			option minus15degrees
			{
				label	"- 15"
				value	"-15"
			}
			option plus0degrees
			{
				label	"+- 0"
				value	"0"
			}
			// default
			option plus10degrees
			{
				label	"+ 10"
				value	"10"
			}
			option plus15degrees
			{
				label	"+ 15"
				value	"15"
			}
			option plus45degrees
			{
				label	"+ 45"
				value	"45"
			}
		}
		selectbox campitch_max
		{
			{
				tooltip		"_ATTENTION: Setting maximum camera pitch shift over 90 degrees will turn your camera upside down when exceeding that point."
				cvar		*cvar:cl_campitchmax
				pos			"418 210"
				size		"75 20"
			}
			option plus45degrees
			{
				label	"+ 45"
				value	"45"
			}
			option plus60degrees
			{
				label	"+ 60"
				value	"60"
			}
			option plus75degrees
			{
				label	"+ 75"
				value	"75"
			}
			// default
			option plus89degrees
			{
				label	"+ 89"
				value	"89"
			}
			option plus135degrees
			{
				label	"+135"
				value	"135"
			}
			option plus180degrees
			{
				label	"+180"
				value	"180"
			}
		}
		// ========================================
		// BATTLESCAPE CAMERA ZOOM MINIMUM/MAXIMUM
		// ========================================
		string camzoom_str
		{
			string		"_Minimum/Maximum Zoom:"
			pos			"72 235"
			size		"250 20"
		}
		selectbox camzoom_min
		{
			{
				tooltip		"_Default minimum zoom level of the battlescape camera is 0.30."
				cvar		*cvar:cl_camzoommin
				pos			"343 235"
				size		"75 20"
			}
			option zoom_min0
			{
				label	"0.00"
				value	"0"
			}
			option zoom_min005
			{
				label	"0.05"
				value	"0.05"
			}
			option zoom_min010
			{
				label	"0.10"
				value	"0.10"
			}
			option zoom_min015
			{
				label	"0.15"
				value	"0.15"
			}
			option zoom_min020
			{
				label	"0.20"
				value	"0.20"
			}
			option zoom_min025
			{
				label	"0.25"
				value	"0.25"
			}
			// default
			option zoom_min030
			{
				label	"0.30"
				value	"0.3"
			}
		}
		selectbox camzoom_max
		{
			{
				tooltip		"_Default maximum zoom level of the battlescape camera is 3.40."
				cvar		*cvar:cl_camzoommax
				pos			"418 235"
				size		"75 20"
			}
			option zoom_max200
			{
				label	"2.00"
				value	"2.0"
			}
			// default
			option zoom_max340
			{
				label	"3.40"
				value	"3.4"
			}
			option zoom_max600
			{
				label	"6.00"
				value	"6.0"
			}
			option zoom_max800
			{
				label	"8.00"
				value	"8.0"
			}
			option zoom_max1000
			{
				label	"10.00"
				value	"10.0"
			}
			option zoom_max1200
			{
				label	"12.00"
				value	"12.0"
			}
			option zoom_max800
			{
				label	"8.00"
				value	"8.0"
			}
		}
		// ==============================
		// BATTLESCAPE CAMERA ZOOM QUANT
		// ==============================
		string camzoomquant_str
		{
			tooltip		"_The zoom quant of the battlescape camera defines how big the difference between zoom steps will be."
			string		"_Zoom Quant:"
			pos			"72 260"
			size		"170 20"
		}
		selectbox zoom_quant
		{
			{
				tooltip		"_Set zoom quant to low values if you want more control, set this value higher if the zoom is too slow."
				cvar		*cvar:cl_camzoomquant
				pos			"418 260"
				size		"75 20"
			}
			option zoom_quant000
			{
				label	"0.00"
				value	"0"
			}
			option zoom_quant005
			{
				label	"0.05"
				value	"0.05"
			}
			option zoom_quant010
			{
				label	"0.10"
				value	"0.10"
			}
			// default
			option zoom_quant016
			{
				label	"0.16"
				value	"0.16"
			}
			option zoom_quant020
			{
				label	"0.20"
				value	"0.20"
			}
			option zoom_quant025
			{
				label	"0.25"
				value	"0.25"
			}
			option zoom_quant030
			{
				label	"0.30"
				value	"0.30"
			}
		}
		// ==================================
		// BATTLESCAPE CAMERA ISOMETRIC MODE
		// ==================================
		string isomode_str
		{
			tooltip		"_Isometric Mode means the 3D world will be rendered without perspective correction."
			string		"_Isometric Mode:"
			pos			"72 285"
			size		"240 20"
		}
		checkbox isomode
		{
			current		*cvar:r_isometric
			pos			"485 285"
			size		"20 18"
			image		"ui/checkbox_blue"
		}
		// ===============================
		// BATTLESCAPE CAMERA CENTER VIEW
		// ===============================
		string centerview_str
		{
			tooltip		"_Center the camera view automatically at the soldier when selecting it, or at the enemy when he is shooting or appearing."
			string		"_Center View:"
			pos			"72 310"
			size		"170 20"
		}
		checkbox centerview_value
		{
			current		*cvar:cl_centerview
			pos			"485 310"
			size		"20 18"
			image		"ui/checkbox_blue"
		}
		// ======================================
		// BATTLESCAPE GAMEPLAY SETTINGS SUBMENU
		// ======================================
		string gameplaysettings
		{
			string		"_Battlescape Gameplay Settings"
			pos			"62 340"
			size		"240 20"
			color		"1.0 0.75 0 1"
		}
		// ============
		// ACTOR SPEED
		// ============
		string actorsspeed_str
		{
			string		"_Actor Speed:"
			pos			"72 365"
			size		"150 20"
		}
		image actorsspeed_rahmen
		{
			src		"ui/bar_rahmen"
			pos			"244 367"
		}
		bar actorsspeed_bar
		{
			tooltip		"_Choose between 0-9, higher values mean higher actor speed."
			current		*cvar:g_actorspeed
			pos			"253 372"
			size		"238 6"
			color		"0 .8 0 1"
			max			9
			onClick		{ }
		}
		// ============================
		// BATTLESCAPE CONFIRM ACTIONS
		// ============================
		string confirm_str
		{
			tooltip		"_Enabling this will give you a preview of your actions on the battlescape which you then have to confirm."
			string		"_Confirm Actions:"
			pos			"72 390"
			size		"240 20"
		}
		selectbox select_confirmactions
		{
			{
				tooltip		"_'No' means all your orders on the battlescape will be executed immediately. 'Yes' means you will be prompted to confirm all of your orders before the soldier begins to execute them. 'Only for moving' means the path will be displayed before you confirm a soldier's move."
				cvar		*cvar:confirm_actions
				pos			"343 390"
				size		"155 20"
			}
			option confirm_value {
				label	"_Yes"
				value	"1"
			}
			option noconfirm_value {
				label	"_No"
				value	"0"
			}
			option moveconfirm_value {
				label	"_Only for moving"
				value	"2"
			}
		}
		// ======================
		// BATTLESCAPE AUTOSTAND
		// ======================
		string autostand_str
		{
			tooltip		"_Autostand regulates if your soldier should automatically stand up when he is getting the order to move while being crouched."
			string		"_Autostand:"
			pos			"72 415"
			size		"240 20"
		}
		selectbox select_autostand
		{
			{
				cvar		*cvar:cl_autostand
				pos			"343 415"
				size		"155 20"
			}
			option confirm_value
			{
				label	"_Yes"
				value	"1"
			}
			option noconfirm_value
			{
				label	"_No"
				value	"0"
			}
		}
		// ================
		// BATTLESCAPE HUD
		// ================
		string hud_str
		{
			tooltip		"_Select the Head-Up-Display for the battlescape."
			string		"_HUD Design:"
			pos			"72 440"
			size		"250 20"
		}

		// @note selectbox completed by the confunc add_hud_name
		selectbox select_hud
		{
			cvar		*cvar:cl_hud
			pos			"343 440"
			size		"155 20"
		}
		// =================================
		// JOYSTICK/JOYPAD SETTINGS SUBMENU
		// =================================
		string joystick_settings_str
		{
			string		"_Joystick Settings"
			pos			"62 495"
			size		"240 20"
			color		"1.0 0.75 0 1"
		}
		// ================
		// JOYSTICK/JOYPAD
		// ================
		string joystick_enable_str
		{
			string		"_Enable:"
			pos			"72 520"
			size		"240 20"
		}
		checkbox joystick_enable
		{
			current		*cvar:in_joystick
			pos			"485 520"
			size		"20 18"
			image		"ui/checkbox_blue"
		}
		// ==========================
		// JOYSTICK/JOYPAD SELECTION
		// ==========================
		string joystick_str
		{
			string		"_Joystick or Joypad:"
			pos			"72 545"
			size		"150 20"
		}
		selectbox select_joystick
		{
			tooltip		"_Available game controllers."
			cvar		*cvar:in_joystickNo
			dataid		OPTION_JOYSTICKS
			pos			"243 545"
			size		"256 20"
		}
		// ===================
		// JOYSTICK THRESHOLD
		// ===================
		string joystick_threshold_str
		{
			tooltip		"_Adjust the joystick threshold to a high enough value, so movement of the analogue stick will be recognized."
			string		"_Threshold:"
			pos			"72 570"
			size		"150 20"
		}
		image joystick_threshold_rahmen
		{
			src		"ui/bar_rahmen"
			pos			"244 572"
		}
		bar joystick_threshold_bar
		{
			current		*cvar:in_joystickThreshold
			pos			"253 577"
			size		"238 6"
			color		"0 .8 0 1"
			min			0.0
			max			1.0
			onClick		{ }
		}
		// ===============
		// JOYSTICK SPEED
		// ===============
		string joystick_speed_str
		{
			string		"_Speed:"
			pos			"72 595"
			size		"150 20"
		}
		image joystick_speed_rahmen
		{
			src		"ui/bar_rahmen"
			pos			"244 597"
		}
		bar joystick_speed_bar
		{
			current		*cvar:in_joystickSpeed
			pos			"253 602"
			size		"238 6"
			color		"0 .8 0 1"
			min			5
			max			30
			onClick		{ }
		}
		// =============
		// KEY BINDINGS
		// =============
		cmpMenuButton button_keys
		{
			string		"_Show Keys"
			pos			"162 635"
			onClick
			{
				*node:root.options_gameplay@invis = true
				*node:root.options_keys@invis = false
				cmd "mn_init_keylist;"
			}
		}
	}

	/**
	 * Add a hud window the user can use to play battlescape
	 * <1> HUD identifier name
	 * <2> HUD label
	 */
	confunc add_hud_name {
		call *node:root.options_gameplay.select_hud@createchild("option<1>", "option")
		*node:root.options_gameplay.select_hud.option<1>@value = <1>
		*node:root.options_gameplay.select_hud.option<1>@label = <2>
	}

	// ==================
	// SHOW KEY BINDINGS
	// ==================
	panel options_keys
	{
		{
			pos			"415 0"
			size		"608 768"
		}

		string showkeys
		{
			string		"_Key Bindings"
			pos			"55 67"
			size		"465 35"
			font		"f_menubig"
			contentalign	ALIGN_CC
			color		"0 0.79 0 1"
		}
		// only show the current binding - no support for changing them atm
		text keylist
		{
			dataid		TEXT_LIST
			pos			"55 120"
			size		"450 575"
			rows		"28"
			lineheight	"20"
			tabwidth	"120"
			font		"f_verysmall"
			onViewChange
			{
				*node:root.options_keys.keyscroll@fullsize = <fullsize>
				*node:root.options_keys.keyscroll@current = <viewpos>
			}
			onWheel
			{
				*node:root.options_keys.keyscroll@current = <viewpos>
			}
		}
		vscrollbar keyscroll
		{
			image		"ui/scrollbar_v_green"
			pos			"508 120"
			height		"575"
			current		0
			viewsize	28
			fullsize	50
			onChange	{ *node:root.options_keys.keylist@viewpos = <current> }
		}
	}
	func onWindowOpened
	{
		*cvar:mn_vidratio = "all"
		cmd "cl_setratiofilter <cvar:mn_vidratio>"
		call *node:root.hidepanels

		*cvar:mn_videotab = "general"
		*node:root.options_video.options_advanced_video@invis = true
		*node:root.options_video.options_other_video@invis = true
		*node:root.options_video.options_general_video@invis = false

		*node:root.options_overall@invis = false
	}
}
