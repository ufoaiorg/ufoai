// ==================
// IRC MENU
// ==================

// don't change this name!
window irc
{
	/** @todo Remove me */
	{ } // empty properties to fix parsing problem with "image" token (behaviour or property?)

	// ==================
	// background
	// ==================

	image background
	{
		src		"background/irc_bg"
	}

	string title
	{
		string		"_Lobby"
		pos			"45 17"
		size		"933 35"
		textalign	ALIGN_CC
		font		"f_big"
	}

	button button_disconnect
	{
		string		"_Disconnect"
		tooltip		"_Close menu and disconnect"
		image		"ui/button_blue"
		pos			"780 720"
		size		"160 37"
		selectcolor "1 1 1 1"
		font		"f_normal"
		onClick		{ cmd "irc_disconnect; mn_pop;" }
	}

	custombutton button_exit
	{
		tooltip		"_Close menu but stay connected"
		image		"ui/button_background"
		texl		"192 0"
		pos			"950 720"
		size		"64 37"
		icon		return
		onClick		{ cmd "mn_pop;" }
	}

	string users_string
	{
		string		"_Users"
		pos			"735 80"
		size		"270 20"
		textalign	ALIGN_UC
		font		"f_small"
	}

	textlist irc_userlist
	{
		pos			"750 100"
		size		"240 594"
		lineheight	22
		dataid		TEXT_IRCUSERS
		rows		"27"
		onClick		{ cmd "irc_userlist_click <lineselected>;" }
		onRClick	{ cmd "irc_userlist_rclick <lineselected>;" }
		onViewChange {
			*node:root.irc_userlist_scroll@fullsize = <fullsize>
			*node:root.irc_userlist_scroll@current = <viewpos>
			*node:root.irc_userlist_scroll@viewsize = <viewsize>
		}
	}

	vscrollbar irc_userlist_scroll {
		image		"ui/scrollbar_v_green"
		pos			"980 100"
		height		"594"
		current		0
		viewsize	27
		fullsize	27
		hidewhenunused	true
		onChange		{ *node:root.irc_userlist@viewpos = <current> }
	}

	text irc_data
	{
		pos			"30 168"
		size		"660 528"
		rows		"24"
		lineheight  "22"
		dataid		TEXT_IRCCONTENT
		onViewChange {
			*node:root.irc_data_scroll@fullsize = <fullsize>
			*node:root.irc_data_scroll@current = <viewpos>
			*node:root.irc_data_scroll@viewsize = <viewsize>
		}
	}

	vscrollbar irc_data_scroll {
		image		"ui/scrollbar_v_green"
		pos			"701 168"
		height		"528"
		current		0
		viewsize	24
		fullsize	24
		onChange	{ *node:root.irc_data@viewpos = <current> }
	}

	string irc_server_string
	{
		string		*cvar:irc_server
		pos			"412 44"
		size		"200 16"
		textalign	ALIGN_UC
		font		"f_verysmall"
	}

	string irc_server_topic
	{
		string		*cvar:irc_topic
		pos			"84 80"
		size		"512 16"
		textalign	ALIGN_UC
		font		"f_verysmall"
	}

	string irc_server_channel
	{
		string		*cvar:irc_channel
		pos			"730 44"
		size		"240 16"
		textalign	ALIGN_UC
		font		"f_verysmall"
	}

	textentry slot_irc_send_buffer
	{
		string		*cvar:irc_send_buffer
		image		"ui/button_green_verysmall"
		color 		"0 .78 0 1"
		selectcolor	"1 1 1 1"
		textalign	ALIGN_CL
		padding		8
		font		"f_verysmall"
		pos			"30 710"
		size		"690 34"
		bgcolor		"0.5 0.5 0.5 0.2"
		clickoutabort	true
		onChange	{
			cmd "irc_chanmsg; activate_irc_textentry;"
		}
	}

	confunc activate_irc_textentry {
		call *node:root.slot_irc_send_buffer@edit
	}

	// ==================
	// init
	// ==================
	func onInit
	{
		cmd "irc_activate; activate_irc_textentry;"
	}

	// ==================
	// close
	// ==================
	func onClose
	{
		cmd "irc_deactivate;"
	}
}

// when your nick name is already in use you get this popup
window irc_changename
{
	{
		pos			"264 262"
		size		"496 247"
		dragbutton	true
		closebutton	true
		image		"ui/popup_alpha_tile"
		string		"_Name is already in use"
	}

	string popup_text
	{
		string		"_Your name is already in use or is invalid."
		pos			"26 58"
		size		"446 64"
		longlines	LONGLINES_WRAP
	}

	// ===================
	// NAME
	// ===================
	string current_name
	{
		string		"_Name:"
		pos			"26 128"
		size		"300 20"
		color		"1 1 1 1"
	}
	textentry slot_name
	{
		string		*cvar:cl_name
		image		"ui/button_green_verysmall"
		color		"0 .78 0 1"
		selectcolor	"1 1 1 1"
		textalign	ALIGN_CL
		padding		8
		font		"f_verysmall"
		pos			"170 122"
		size		"185 34"
	}

	cmpPopupButton bt_ok
	{
		string		"_Retry"
		tooltip		"_Retry with your changed name"
		pos			"110 192"
		onClick		{ cmd "mn_pop;irc_disconnect;irc_connect;" }
	}

	cmpCancelButton bt_cancel
	{
		pos			"296 192"
		onClick		{ cmd "mn_pop;irc_disconnect;mn_pop;" }
	}
}

// when you aren't connected you get this popup
// don't change this name or change it in the code, too
window irc_popup
{
	{
		pos			"264 262"
		size		"496 247"
		dragbutton	true
		closebutton	true
		image		"ui/popup_alpha_tile"
		string		"_Not connected"
	}

	string popup_text
	{
		string		"_You are not connected to the lobby. Do you want to connect?"
		pos			"26 58"
		size		"446 128"
		longlines	LONGLINES_WRAP
	}

	cmpPopupButton bt_ok
	{
		string		"_Connect"
		tooltip		"_Connect to the lobby"
		pos			"110 192"
		onClick		{ cmd "irc_connect; mn_pop; mn_push irc;" }
	}

	cmpCancelButton bt_cancel
	{
		pos			"296 192"
		onClick		{ cmd "mn_pop;" }
	}
}
