// ==================
// EMPLOYEES MENU
// ==================

component cmpSelect extends CheckBox
{
		current		0
		size		"20 20"
		onChange	{ cmd "employee_hire <num>; employee_equipcheck;" }
		onRClick		{ cmd "employee_delete <num>; employee_equipcheck;" }
}

component cmpEmployee extends panel
{
	{
		size "360 38"
	}

	string name
	{
		size		"280 38"
		pos			"15 0"
		color		"0.56 0.81 0.76 1"
		onClick		{
			cmd "employee_select <node:parent@num>;"
			cmd "set_healthbar_color;"
		}
	}
	CheckBox select
	{
		current		0
		size		"20 20"
		pos			"325 9"
		onChange	{ cmd "employee_hire <node:parent@num>; employee_equipcheck;" }
		onRClick	{ cmd "employee_delete <node:parent@num>; employee_equipcheck;" }
	}
}

window employees
{
	{
		fill		true
	}

	// Base name and container panel
	BaseContainerPanel base_container
	{
		// hireand fire panel
		panel mainBody
		{
			// Title bar
			panel header
			{
				{}
				string title
				{
					string			"_Hire and Fire"
				}
			}


			// ==================
			// tabs
			// ==================

			tab empl_switch {
				{
					cvar		*cvar:mn_hiretype
					pos			"1 36"
					size		"713 39"
					onChange {
						cmd "employee_init <cvar:mn_hiretype>;"
						*node:parent.employees.employee_list@lineselected = 0
						*node:parent.employees.employee_list@viewpos = 0
						*node:parent.employees.scroll@current = 0
					}
				}
				option soldier {
					label	"_Soldiers"
					value	"0"
					icon	"icons/smallhead_soldier"
				}
				option pilot {
					label	"_Pilots"
					value	"3"
					icon	"icons/smallhead_pilot"
				}
				option worker {
					label	"_Workers"
					value	"2"
					icon	"icons/smallhead_worker"
				}
				option scientist {
					label	"_Scientists"
					value	"1"
					icon	"icons/smallhead_scientist"
				}
			}

			panel employees
			{
				{
					pos			"5 75"
					size		"520 460"
				}


				// ==================
				// names list
				// ==================

				rows list_background {
					pos			"5 5"
					size		"380 440"
					lineheight	38
				}

				panel scroll_background {
					pos			"333 10"
					size		"40 440"
					bgcolor		"0 0 0 0.5"
				}

				panel list_border {
					pos				"5 5"
					size			"393 455"
					padding			6
					layout			LAYOUT_TOP_DOWN_FLOW
					layoutMargin	2
				}

				/**
				 * @brief Disable checkbox (employee is away from base)
				 * @param[in] <1> row of the employee in the view
				 */
				confunc employeedisable
				{
					*node:parent.bt_employee<1>.select@current = 1
					*node:parent.bt_employee<1>.select@invis = false
					*node:parent.bt_employee<1>.select@disabled = true
					*node:parent.bt_employee<1>.select@tooltip = "_Employee is away"
				}

				/**
				 * @brief Enable checked checkox (Employee can be fired)
				 * @param[in] <1> row of the employee in the view
				 */
				confunc employeefire
				{
					*node:parent.bt_employee<1>.select@current = 1
					*node:parent.bt_employee<1>.select@invis = false
					*node:parent.bt_employee<1>.select@disabled = false
					*node:parent.bt_employee<1>.select@tooltip = "_Unhire - Right click to remove"
				}

				/**
				 * @brief Enable unchecked checkox (Employee can be hired)
				 * @param[in] <1> row of the employee in the view
				 */
				confunc employeehire
				{
					*node:parent.bt_employee<1>.select@current = 0
					*node:parent.bt_employee<1>.select@invis = false
					*node:parent.bt_employee<1>.select@disabled = false
					*node:parent.bt_employee<1>.select@tooltip = "_Hire - Right click to remove"
				}

				/**
				 * @brief Hide checkox (Empty slot)
				 * @param[in] <1> row of the employee in the view
				 */
				confunc employeehide
				{
					*node:parent.bt_employee<1>.select@invis = true
				}

				confunc employee_equipcheck {
					if ( *cvar:mn_hiresoldiers > 0 ) {
						*node:employees.bt_equipsoldiers@disabled = false
					} else {
						*node:employees.bt_equipsoldiers@disabled = true
					}
				}

				cmpEmployee bt_employee0
				{
					num 0
					pos "0 15"
				}

				cmpEmployee bt_employee1
				{
					num 1
					pos "0 53"
				}

				cmpEmployee bt_employee2
				{
					num 2
					pos "0 91"
				}

				cmpEmployee bt_employee3
				{
					num 3
					pos "0 129"
				}

				cmpEmployee bt_employee4
				{
					num 4
					pos "0 167"
				}

				cmpEmployee bt_employee5
				{
					num 5
					pos "0 205"
				}

				cmpEmployee bt_employee6
				{
					num 6
					pos "0 243"
				}

				cmpEmployee bt_employee7
				{
					num 7
					pos "0 281"
				}

				cmpEmployee bt_employee8
				{
					num 8
					pos "0 319"
				}

				cmpEmployee bt_employee9
				{
					num 9
					pos "0 357"
				}

				cmpEmployee bt_employee10
				{
					num 10
					pos "0 395"
				}

				cmpEmployee bt_employee11
				{
					num 11
					pos "0 433"
				}

				cmpEmployee bt_employee12
				{
					num 12
					pos "0 471"
				}

				cmpEmployee bt_employee13
				{
					num 13
					pos "0 519"
				}

				/**
				 * @param[in] <1> number of employee in the list
				 */
				confunc hire_update_number {
					*node:parent.scroll@fullsize = <1>
				}

				confunc hire_fix_scroll {
					*node:parent.scroll@current = <1>
					*node:parent.list_background@current = <1>
				}

				confunc hire_select {
					*node:parent.employee_list@lineselected = <1>
				}

				// Ugly, but this is needed to prevent the call from beeing executed before employee_select is
				confunc set_healthbar_color {
					call *node:root.actorhealth.hp.setColour
				}
				textlist employee_list
				{
					dataid			TEXT_LIST
					pos				"15 5"
					width			280
					lineheight		38
					tabwidth		100
					rows			"14"
					color			"0.56 0.81 0.76 1"
					selectcolor		"1 1 1 1"
					onClick			{
						cmd "employee_select <lineselected>;"
						cmd "set_healthbar_color;"
					}
					onViewChange	{
						*node:parent.scroll@fullsize = <fullsize>
						*node:parent.scroll@current = <viewpos>
						*node:parent.scroll@viewsize = <viewsize>
						cmd "employee_scroll <viewpos>;"
					}
				}

				vscrollbar scroll
				{
					image		"ui/scrollbar_v"
					pos			"368 5"
					height		"448"
					current		0
					viewsize	15
					fullsize	50
					onChange	{
						*node:parent.employee_list@viewpos = <current>
						cmd "employee_scroll <current>;"
					}
				}
			}

			panel placeholder
			{
				{
					pos			"530 75"
					size		"183 460"
					contentalign	ALIGN_CC
				}

				string pltext
				{
					string		"Placeholder"
					align		LAYOUTALIGN_MIDDLE
				}
			}
		}
	}

	actor_info local
	{
		textentry name
		{
			visiblewhen "*cvar:mn_show_employee >= 1"
			string		*cvar:mn_name
			tooltip		"_Rename your unit"
			pos			"3 0"
			size		"290 20"
			color		"0.56 0.81 0.76 1"
			onChange	{ cmd "employee_changename;" }
		}

		image	rank_img
		{
			visiblewhen "*cvar:mn_show_employee >= 1"
		}

		model body
		{
			{
				visiblewhen "*cvar:mn_show_employee >= 1"
			}

			model head
			{
				visiblewhen "*cvar:mn_show_employee >= 1"
			}
		}

		string rank_lbl
		{
			visiblewhen "*cvar:mn_show_employee >= 1"
		}


		panel soldier_info {
			{
				visiblewhen "*cvar:mn_show_employee == 1"
			}
		}
	}

	MainMenu3Btn bt_equipsoldiers
	{
		visiblewhen "*cvar:mn_show_employee == 1"
		string	  "_Equip soldiers"
		pos			"760 380"
		size		"198 30"
		font		"f_small_bold"
		color		"0 0 0 0.9"
		selectcolor	"0 0 0 1"
		onClick	{
			cmd "ui_push equipment -1;"
		}
	}

/*	BuildingSpace quarters
	{
		{ pos "739 0" }

		button smlicon { icon	"icons/building_quarters" }
		string label { string	"_Living Quarters" }
		button alert { invis true }
	}
*/
	string label_people {
		string	"_Occupation"
		font	"f_normal_bold"
		pos		"740 08"
		size	"140 20"
		contentalign	ALIGN_UC
	}
	string number_people {
		string	*cvar:mn_hirepeople
		font	"f_normal_bold"
		pos		"880 08"
		size	"100 20"
		contentalign	ALIGN_UC
	}

	// ==================
	// buttons
	// ==================

	panel global_info {
		{
			pos		"740 30"
			size	"250 70"
		}

		button icon_soldier {
			icon	"icons/head_soldier"
			pos		"10 8"
			size	"42 42"
		}
		string number_soldier {
			string	*cvar:mn_hiresoldiers
			pos		"12 48"
			size	"42 20"
			contentalign	ALIGN_UR
		}

		button icon_pilot {
			icon	"icons/head_pilot"
			pos		"70 8"
			size	"42 42"
		}
		string number_pilot {
			string	*cvar:mn_hirepilots
			pos		"72 48"
			size	"42 20"
			contentalign	ALIGN_UR
		}

		button icon_worker {
			icon	"icons/head_worker"
			pos		"130 8"
			size	"42 42"
		}
		string number_worker {
			string	*cvar:mn_hireworkers
			pos		"132 48"
			size	"42 20"
			contentalign	ALIGN_UR
		}

		button icon_scientist {
			icon	"icons/head_scientist"
			pos		"190 8"
			size	"42 42"
		}
		string number_scientist {
			string	*cvar:mn_hirescientists
			pos		"192 48"
			size	"42 20"
			contentalign	ALIGN_UR
		}
	}

	abilities hire_abilities
	{
		visiblewhen "*cvar:mn_show_employee == 1"
	}

	skillbars hire_skills
	{
		visiblewhen "*cvar:mn_show_employee == 1"
	}

	skillspilot pilotskills
	{
		visiblewhen "*cvar:mn_show_employee == 2"
	}

	healthbar actorhealth
	{
		{
			visiblewhen "*cvar:mn_show_employee <= 2"
		}
	}


	// ==================
	// init function
	// ==================

	func onWindowOpened
	{
		*node:root.base_container.mainBody.employees.employee_list@viewpos = 0
		*node:root.base_container.mainBody.employees.employee_list@lineselected = 0
		*node:root.base_container.mainBody.employees.scroll@current = 0
		cmd "employee_init <1>;"
		*cvar:mn_hiretype = <1>
		cmd "employee_update_count; employee_equipcheck;"
	}

	func onWindowClosed
	{
		// call base init function because the amount of workers, scientists and
		// so on can also change the button state in the base view
		cmd "base_init;"
	}

	// Fuzzy screen effect (see assets.ufo)
	fuzzyScreen overlay { }
}
