// ==================
// AIRCRAFT PILOTS MENU
// ==================

component cmpActorSelect cmpPilotSelect {
	{
		num "0"
	}
	checkbox check_actor
	{
		tooltip	"_Add/remove this pilot"
		onChange { cmd "pilot_hire <node:parent@num> <cvar:mn_relative_id>;" }
	}

	string txt_actor
	{
		onClick { cmd "pilot_select <node:parent@num> <cvar:mn_relative_id>;" }
	}

}


window aircraft_pilots extends iaircraft
{


	// ==================
	// background
	// ==================

	panel actors_background {
		pos		"18 443"
		size	"329 321"
		image	"ui/panel_grey_green_large"
	}

	panel actormodel_background {
		pos		"358 443"
		size	"205 321"
		image	"ui/panel_grey_green_large"
	}

	panel actorstats_background {
		pos		"564 443"
		size	"458 321"
		image	"ui/panel_grey_green_large"
	}

	// ==================
	// aircraft
	// ==================

	model aircraft
	{
		src			*cvar:mn_aircraft_model
		pos			"478 107"
		size		"529 260"
		origin		"-45 25 0"
		angles		"160 20 -120"
		autoscale	true
	}

	image aircraft_slot
	{
		src	"ui/button_square"
		pos		"494 120"
		size	"64 64"
		texh	"64 64"
	}

	model assigned_pilot_body
	{
		{
			src		*cvar:mn_pilot_body
			skin	*cvar:mn_pilot_skin
			anim	"stand_still"
			angles	"0 90 90"
			scale	"5.5 5.5 5.5"
			pos		"495 121"
			size	"62 62"
			origin	"0 70 0"
		}

		model assigned_pilot_head
		{
			src		*cvar:mn_pilot_head
			skin	*cvar:mn_pilot_skin
			tag		"tag_head"
		}
	}

	string assigned_pilot_name
	{
		string		*cvar:mn_pilot_name
		pos			"480 185"
		size		"200 20"
		textalign	ALIGN_UL
	}

	// ==================
	// pilot selected
	// ==================

	image	rank_img {
		pos "378 465"
		size "50 50"
		src *cvar:mn_chrrank_img
	}

	model body
	{
		{
			src		*cvar:mn_body
			skin	*cvar:mn_skin
			anim	"stand2"
			angles	"0 70 90"
			scale	"5.5 5.5 5.5"
			pos		"378 465"
			size	"167 278"
			origin	"0 -35 0"
		}

		model head
		{
			src		*cvar:mn_head
			skin	*cvar:mn_skin
			tag		"tag_head"
			scale	"1.0 1.0 1.0"
		}
	}

	// ==================
	// pilot list
	// ==================

	/**
	 * @param[in] <1> id of employee in the list
	 */
	confunc aircraft_pilot_usedelsewhere
	{
		*node:root.actor_select<1>.txt_actor@color = ".50 .50 .50 1"
		*node:root.actor_select<1>.check_actor@current = 0
		*node:root.actor_select<1>.check_actor@tooltip = "_Pilot is assigned to another craft"
		*node:root.actor_select<1>.check_actor@disabled = true
		*node:root.actor_select<1>.check_actor@invis = false
		*node:root.actor_select<1>.txt_actor@invis = false
	}

	/**
	 * @param[in] <1> id of employee in the list
	 */
	confunc aircraft_pilot_unassigned {
		*node:root.actor_select<1>.txt_actor@color = "0 .78 0 1"
		*node:root.actor_select<1>.check_actor@disabled = false
		*node:root.actor_select<1>.check_actor@current = 0

		*node:root.actor_select<1>.check_actor@invis = false
		*node:root.actor_select<1>.txt_actor@invis = false
		if ( *cvar:mn_pilotassigned == 1 ) {
			*node:root.actor_select<1>.check_actor@disabled = true
			*node:root.actor_select<1>.check_actor@tooltip = "_A pilot is already assigned to this craft"
		} else {
			*node:root.actor_select<1>.check_actor@tooltip = "_Assign pilot to this craft"
		}
	}

	/**
	 * @param[in] <1> id of employee in the list
	 */
	confunc aircraft_pilot_assigned {
		*node:root.actor_select<1>.txt_actor@color = "0 .78 0 1"
		*node:root.actor_select<1>.check_actor@disabled = false
		*node:root.actor_select<1>.check_actor@current = 1
		*node:root.actor_select<1>.check_actor@tooltip = "_Remove pilot from this craft"
		*node:root.actor_select<1>.check_actor@invis = false
		*node:root.actor_select<1>.txt_actor@invis = false
	}

	/**
	 * @param[in] <1> id of employee in the list
	 */
	confunc aircraft_pilot_holdsequip {
	}

	/**
	 * @param[in] <1> id of employee in the list
	 */
	confunc aircraft_pilot_holdsnoequip {
	}

	/**
	 * @param[in] <1> id of employee in the list
	 */
	confunc aircraft_pilot_unusedslot {
		*node:root.actor_select<1>.check_actor@invis = true
		*node:root.actor_select<1>.txt_actor@invis = true
	}

	/**
	 * @param[in] <1> number of employee in the list
	 */
	confunc aircraft_pilot_list_size {
		*node:root.scroll_employees@fullsize = <1>
	}

	/**
	 * @param[in] <1> number of employee in the list
	 */
	confunc aircraft_pilot_selected {
		*node:root.actor_select<1>.txt_actor@color = "1 1 1 1"
	}

	cmpPilotSelect actor_select0 {
		pos "38 470"
	}

	cmpPilotSelect actor_select1 {
		pos "38 505"
	}

	cmpPilotSelect actor_select2
	{
		pos			"38 540"
	}

	cmpPilotSelect actor_select3
	{
		pos			"38 575"
	}

	cmpPilotSelect actor_select4
	{
		pos			"38 610"
	}

	cmpPilotSelect actor_select5
	{
		pos			"38 645"
	}

	cmpPilotSelect actor_select6
	{
		pos			"38 680"
	}

	cmpPilotSelect actor_select7
	{
		pos			"38 715"
	}

	vscrollbar scroll_employees
	{
		image	"ui/scrollbar_v_green"
		pos "310 463"	height	"282"
		current 0
		viewsize 8
		fullsize 50
		onChange {
			cmd "update_pilot_list 8 <current>"
			*cvar:mn_relative_id = <current>
		}
	}

	// ==================
	// additional stuff
	// ==================

	/**
	 * @note iaircraft interface
	 */
	confunc aircraft_change
	{
		cmd "update_pilot_list 8 0;pilot_select 0;set mn_show_employee 1;"
		*node:root.aircraft_list@lineselected = <1>
		// absent from base, go back to the main GUI
		if ( *cvar:mn_equipsoldierstate == 1 ) {
			cmd "ui_pop;ui_push aircraft;"
		}
	}

	func onWindowOpened
	{
		*cvar:i = 0
		while ( *cvar:i < 8 ) {
			*node:root.actor_select<cvar:i>@num = <cvar:i>
			*node:root.actor_select<cvar:i>.txt_actor@string = *cvar:mn_ename<cvar:i>
			*cvar:i = ( *cvar:i + 1 )
		}
		delete *cvar:i

		cmd "mn_select_aircraft <cvar:mn_aircraft_id>"
		*cvar:mn_relative_id = 0
		cmd "update_pilot_list 8 0;pilot_select 0;set mn_show_employee 1;"
	}
}
