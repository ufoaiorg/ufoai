// ===================
// BASE RESEARCH MENU
// ===================

component cmpResearchItem extends panel {
	{
		size		"590 50"
	}

	// use a node icon, instead of a button
	button img_status {
		icon		"icons/bordered_question"
		pos			"0 17"
		size		"16 16"
		padding		0
		onClick		{ cmd "research_select <node:parent@num>;" cmd "mn_show_ufopedia;" }
		tooltip		"_Open UFOPaedia"
	}

	string txt_item	{
		pos			"20 0"
		size		"355 50"
		color		"0 .78 0 1"
		longlines	LONGLINES_PRETTYCHOP
		contentalign	ALIGN_CL
		onClick		{ cmd "research_select <node:parent@num>;" }
	}

	string txt_assigned	{
		pos			"345 0"
		size		"40 50"
		color		"0 .78 0 1"
		contentalign	ALIGN_CR
	}

	string txt_rsstatus	{
		pos			"480 0"
		size		"60 50"
		color		"0 .78 0 1"
		contentalign	ALIGN_CR
	}

	spinner_horiz bt_rs_change {
		tooltip		"_Add/Remove scientist"
		pos			"390 0"
		inverted	true
		min			0
		current		10000
		max			20000
		delta		1
		onChange	{
			cmd "research_select <node:parent@num>;"
			cmd "mn_rs_change <node:parent@num> <lastdiff>;"
		}
	}
}

// ================
// RESEARCH WINDOW
// ================

window research
{
	/** @todo Remove me */
	{ } // empty properties to fix parsing problem with "image" token (behaviour or property?)

	// ==================
	// background
	// ==================

	image background
	{
		src	"background/base_bg"
	}

	cmpBaseHeader baseHeader {
	}

	rows lines {
		pos			"43 108"
		size		"564 568"
		color1		"0 0.08 0 1"
		color2		"0 0.16 0 1"
		lineheight	25
	}

	// ======================
	// Techlist Panel (left)
	// ======================

	panel techlist {
		pos			"29 93"
		size		"587 597"
		background		"ui/panel_grey_green_large2"
	}

	// ==================
	// list
	// ==================
	// buttons_small - research icons
	//
	// under research: 	texl	"32 0"	texh	"48 16"
	// paused: 		texl	"48 0"	texh	"64 16"
	// unknown: 		texl	"32 16"	texh	"48 32"
	// unresearchable: 	texl	"48 16"	texh	"64 32"
	// ==================

	/**
	 * @param[in] <1> id of the research
	 * @param[in] <2> quantity of the item in the base
	 * @param[in] <3> min value allowed
	 * @param[in] <4> max value allowed
	 */
	confunc research_updateitem {
		*node:root.item<1>.bt_rs_change@current = <2>
		*node:root.item<1>.bt_rs_change@max = <3>
	}

	/**
	 * @brief Functions to hide a single entry in the list (icons only right now)
	 * @param[in] <1> Item row
	 */
	confunc research_hide {
		*node:root.item<1>.txt_item@invis = true
		*node:root.item<1>.img_status@invis = true
		*node:root.item<1>.bt_rs_change@invis = true
		*node:root.item<1>.txt_assigned@invis = true
		*node:root.item<1>.txt_rsstatus@invis = true
	}

	confunc research_research {
		*node:root.item<1>.txt_item@invis = false
		*node:root.item<1>.txt_item@left = 20
		*node:root.item<1>.txt_item@color = "0 .78 0 1"
		*node:root.item<1>.img_status@invis = false
		*node:root.item<1>.bt_rs_change@invis = false
		*node:root.item<1>.txt_assigned@invis = false
		*node:root.item<1>.txt_rsstatus@invis = false
	}

	confunc research_outterresearch {
		*node:root.item<1>.txt_item@invis = false
		*node:root.item<1>.txt_item@left = 20
		*node:root.item<1>.txt_item@color = "0 .78 0 1"
		*node:root.item<1>.img_status@invis = false
		*node:root.item<1>.bt_rs_change@invis = true
		*node:root.item<1>.txt_assigned@invis = false
		*node:root.item<1>.txt_rsstatus@invis = false
	}

	confunc research_basetitle {
		*node:root.item<1>.txt_item@invis = false
		*node:root.item<1>.txt_item@left = 0
		*node:root.item<1>.txt_item@color = "1 1 1 1"
		*node:root.item<1>.img_status@invis = true
		*node:root.item<1>.bt_rs_change@invis = true
		*node:root.item<1>.txt_assigned@invis = true
		*node:root.item<1>.txt_rsstatus@invis = true
	}

	confunc research_baseinfo {
		*node:root.item<1>.txt_item@invis = false
		*node:root.item<1>.txt_item@color = "0.7 0.7 0.7 1"
		*node:root.item<1>.txt_item@left = 20
		*node:root.item<1>.img_status@invis = true
		*node:root.item<1>.bt_rs_change@invis = true
		*node:root.item<1>.txt_assigned@invis = false
		*node:root.item<1>.txt_rsstatus@invis = true
	}

	confunc research_unresearchableitemtitle {
		*node:root.item<1>.txt_item@invis = false
		*node:root.item<1>.txt_item@color = "1 1 1 1"
		*node:root.item<1>.txt_item@left = 0
		*node:root.item<1>.img_status@invis = true
		*node:root.item<1>.bt_rs_change@invis = true
		*node:root.item<1>.txt_assigned@invis = true
		*node:root.item<1>.txt_rsstatus@invis = true
	}

	confunc research_unresearchableitem {
		*node:root.item<1>.txt_item@invis = false
		*node:root.item<1>.txt_item@color = "0 .78 0 1"
		*node:root.item<1>.txt_item@left = 20
		*node:root.item<1>.img_status@invis = true
		*node:root.item<1>.bt_rs_change@invis = true
		*node:root.item<1>.txt_assigned@invis = true
		*node:root.item<1>.txt_rsstatus@invis = true
	}

	/**
	 * @brief Functions to make a single entry visible again in the list (icons only right now)
	 * @param[in] <1> Item row
	 */
	confunc research_show {
		*node:root.item<1>.img_status@invis = false
		*node:root.item<1>.bt_rs_change@invis = false
	}

	/**
	 * @param[in] <1> Item row
	 */
	confunc research_selected {
		*node:root.item<1>.txt_item@color = ".3 1 0 1"
	}

	/**
	 * @param[in] <1> Item row
	 */
	confunc research_changebasetooltype {
		*node:root.item<1>.txt_item@tooltip = "_Switch to another base"
	}

	/**
	 * @param[in] <1> Item row
	 */
	confunc research_normal {
		*node:root.item<1>.img_status@icon = "icons/bordered_magnifying_glass"
	}

	/**
	 * @param[in] <1> Item row
	 */
	confunc research_running {
		*node:root.item<1>.img_status@icon	= "icons/bordered_magnifying_glass"
	}

	/**
	 * @param[in] <1> Item row
	 */
	confunc research_paused {
		*node:root.item<1>.img_status@icon	= "icons/bordered_sleep"
	}

	/**
	 * @brief Switch to another reserch base
	 * @param[in] <1> New base id
	 * @param[in] <2> Selected item row
	 */
	confunc research_changebase {
		cmd "ui_pop;ui_pop;mn_base_select <1>;ui_push research;"
		cmd "research_select <2>;"
	}

	cmpResearchItem item0 {
		pos "50 113"
	}
	cmpResearchItem item1 {
		pos	"50 163"
	}
	cmpResearchItem item2 {
		pos	"50 213"
	}
	cmpResearchItem item3 {
		pos	"50 263"
	}
	cmpResearchItem item4 {
		pos	"50 313"
	}
	cmpResearchItem item5 {
		pos	"50 363"
	}
	cmpResearchItem item6 {
		pos	"50 413"
	}
	cmpResearchItem item7 {
		pos	"50 463"
	}
	cmpResearchItem item8 {
		pos	"50 513"
	}
	cmpResearchItem item9 {
		pos	"50 563"
	}
	cmpResearchItem item10 {
		pos	"50 613"
	}

	// ===================
	// Info Panel (right)
	// ===================

	panel infopanel {
		pos			"645 93"
		size		"352 660"
		background		"ui/panel_grey_green_large"
	}

	// ===================================
	// Research infos related to the base
	// ===================================

	string str_base_info
	{
		string		"_Base Research Info"
		pos			"663 115"
		size		"317 18"
		font		"f_small_bold"
		longlines	LONGLINES_PRETTYCHOP
		contentalign	ALIGN_CC
	}

	// free labs in the selected base
	string txt_base_labs
	{
		string		*cvar:mn_research_labs
		font		"f_verysmall"
		pos			"663 135"
		size		"317 18"
		longlines	LONGLINES_PRETTYCHOP
		color		"0 .78 0 1"
		contentalign	ALIGN_CC
	}
	image labspace_img
	{
		pos			"693 152"
		src		"ui/bar_rahmen"
	}
	bar labspace_bar
	{
		pos			"703 157"
		current		*cvar:mn_current_labspace
		size		"236 6"
		color		"0 .8 0 1"
		max			*cvar:mn_max_labspace
		ghost		true
	}

	// free scientists in base
	string txt_base_scis
	{
		string		*cvar:mn_research_scis
		font		"f_verysmall"
		pos			"663 170"
		size		"317 18"
		longlines	LONGLINES_PRETTYCHOP
		color		"0 .78 0 1"
		contentalign	ALIGN_CC
	}
	image occ_scientists_img
	{
		pos			"693 187"
		src		"ui/bar_rahmen"
	}
	bar occ_scientists_bar
	{
		pos			"703 192"
		current		*cvar:mn_scientists_available
		size		"236 6"
		color		"0 .8 0 1"
		max			*cvar:mn_scientists_hired
		ghost		true
	}

	// =========================================================
	// Informations on research progress and technology details
	// =========================================================

	string txt_selected_name
	{
		string		*cvar:mn_research_selname
		pos			"663 215"
		size		"317 18"
		font		"f_small_bold"
		longlines	LONGLINES_PRETTYCHOP
		contentalign	ALIGN_CC
	}

	// research status
	string txt_selected_status
	{
		string		*cvar:mn_research_selstatus
		font		"f_verysmall"
		tooltip		*cvar:mn_research_selstatus_long
		pos			"663 235"
		size		"317 18"
		color		"0 .78 0 1"
		longlines	LONGLINES_PRETTYCHOP
		contentalign	ALIGN_CC
	}

	// research image
	image image_top
	{
		src		*cvar:mn_research_image
		pos			"663 260"
		size		"317 350"
		preventratio	true
		contentalign	ALIGN_CC
	}

	// research model
	model mdl_top
	{
		src			*cvar:mn_research_model
		angles		"0 0 90"
		pos			"663 260"
		size		"317 350"
		autoscale	true
		omega		"0 -3 0"
		rotatewithmouse	true
	}

	// the base this research item is researched in
	string txt_selected_base
	{
		string		*cvar:mn_research_selbase
		font		"f_verysmall"
		pos			"663 615"
		color		"0 .78 0 1"
		size		"317 18"
		contentalign	ALIGN_CC
	}

	// the progress of the selected research topic in percent
	string txt_selected_time
	{
		string		*cvar:mn_research_seltime
		font		"f_verysmall"
		pos			"663 635"
		color		"0 .78 0 1"
		size		"317 18"
		contentalign	ALIGN_CC
	}
	image progress_img
	{
		visiblewhen	"*cvar:mn_research_seltimebar != 0"
		pos			"693 652"
		src		"ui/bar_rahmen"
	}
	bar progress_bar
	{
		visiblewhen	"*cvar:mn_research_seltimebar != 0"
		pos			"703 657"
		current		*cvar:mn_research_seltimebar
		size		"236 6"
		color		"0 .8 0 1"
		max			100
		ghost		true
	}

	// ==================
	// buttons
	// ==================

	button bt_start
	{
		tooltip		"_Start research"
		background	"buttons/big_blue"
		icon		"icons/research_start"
		pos			"725 674"
		size		"59 55"
		onClick		{ cmd "mn_start_research;" }
	}

	button bt_stop
	{
		tooltip		"_Stop research"
		background	"buttons/big_blue"
		icon		"icons/research_stop"
		pos			"795 674"
		size		"59 55"
		onClick		{ cmd "mn_stop_research;" }
	}

	button bt_pedia
	{
		tooltip		"_Show research proposal"
		background	"buttons/big_blue"
		icon		"icons/mail"
		pos			"865 674"
		size		"59 55"
		onClick		{ cmd "mn_show_ufopedia;" }
	}

	// ==================
	// init
	// ==================
	func onWindowOpened
	{
		*cvar:i = 0
		while ( *cvar:i < 11 ) {
			*node:root.item<cvar:i>@num = <cvar:i>
			*node:root.item<cvar:i>.img_status@tooltip = *cvar:mn_researchstatus<cvar:i>
			*node:root.item<cvar:i>.txt_item@string = *cvar:mn_researchitem<cvar:i>
			*node:root.item<cvar:i>.txt_assigned@string = *cvar:mn_researchassigned<cvar:i>
			*node:root.item<cvar:i>.txt_rsstatus@string = *cvar:mn_rsstatus<cvar:i>
			*cvar:i = ( *cvar:i + 1 )
		}
		delete *cvar:i

		cmd "research_init 11;"
	}

	func onWindowClosed
	{
		// call base init function because the amount of workers, scientists and
		// so on can also change the button state in the base view
		cmd "base_init;"
		// delete research window related cvars
		if ( exists *cvar:mn_research_image ) { delete *cvar:mn_research_image }
		if ( exists *cvar:mn_research_model ) { delete *cvar:mn_research_model }
		if ( exists *cvar:mn_research_selname ) { delete *cvar:mn_research_selname }
		if ( exists *cvar:mn_research_selstatus ) { delete *cvar:mn_research_selstatus }
		if ( exists *cvar:mn_research_selstatus_long ) { delete *cvar:mn_research_selstatus_long }
		if ( exists *cvar:mn_research_selbase ) { delete *cvar:mn_research_selbase }
		if ( exists *cvar:mn_research_seltime ) { delete *cvar:mn_research_seltime }
		if ( exists *cvar:mn_research_seltimebar ) { delete *cvar:mn_research_seltimebar }
		if ( exists *cvar:mn_research_labs ) { delete *cvar:mn_research_labs }
		if ( exists *cvar:mn_current_labspace ) { delete *cvar:mn_current_labspace }
		if ( exists *cvar:mn_max_labspace ) { delete *cvar:mn_max_labspace }
		if ( exists *cvar:mn_research_scis ) { delete *cvar:mn_research_scis }
		if ( exists *cvar:mn_scientists_available ) { delete *cvar:mn_scientists_available }
		if ( exists *cvar:mn_scientists_hired ) { delete *cvar:mn_scientists_hired }
	}
}
